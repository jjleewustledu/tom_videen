#!/bin/csh
#	Module:	cv7topett6
#	Date:	May 1996
#	Author:	Tom Videen
#	Use:	Convert Version 7 ECAT image file to PETT VI image file

if ($#argv < 2 || $#argv > 3) then
	echo "Usage:  cv7topett6 infile frame [outfile]"
	echo " e.g.:  cv7topett6 p5000ho1 1"
	echo "   or   cv7topett6 p5000ho1 1 newimage"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg
set infile = $1.v
set frm = $2
if ($#argv == 3) then
    set outfile = $3.img
else
	set outfile = $1.img
endif
set rec = ${outfile}.rec
set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (! -e $infile) then
	echo "ERROR: [cv7topett6]: $infile does not exist"
	exit 1
endif

if (-e $outfile) rm $outfile
if (-e $rec) rm $rec
echo "rec ${outfile} ${now} ${uid}" > ${rec}
echo "cv7topett6 $1 $2" >> ${rec}
ident ${NPG}/bin/cv7topett6.bin | grep cv7topett6.c >> ${rec}
${NPG}/bin/cv7topett6.bin ${infile} ${frm} ${outfile} ${rec} ${now}
set endstat = $status
echo "endrec" >> ${rec}
if ($endstat != 0) echo "End of Task Status = $endstat"
