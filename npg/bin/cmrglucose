#!/bin/csh
#	Module:	cmrglucose
#	Date:	Apr 1998
#	Author:	Tom Videen
#	Use:	Create an CMRGlucose image from single frame FDG image

if ($#argv != 12) then
	echo "Usage:  cmrglucose img dta k1 k2 k3 k4 lc glucose piefactor starttime model out"
	echo "model 0 = Huang's formula: Am J Physiol 238:E69-E82, 1980"
	echo "      1 = Brooks' formula: J Nucl Med 23:538-539, 1982"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

set scan = $1
set dta = $2
set k1 = $3
set k2 = $4
set k3 = $5
set k4 = $6
set lc = $7
set gc = $8
set pie = $9
set time = $10
if ($11 != 0 && $11 != 1) then
	echo ERROR: FDG model must be 0 or 1
	exit 1
endif
set model = $11
set out = $12
set hdr = $out:r.hdr
set rec = ${out}.rec
set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (! -e $scan) then
	echo "ERROR: $scan does not exist"
	exit 1
endif
if (! -e $dta) then
	echo "ERROR: $dta does not exist"
	exit 1
endif
if (-e $rec) rm $rec

echo "rec ${out} ${now} ${uid}" > ${rec}
echo $argv >> ${rec}
ident ${NPG}/bin/cmrglucose.bin | grep cmrglucose.c >> ${rec}
echo frame time = $time sec >> ${rec}

${NPG}/bin/cmrglucose.bin ${scan} ${dta} ${k1} ${k2} ${k3} ${k4} ${lc} ${gc} ${pie} ${time} ${model} ${out} ${hdr} ${rec} ${now} ${today} ${uid}

echo "endrec" >> ${rec}
