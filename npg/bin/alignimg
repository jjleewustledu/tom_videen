#!/bin/csh
# Program:	alignimg
# Authors:	Jane Dunford-Shore
# Date:		Sep 1996
# Usage:	Align all images in a list to a specified standard

set usage	= "Usage: alignimg prm imagelist -s<num> [-u] [-r] [-m mask]"
if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv < 3) then
	echo "$usage"
	echo "	num = standard image number"
	echo "	-u  = unidirectional alignment to standard (default is bidirectional)"
	echo "	-r  = use ratios for cost function (default is least squares)"
	echo "	-m  = use specified mask for fit (must be in Analyze format)"
	exit 1
endif

# Get first 2 parameters

switch ("$argv[1]")
case -*:
	echo "Usage ERROR: prm and imagelist must precede all flags"
	echo "$usage"
	exit 1
default:
	set prm = $argv[1]
	shift argv
	set lst = $argv[1]
	shift argv
endsw

# Check that input files exist

if (! -e $prm) then
	echo "ERROR: Parameter file $prm does not exist."
	exit 1
endif
if (! -e $lst) then
	echo "ERROR: Imagelist file $lst does not exist."
	exit 1
endif 

# Set optional parameters

set std = 0	
set uni = 1
set fit = 3
set mask = ''

top:	 
	if ($#argv > 0) then
		switch ("$argv[1]")
		case -s?*:		 # e.g., "-s2" or "-s10"
			set std = `echo $argv[1] | sed 's/^-s//'`
			shift argv
			goto top
		case -s:			# e.g., "-s2" or "-s\n"
			shift argv
			if ($#argv > 0) then
				set std = $argv[1]
				shift argv
				goto top
			else
				echo "Usage ERROR: ... -s scan# ..."
				echo "$usage"
				exit 1
			endif
		case -u:
			set uni = 0
			shift argv
			goto top
		case -r:
			set fit = 1
			shift argv
			goto top
		case -m:
			shift argv
			set mask = $argv[1]
			shift argv
			goto top
		case -?*:
			echo "Usage ERROR: illegal option $argv[1]."
			echo "$usage"
			exit (1)
		endsw
	endif

${NPG}/bin/alignimg.bin $prm $lst $std $uni $fit $mask

