#!/bin/csh
# Name:		recon3dchk
# Author:	Tom Videen
# Date:		Jan 2003
# Intent:	Check files before calling recon3d

setenv TABLES /usr/local/lib/tables
setenv NRMDIR /data/recon/norm

if ($#argv != 1) then
	echo "Usage: recon3dchk filename"
	echo "e.g.,  recon3dchk m900fd1"
	exit (1)
endif

set emis    = $1
set tmp     = `ls *tr1.scn | head -1`
set tran    = ${tmp:r}
set tmp     = `ls *bl1.scn | head -1`
set blank   = ${tmp:r}
set tmp     = `ls *.nrm | head -1`
set nrmdate = `echo ${tmp:r} | cut -f2 -d_`
set norm    = $NRMDIR/normal3d_80x80_{$nrmdate}

if (! -e $emis.scn) echo "$emis.scn does not exist"
if (! -e $tran.scn) echo "$tran.scn does not exist"
if (! -e $blank.scn) echo "$blank.scn does not exist"
if (! -e ${tran}_f3datn.img) then
	if (! -e ${tran}l.scn) then
		echo "neither ${tran}_f3datn.img nor ${tran}l.scn exists"
	endif
endif
if (! -e $norm.nrm) echo "$norm.nrm does not exist"

set species = `echo $emis | cut -c1`
if ($species == 'p') then
	echo "recon3d $emis $tran $blank $nrmdate 2.086"
else
	echo "recon3d $emis $tran $blank $nrmdate 1.956"
endif
