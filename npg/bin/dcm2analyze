#!/bin/csh
#	Name:	dcm2analyze
#	Date:	Oct 2012
#	Author:	Tom Videen
#	Use:	Convert DICOM MR/CT files to 3D ANALYZE/Interfile image

if ($#argv < 3 || $#argv > 4) then
	echo "Usage:  dcm2analyze first last output_root [m/s/t]"
	echo " e.g.,  dcm2analyze 4 131 p5000_mprage"
	echo "   or,  dcm2analyze 4 72 stretch_mprage m"
	echo "   or,  dcm2analyze 4 131 p5000_fl3d s"
	echo "        (use the 'm' flag to indicate monkey scans)"
	echo "        (use the 's' flag to indicate skip slice reordering)"
	echo "        (use the 't' flag to indicate monkey in Trio)"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg
set first = $1
set last = $2
set out = $3
set flag = 0
set skip = 0
if ($#argv == 4) then
	if ($4 == 'm') then
		set flag = 1
	else if ($4 == 't') then
		set flag = 2
	else if ($4 == 's') then
		set skip = 1
	else
		echo "ERROR [dcm2analyze]: flag must be 'm', 's' or 't'"
		exit 1
	endif
endif

set hdr = dcm2analyze$$.hdr
set tmp = dcm2analyze$$.tmp

if ($first > $last) then
	echo "first file number is greater than last"
	exit 1
endif

# Check that all scan files exist
# -------------------------------
set i = $first
while ($i <= $last)
	if ($i < 10) then
		set n = "0000$i"
	else if ($i < 100) then
		set n = "000$i"
	else if ($i < 1000) then
		set n = "00$i"
	else if ($i < 10000) then
		set n = "0$i"
	else
		set n = $i
	endif
	if (! -e $n.dcm) then
		echo "Cannot find $n.dcm"
		exit 1
	endif
	@ i++
end

# Get name of first and last scan files and check DICOM format
# -----------------------------------------------------
if ($first < 10) then
	set name = "0000$first.dcm"
else if ($first < 100) then
	set name = "000$first.dcm"
else if ($first < 1000) then
	set name = "00$first.dcm"
else if ($first < 10000) then
	set name = "0$first.dcm"
else
	set name = $first.dcm
endif

if ($last < 10) then
	set name2 = "0000$last.dcm"
else if ($last < 100) then
	set name2 = "000$last.dcm"
else if ($last < 1000) then
	set name2 = "00$last.dcm"
else if ($last < 10000) then
	set name2 = "0$last.dcm"
else
	set name2 = $last.dcm
endif

${NPG}/bin/dcm_dump_element -t 0008 0033 $name $tmp >& /dev/null
if ($status == 0) then
	set type = "-t"
else
	${NPG}/bin/dcm_dump_element 0008 0033 $name $tmp >& /dev/null
	if ($status == 0) then
		set type = ""
	else
		echo "$name is not DICOM format"
		exit 1
	endif
endif
unalias rm
rm $tmp

# Sort the files by slice position (check patient position in header)
# ------------------------------------------------------------------
${NPG}/bin/dcm_dump_element $type 0018 5100 $name $tmp
set ppos = `cat $tmp`
rm $tmp
if ($skip == 0) then
	echo "Patient Position" $ppos
	if ($ppos == 'FFS') then
		${NPG}/bin/dcm_reverse_reorder $first $last
	else
		${NPG}/bin/dcm_slice_reorder $first $last
	endif
endif

# Get slice location of first and last files.
# Get the header of the first file, and convert
# -----------------------------------------------------
${NPG}/bin/dcm_dump_element $type 0020 1041 $name $tmp
set s1 = `cat $tmp`
rm $tmp
${NPG}/bin/dcm_dump_element $type 0020 1041 $name2 $tmp
set s2 = `cat $tmp`
rm $tmp
${NPG}/bin/dcm_dump_file $type $name >! $hdr
${NPG}/bin/dcm2analyze.bin $first $last $hdr $out $flag $s1 $s2
rm $hdr

# Subtract 1020 to convert to HU for CT
# -----------------------------------------------------
${NPG}/bin/dcm_dump_element $type 0008 0060 $name $tmp >& /dev/null
set modality = `cat $tmp`
rm $tmp
if ($modality == 'CT') anasub $out 1020

