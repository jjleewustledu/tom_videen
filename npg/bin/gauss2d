#!/bin/csh
#	Module:	gauss2d
#	Author:	Tom Videen
#	Date:	Dec 1998
# 	Use:	Apply 2D Gaussian filter to a PETT VI or ECAT image

if ($#argv != 2 && $#argv != 3) then
	echo "Usage:  gauss2d infile [outfile] half_freq"
	echo " e.g.:  gauss2d p2000ho1.img 0.3"
	echo "        gauss2d p2000ho1.img p2000ho1_2dg3.img 0.3"
	exit 1
endif

set PROGRAM = gauss2d
if (! $?NPG) setenv NPG /usr/local/npg

set now = `date`
set uid = `whoami`
set infile = $1
if ($#argv == 3) then
	set outfile	= $2
	set f0 = $3
else
	set f0 = $2
	set base = `basename ${infile}`
	set root = `echo ${base} | awk -F. '{print $1}'`
	set ext = `echo ${base} | awk -F. '{print $2}'`
	if (${ext} != "") then
		set ext = .${ext}
	endif
	set filterid = `echo "scale=0;${f0} * 10" | bc`
	set whole = `echo ${filterid} | awk -F. '{print $1}'`
	set fraction = `echo ${filterid} | awk -F. '{print $2}'`
	if (${fraction} == 0 || ${fraction} == 00 || ${fraction} == 000) then
		set filterid = ${whole}
	endif
	set outfile = ${root}_2dg${filterid}${ext}
endif

if (! -e $infile) then
	echo "ERROR [gauss2d]: cannot find $infile"
	exit 1
endif
if (-e $outfile) rm $outfile

# Initialize recfile

set recfile = ${outfile}.rec
if (-e ${recfile}) rm $recfile
echo "rec ${outfile} ${now} ${uid}" > ${recfile}
echo "${PROGRAM} $1 $2 $3" >> ${recfile}
ident ${NPG}/bin/${PROGRAM}.bin | grep ${PROGRAM}.c | cut -d$ -f2 >> ${recfile}

${NPG}/bin/gauss2d.bin ${infile} ${outfile} ${f0} ${recfile} ${now} ${uid}

set endstat = $status
if ($endstat == 0) then
    echo "endrec" >> ${recfile}
else if ($endstat == 3) then
    rm $recfile
else
    echo "End of Task Status = $endstat"
endif
