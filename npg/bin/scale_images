#!/bin/csh
#	Name:	scale_images
#	Author:	Tom Videen
#	Date:	Sept 2009
#	Intent:	Scale images to values for aligning water to oxygen and to meanpet

if ($#argv != 2) then
    echo "Usage:  scale_images oo ho"
    echo " e.g.,  scale_images p5000oo1 p5000ho1"
    exit 1
endif

set OO  = $1.v
set HO  = $2.v
set OO1 = ${1}_g3.v
set HO1 = ${2}_g3.v

if (! -e $HO1) then
	if (! -e $HO) then
		echo "Error [scale_images]: missing $HO"
		exit 1
	else
		gauss $HO $HO1 0.3
	endif
endif
if (! -e $OO1) then
	if (! -e $OO) then
		echo "Error [scale_images]: missing $OO"
		exit 1
	else
		gauss $OO $OO1 0.3
	endif
endif
cp $HO1 s$HO1
cp $OO1 s$OO1

set NPLANES = `imghdr s$HO1 | grep slices | cut -f1 -ds`
set MASK = `echo 2 \* $NPLANES/3 | bc`
maskslc s$HO1 tmpho.v ${MASK}to${NPLANES}
maskslc s$OO1 tmpoo.v ${MASK}to${NPLANES}

set maxHO = `imghdr tmpho.v | grep Max | cut -f2 -d= | cut -f1 -dM`
set maxOO = `imghdr tmpoo.v | grep Max | cut -f2 -d= | cut -f1 -dM`
set scaleHO = `echo 1550 / $maxHO | bc -l`
set scaleOO = `echo 1550 / $maxOO | bc -l`
echo "s$HO1    Max = $maxHO   Scaler = $scaleHO"
echo "s$OO1    Max = $maxOO   Scaler = $scaleOO"

scaleimage s$HO1 $scaleHO
scaleimage s$OO1 $scaleOO

set maxHO = `imghdr s$HO1 | grep Max | cut -f2 -d= | cut -f1 -dM`
set maxOO = `imghdr s$OO1 | grep Max | cut -f2 -d= | cut -f1 -dM`
echo "s$HO1    Max = $maxHO"
echo "s$OO1    Max = $maxOO"

/usr/bin/rm tmpho.v tmpoo.v

