#!/bin/csh
#	Name:	ising
#	Date:	Jun 2006
#	Author:	Tom Videen
#	Use:	Script to create individual imean images from a composite list
#			Uses parameter and imagelist files

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 2) then
	echo "Usage: ising prm list"
	echo " e.g.: ising my.prm my.lst"
	exit 1
endif

set prm = $1
set lst = $2
unalias rm

if (! -e $prm) then
	echo "ERROR [ising]: parameter file $prm does not exist"
	exit 1
endif

if (! -e $lst) then
	echo "ERROR [sing]: imagelist file $lst does not exist"
	exit 1
endif
split -1 $lst /tmp/im$$.lst.
set list = `ls /tmp/im$$.lst.*`
if ($#list < 1) then
	echo "ERROR [sing]: empty file $lst"
	exit 1
endif

set filterspec = `grep imgfilter: $prm | cut -d":" -f2-`
set imgfilter = `echo ${filterspec} | awk '{print $1}'`
switch (${imgfilter})
	case none:
		set suffix = ""
		breaksw
	case butterworth:
		set cutoff = `echo ${filterspec} | awk -F. '{print $2}' | awk '{print $1}'`
		set order = `echo ${filterspec} | awk '{print $3}'`
		set suffix = _${order}b${cutoff}
		breaksw
	case gauss:
    	set cutoff = `echo ${filterspec} | awk -F. '{print $2}' | awk '{print $1}'`
		set suffix = _g${cutoff}
    breaksw
	default:
		set suffix = ""
		breaksw
endsw

set species = `grep species: $prm | cut -d":" -f2-`
set tracer = `grep tracer: $prm | cut -d":" -f2-`
set mode = `grep smg_mode: $prm | cut -d":" -f2-`
set imgmode = `grep imgmode: $prm | cut -d":" -f2-`
set proc = `grep processing: $prm | cut -d":" -f2-`
set ext = `grep extension: $prm | cut -d":" -f2-`
foreach i (${list})
	set id = `cat $i | cut -f1`
	set num = `cat $i | cut -f2`
	if (${mode} == "single" && ${imgmode} == "multitracer" && ${proc} == "cmro2") then
		set smg = ${species}${id}${proc}${suffix}.${ext}
	else if (${mode} == "single" || ${mode} == "diff") then
		echo ${species}${id}${tracer}${num}${suffix}.im
		set smg = ${species}${id}${tracer}${num}${suffix}.im
	else if (${mode} == "paired") then
		set num2 = `cat $i | cut -f4`
		set smg = ${species}${id}${tracer}${num}-${num2}${suffix}.im
	else
		echo "ERROR [sing]: cannot identify $i"
		rm /tmp/im$$.lst.*
		exit 1
	endif
	echo "Processing $smg"
 	${NPG}/bin/imean $prm $i $smg
end
rm /tmp/im$$.lst.*
exit 0
