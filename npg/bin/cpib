#!/bin/csh
# Module: cpib
# Date:   Apr 2004
# Author: Tom Videen
# Use: Copy ECAT image to a new file removing patient identifiers
#      from the header (uses hdrEdit)
#      Main Header Field     Set Value
#      -----------------     -----------
#      Original File Name -> {PID}{SCAN}.v
#      Study Name         -> {PID}
#      Patient ID         -> {PID}{SCAN}
#      Patient Name       -> {PID}{SCAN}
#      Patient Sex        -> 0
#      Patient Dexterity  -> 0
#      Patient Birth Date -> 31 Dec 1969

if ($#argv != 3) then
	echo "Usage:  cpib filename PID SCAN"
	echo " e.g.,  cpib p1234_567_8b12_de3.v pb1000 bta1"
	exit 1
endif

set ORG  = $1
set PID  = $2
set SCAN = $3

set image = ${PID}${SCAN}
set fname = $image.v

if (! -e $ORG) then
	echo "ERROR [cpib]: cannot find $ORG"
	exit 1
endif

if (-e $fname) then
	echo "ERROR [cpib]: $fname exists"
	exit 1
else
	cp $ORG $fname
endif
chmod u+w $fname
set tmp = cpib.tmp

echo "e" >! $tmp
echo "file" >> $tmp
echo $image.v >> $tmp
echo "study_name" >> $tmp
echo $PID >> $tmp
echo "patient_id" >> $tmp
echo $image >> $tmp
echo "patient_name" >> $tmp
echo $image >> $tmp
echo "patient_sex" >> $tmp
echo 0 >> $tmp
echo "patient_dext" >> $tmp
echo 0 >> $tmp
echo "patient_age" >> $tmp
echo 0 >> $tmp
echo "patient_birth_da" >> $tmp
echo "31 Dec 1969" >> $tmp
echo "d" >> $tmp
echo "done" >> $tmp

hdrEdit -c -f $fname < $tmp >! /dev/null
unalias rm
rm $tmp
echo "New filename is $fname"
echo " "
echo "Transfer to petsun33 using:"
echo "ftp -i 128.252.241.68"
echo "cd /data/petsun33/data8/pet_data/bta/$PID"
echo "mput ${PID}*"

