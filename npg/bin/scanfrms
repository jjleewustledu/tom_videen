#!/bin/csh
# Module: scanfrms
# Date:   Jun 2004
# Author: Tom Videen
# Use:    Read frame times from ECAT 7 sinograms
#         (uses hdrEdit)

if ($#argv != 3) then
	echo "Usage:  scanfrms file firstfrm lastfrm"
	echo " e.g.,  scanfrms /sd0/p1234_567_8b12_de3.S 1 60"
	exit 1
endif

set nname = $1:t
set out  = $nname:r".frm"
set list = $nname:r".tim"
set cmd  = scanfrms.cmd
set num  = scanfrms.num
set sta  = scanfrms.sta
set dur  = scanfrms.dur
set prompts = scanfrms.pmp
set delay = scanfrms.del
set trues = scanfrms.tru
set singles = scanfrms.sin
set rate  = scanfrms.rat

echo "Frame	Start		Time		Prompts		Delayed		Trues		CoinRate	Singles" >! $num
echo s >! $cmd
echo n >> $cmd
set i = $2
while ($i <= $3)
	echo m >> $cmd
	echo $i >> $cmd
	echo d >> $cmd
	echo "   "$i >> $num
	@ i++
end
echo q >> $cmd
hdrEdit -c -f $1 < $cmd >! $out

echo "" >! $sta
grep "^Frame Start Time" $out | cut -f2 -d: >> $sta
echo "" >! $dur
grep "^Frame Duration" $out | cut -f2 -d: >> $dur
echo "" >! $prompts
grep "^Prompts" $out | cut -f2 -d: >> $prompts
echo "" >! $delay
grep "^Delayed" $out | cut -f2 -d: >> $delay
echo "" >! $trues
grep "^Net Trues" $out | cut -f2 -d: >> $trues
echo "" >! $rate
grep "^Total Coincidence Rate" $out | cut -f2 -d: >> $rate
echo "" >! $singles
grep "^Total Avg. Corrected Singles" $out | cut -f2 -d: >> $singles
paste $num $sta $dur $prompts $delay $trues $rate $singles>! $list
cat $list
echo "Frame times saved in $list"

unalias rm
rm $cmd
rm $out
rm $num
rm $sta
rm $dur
rm $prompts
rm $delay
rm $trues
rm $rate
rm $singles
exit

