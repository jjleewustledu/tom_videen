#!/bin/csh
#	Module:	align_ct2ct
#	Author:	Tom Videen
#	Date:	Oct-99
#	Use:	Align 2 CT images from same subject.
#	Creates:
#		p5002_to_p5001_ct.air
#	Uses:
#		p5001ct.img	- reference CT
#		p5002ct.img	- CT to be aligned

if ($#argv != 4) then
    echo "Usage: align_ct2ct ct1path ct1 ct2 air"
    echo " i.e., align_ct2ct /data/wjp/data5/ich/p5001/PVC_processing p5001 p5002 p5002_to_p5001_ct"
    exit 1
endif

set DIR1 = $1
set ORG  = $2
set PID  = $3
set AIR  = $4
set STD  = ${DIR1}/${ORG}ct
set CT   = ${PID}ct
set SMSK = ${ORG}msk
set RMSK = ${PID}msk

set EFILES = "$STD.img $CT.img $STD.hdr $CT.hdr"
set STATUS = 0
foreach i ($EFILES)
	if (! -e $i) then
		echo "ERROR [align_ct2ct]: $i does not exist."
		set STATUS = 1
	endif
end
if ( $STATUS != 0) exit 1

echo "Aligning $CT to $STD ..."
setanamin $STD 0
setanamin $CT 0
set PRM = "-m 6 -p1 1 -p2 1 -t1 1000 -t2 1000 -e1 $SMSK -e2 $RMSK -x 3 -v"
echo $PRM
alignlinear $STD $CT $AIR.air $PRM >! log
tail -7 log
tail -7 log >! align_ct2ct.log
set ERROR = `grep HESSIAN log`
set CHK = `echo $ERROR | wc -w`
if ($CHK > 0) then
    echo $ERROR
    echo "Alignment may require an initialization file"
    echo $ERROR >> align_ct2ct.log
    exit 1
endif
/usr/bin/rm log

