#!/bin/csh
# Name:		recon3d
# Author:	Tom Videen
# Date:		Oct 2002
# Intent:	simplified call to recon3d_scat

setenv TABLES /usr/local/lib/tables
setenv NRMDIR /data/recon/norm

if ($#argv != 7) then
	echo "Usage: recon3d emis trans blank nrmdate pixsize firstfrm lastfrm"
	echo "e.g.,  recon3d m900fd1  m900tr1  040909bl1 040902 1.956 1 3"
	echo "  or,  recon3d p2900fd1 p2900tr1 040909bl1 040902 2.086 1 44"
	echo 
	echo "Always scatter corrects and uses current path for raw and image files."
	echo "Use the following pixel sizes:"
	echo "	Monkey = 1.956"
	echo "	Human  = 2.086 (hdr shows 0.208555 cm)"
	exit 1
endif

set dpath    = $cwd
set emis     = $1
set tran     = $2
set blank    = $3
set nrmdate  = $4
set pixsize  = $5
set firstfrm = $6
set lastfrm  = $7
unalias rm

set imgdim = 80
set imgdim_fctr = `expr $imgdim / 40`
set norm = $NRMDIR/normal3d_${imgdim}x${imgdim}_{$nrmdate}

if ($firstfrm > $lastfrm) then
	echo "ERROR [recon3d]: $firstfrm > $lastfrm"
	exit 1
endif

if (! -e ${emis}.scn) then
	echo "ERROR [recon3d]: ${emis}.scn missing"
	exit 1
else if (! -e ${tran}.scn) then
	echo "ERROR [recon3d]: ${tran}.scn missing"
	exit 1
else if (! -e ${tran}_f3datn.img) then
	if (! -e ${tran}l.scn) then
		echo "ERROR [recon3d]: neither ${tran}_f3datn.img nor ${tran}l.scn exists"
		exit 1
	endif
else if (! -e ${blank}.scn) then
	echo "ERROR [recon3d]: ${blank}.scn missing"
	exit 1
endif
if (-e ${emis}.img) then
	echo "Replacing existing file ${emis}.img"
	rm -f ${emis}.img
endif
if (-e ${emis}.rec) rm -f ${emis}.rec

# If normalization files don't exist, create them.

if (! -e ${norm}.nrm) then
	makenorm_f3d $nrmdate $imgdim_fctr
	set file = ${norm:t}
	mv ${file}.nrm ${norm}.nrm
endif

# Determine number of frames to reconstruct

set frames = `cti_getparam ${emis}.scn -nframe`
if ($lastfrm > $frames) set lastfrm = $frames
set frames = `expr $lastfrm - $firstfrm + 1`

set ifrm = $firstfrm
set ofrm = 1
while ($ifrm <= $lastfrm)
	echo "--------------------------------------------------------------"
	echo Reconstructing Frame $ifrm
 	rec3dscat $dpath $emis $norm $tran $blank -f $ifrm -fo $ofrm \
		-scat -if $imgdim_fctr -num_frames $frames -p $pixsize
	@ ofrm++
	@ ifrm++
end

#nframes ${emis}.img $frames
