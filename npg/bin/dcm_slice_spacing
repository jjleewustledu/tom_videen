#!/bin/csh
#	Name:	dcm_slice_spacing
#	Date:	Nov 2009
#	Author:	Tom Videen
#	Use:	Check slice spacing for a subject

if ($#argv != 2) then
	echo "Usage:  dcm_slice_spacing first last"
	echo " e.g.,  dcm_slice_spacing 2 100"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

set m = $1
if ($m < 10) then
	set first = 0000$m.dcm
else if ($m < 100) then
	set first = 000$m.dcm
else if ($m < 1000) then
	set first = 00$m.dcm
endif
if (! -e $first) then
	echo "ERROR [dcm_slice_spacing]: cannot find $first"
	exit 1
endif

set n = $2
if ($n < 10) then
	set last = 0000$n.dcm
else if ($n < 100) then
	set last = 000$n.dcm
else if ($n < 1000) then
	set last = 00$n.dcm
endif
if (! -e $last) then
	echo "ERROR [dcm_slice_spacing]: cannot find $last"
	exit 1
endif

set tmp = dcm_slice_spacing$$.tmp

# Check DICOM format

${NPG}/bin/dcm_dump_element -t 0008 0033 $first $tmp >& /dev/null
if ($status == 0) then
	set type = "-t"
else 
	${NPG}/bin/dcm_dump_element 0008 0033 $first $tmp >& /dev/null
	if ($status == 0) then
		set type = ""
	else
		echo "$first is not DICOM format"
		/usr/bin/rm $tmp
		exit 1
	endif
endif

${NPG}/bin/dcm_dump_element $type 0020 1041 $first $tmp >& /dev/null
set z1 = `cat $tmp`
${NPG}/bin/dcm_dump_element $type 0020 1041 $last $tmp >& /dev/null
set z2 = `cat $tmp`
set slices = `echo $n - $m | bc`
set mm = `echo $z2 - $z1 | bc -l`
set space = `echo $mm / $slices | bc -l | cut -c1-7`

echo "Spacing = $space mm -- slice $m ($z1) to slice $n ($z2)"
/usr/bin/rm $tmp
