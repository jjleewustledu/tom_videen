#!/bin/csh
#	Module:	contact_dist_fid
#	Author:	Tom Videen
#	Date:	Feb 2009
#	Use:	Compute vector distance between contact points in fiducial atlas image (1x1x1 mm)

if ($#argv != 1) then
	echo "Usage:  contact_dist_fid dbid"
	echo " e.g.,  contact_dist_fid db123"
	exit 1
endif

set DB = $1
set LEFT = ${DB}_left_atlas_contacts.loc
set RIGHT = ${DB}_right_atlas_contacts.loc

if (! -e $LEFT) then
	echo "ERROR [contact_dist_fid]: missing $LEFT"
	exit 1
endif
if (! -e $RIGHT) then
	echo "ERROR [contact_dist_fid]: missing $RIGHT"
	exit 1
endif

set x0 = `head -2 $LEFT | tail -1 | cut -f2 -d' '`
set y0 = `head -2 $LEFT | tail -1 | cut -f3 -d' '`
set z0 = `head -2 $LEFT | tail -1 | cut -f4 -d' '`

set x3 = `tail -1 $LEFT | cut -f2 -d' '`
set y3 = `tail -1 $LEFT | cut -f3 -d' '`
set z3 = `tail -1 $LEFT | cut -f4 -d' '`

set xs = `echo "($x0 - $x3) * ($x0 - $x3)" | bc -l`
set ys = `echo "($y0 - $y3) * ($y0 - $y3)" | bc -l`
set zs = `echo "($z0 - $z3) * ($z0 - $z3)" | bc -l`

set ldist = `echo "sqrt($xs + $ys + $zs)" | bc -l | cut -c1-4`

set x0 = `head -2 $RIGHT | tail -1 | cut -f2 -d' '`
set y0 = `head -2 $RIGHT | tail -1 | cut -f3 -d' '`
set z0 = `head -2 $RIGHT | tail -1 | cut -f4 -d' '`

set x3 = `tail -1 $RIGHT | cut -f2 -d' '`
set y3 = `tail -1 $RIGHT | cut -f3 -d' '`
set z3 = `tail -1 $RIGHT | cut -f4 -d' '`

set xs = `echo "($x0 - $x3) * ($x0 - $x3)" | bc -l`
set ys = `echo "($y0 - $y3) * ($y0 - $y3)" | bc -l`
set zs = `echo "($z0 - $z3) * ($z0 - $z3)" | bc -l`

set rdist = `echo "sqrt($xs + $ys + $zs)" | bc -l | cut -c1-4`

echo "  Left  = " $ldist
echo "  Right = " $rdist

