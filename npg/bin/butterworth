#!/bin/csh
#	Module:	butterworth
#	Author:	Tom Videen
#	Date:	Dec 1995
# 	Use:	Apply 3D Butterworth filter to a PETT VI or ECAT image

if ($#argv != 3 && $#argv != 4) then
	echo "Usage:  butterworth infile [outfile] half_freq order"
	echo " e.g.:  butterworth p2000ho1.img 0.5 5"
	echo "        butterworth p2000ho1.img p2000ho1_g2.img 0.5 5"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg
set now = `date`
set uid = `whoami`
set infile = $1
if ($#argv == 4) then
	set outfile	= $2
	set f0 = $3
	set order = $4
else
	set f0 = $2
	set order = $3
	set base = `basename ${infile}`
	set root = `echo ${base} | awk -F. '{print $1}'`
	set ext = `echo ${base} | awk -F. '{print $2}'`
	if (${ext} != "") then
		set ext = .${ext}
	endif
	set filterid = `echo "scale=0;${f0} * 10" | bc`
	set whole = `echo ${filterid} | awk -F. '{print $1}'`
	set fraction = `echo ${filterid} | awk -F. '{print $2}'`
	if (${fraction} == 0 || ${fraction} == 00 || ${fraction} == 000) then
		set filterid = ${whole}
	endif
	set outfile = ${root}_${order}b${filterid}${ext}
endif

if (! -e ${infile}) then
	echo "ERROR [butterworth]: ${infile} does not exist."
	exit 1
endif
if (-e ${outfile}) rm $outfile

# Initialize recfile

set recfile = ${outfile}.rec
if (-e ${recfile}) then
    rm $recfile
endif
echo "rec ${outfile} ${now} ${uid}" > ${recfile}
echo "butterworth $1 $2 $3 $4" >> ${recfile}
ident ${NPG}/bin/butterworth.bin | grep butterworth.c | cut -d$ -f2 >> ${recfile}

${NPG}/bin/butterworth.bin ${infile} ${outfile} ${f0} ${order} ${recfile} ${now} ${uid}

set endstat = $status
if ($endstat == 0) then
    echo "endrec" >> ${recfile}
else if ($endstat == 3) then
    rm $recfile
else
    echo "End of Task Status = $endstat"
endif
