#!/bin/csh
#	Name:	voicoss_oo
#	Author:	Tom Videen
#	Date:	Mar 2008
#	Created for: COSS PET Processing
#	Intent: Measure VOI activity and compute left/right ratio

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 5) then
	echo "Usage:  voicoss site subject oo voi logfile"
	echo " e.g.,  voicoss 01 001 oo1 t88newmca.voi proc.log"
	exit 1
endif

set SITE  = $1
set SUBJ  = $2
set OO    = $3
set VOI   = $4
set LOG   = $5

set VER   = 1.0
set DIAM  = 19.0
set GAUSS = _g3
set ext   = t88

set PID = cs$SITE-$SUBJ-
set OO1 = $PID$OO$GAUSS.$ext
set RGT = $PID$OO-mca.rgt

if (! -e $OO1) then
	echo "ERROR [voicoss]: $OO1 not found"
	exit 1
else if (! -e $VOI) then
	echo "ERROR [voicoss]: $VOI not found"
	exit 1
endif

echo "==========================================================================" >>! $LOG
echo "voicoss  Version:" $VER `whoami` `date` >> $LOG
echo $0 $1 $2 $3 $4 $5 >> $LOG

${NPG}/bin/voicoss_ho.bin $OO1 $VOI $RGT $DIAM `date` `whoami`
echo
echo "VOI Diameter: $DIAM mm" | tee -a $LOG
echo "MCA VOI File: $VOI" | tee -a $LOG
echo "RGT File:     $RGT" | tee -a $LOG
echo
cat $RGT | tee -a $LOG

