#!/bin/csh
#	Name:	mrscans
#	Date:	Jan 2002
#	Author:	Tom Videen
#	Use:	Get basic info on multiple DICOM MRI images 

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 1) then
	echo "Usage:  mrscans <root>"
	echo " e.g.,  mrscans MR"
	echo "  or,   mrscans .MR"
	exit 1
endif

unalias rm
set in = $1
set hdr = mrscans$$.hdr
set tmp = mrscans$$.tmp
set Log = Scans.lst
touch $Log

set prefix = `echo $in | cut -c1`
if ($prefix == '.') then
	set n = 4
	set files = `ls -1 .MR..* | cut -c1-7 | uniq | sort -n -t . -k $n,$n`
else
	set n = 3
	set files = `ls -1 MR..* | cut -c1-6 | uniq | sort -n -t . -k $n,$n`
endif

# Check DICOM format
set first = `ls -1 $files[1]* | sort -n -t . -k $n,$n | head -1`
${NPG}/bin/dcm_dump_element -t 0008 0032 $first $tmp >& /dev/null
if ($status == 0) then
	set type = "-t"
else
	${NPG}/bin/dcm_dump_element 0008 0032 $first $tmp >& /dev/null
	if ($status == 0) then
		set type = ""
	else
		echo "$first is not DICOM format"
		exit 1
	endif
endif

foreach i ($files)
	set num = `ls -1 $i* | wc -l`
	set first = `ls -1 $i* | sort -n -t . -k $n,$n | head -1`
	set last = `ls -1 $i* | sort -n -t . -k $n,$n | tail -1`
	${NPG}/bin/dcm_dump_file $type $first >! $hdr
	${NPG}/bin/dcmscans.bin $first $num $hdr $last >> $Log
end
rm $hdr
rm $tmp
cat $Log
echo saved in $Log
