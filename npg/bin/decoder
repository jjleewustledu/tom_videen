#!/bin/csh
#	Module:	decoder
#	Date:	Dec 1999
#	Author:	Tom Videen
#	Use:	Decode old PETT VI image files encoded with ENCODE1

if ($#argv < 1 || $#argv > 2) then
	echo "Usage:  decoder input [output]"
	echo " e.g.:  decoder P100HO1.CMG"
	echo "   or:  decoder P100HO1.C p100ho1"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

set in = $1
if ($#argv == 2) then
	set out = $2
else
	set name = `echo $in | tr "A-Z" "a-z"`
	set root = $name:r
	set ext = $name:e
	if ($ext == 'c') then
		set out = $root
	else if ($ext == 'cmg') then
		set out = $root.img
	else if ($ext == 'csg') then
		set out = $root.smg
	else
		echo "Unrecognized extension for encoded file: $ext"
		exit 1
	endif
endif

if (! -e $in) then
	echo "ERROR: Missing input file $in"
	exit 1
endif
if (-e $out) then
	echo "ERROR: Output file exists $out"
	exit 1
endif

${NPG}/bin/decoder.bin $in $out
