#!/bin/csh
#	Name:	singlefrm_gauss
#	Author:	Tom Videen
#	Date:	May 2005

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 5) then
	echo "Usage: singlefrm_gauss image frames cutoff output_tracer extension"
	echo " e.g., singlefrm_gauss /data/p6000fdg1.v 3to44 0.3 fd v"
	exit 1
endif

unalias rm
set IN	  = $1
set NAME  = $IN:t
set FNAME = $NAME:r
set SPEC  = `echo $FNAME | nawk '{FS="[0123456789]+";print $1}'`
set PID   = `echo $FNAME | nawk '{FS="[a-z]*";print $2}'`
set ROOT  = $SPEC$PID
set FIRST = `echo $2 | nawk '{FS="to";print $1}'`
set LAST  = `echo $2 | nawk '{FS="to";print $2}'`
set CUTOFF = $3
set GF    = `echo "$CUTOFF * 10; quit" | bc -l | cut -f1 -d.`
set OROOT = $ROOT$4
set ANAM  = a$OROOT
set EX    = $5
set EXT   = .$5
set OEXT  = _g$GF$EXT
set AEXT  = _g$GF

if (! -e $IN) then
	echo "ERROR [singlefrm_gauss]: $IN does not exist."
	exit 1
endif

set i = $FIRST
while ($i <= $LAST)
	set ONAM = $OROOT
	set IMG  = $ONAM$i$EXT
	set FIMG = $ONAM$i$OEXT
	set AIMG = $ANAM$i$AEXT
	if ((! -e $AIMG.img) || (! -e $AIMG.hdr)) then
		if (! -e $FIMG) then
			if (! -e $IMG) then
				echo -- separate_dynframes $IN $ONAM $EX $i,$i
				${NPG}/bin/separate_dynframes $IN $ONAM $EX $i,$i
			endif
			echo -- gauss $IMG $FIMG $CUTOFF
			${NPG}/bin/gauss $IMG $FIMG $CUTOFF
		endif
		echo -- img2analyze $FIMG $AIMG
		${NPG}/bin/img2analyze $FIMG $AIMG
		${NPG}/bin/setanamin $AIMG 0
		rm $AIMG.ifh
		if (-e $AIMG.lkup) rm $AIMG.lkup
	else
		echo "-- Single-Frame Analyze file exists:   $AIMG.img"
	endif
	@ i++
end

