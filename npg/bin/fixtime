#!/bin/csh
# Module: fixtime
# Date:   Jan 2003
# Author: Tom Videen
# Use:  Correct frame duration and adjust scale factor in dynamic frames
#       after running fixsingles on ECAT sinogram and reconstructing.
#       (uses hdrEdit)
# Note: On hrcon, must skip the "/home/ecat7/capp" returned after command on on "set first" line:
#       hrcon:  head -7
#       others: head -6

if ($#argv != 1) then
	echo "Usage:  fixtime imagefile"
	echo " e.g.,  fixtime p1234_567_8b12_de3.v"
	exit 1
endif

set file = $1
set nname = $file:t
set out = /tmp/$nname:r".dump"
set cmd = /tmp/$nname:r".cmd"

unalias rm
imgfrms $file | cut -c1-3 >! junk
set first = `head -7 junk | tail -1`
set last  = `tail -1 junk`
rm junk

echo s >! $cmd
echo n >> $cmd
set i = $first
while ($i <= $last)
	echo m >> $cmd
	echo $i >> $cmd
	echo d >> $cmd
	@ i++
end
echo q >> $cmd
hdrEdit -c -f $file < $cmd >&! $out

fixtime.awk $out >! $cmd

hdrEdit -c -f $file < $cmd >& /dev/null 

set len =  `wc -l $cmd | cut -c1-8`
if ($len > 3) then
	echo "Frame Durations restored in $file"
else
	echo "No frame errors found in $file"
#	rm $cmd
endif
rm $out
exit

