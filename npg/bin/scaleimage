#!/bin/csh
#	Name:	scaleimage
#	Author:	Tom Videen
#	Date:	May 1996
# 	Use:	Change scaling factor in ECAT image files.

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 2) then
	echo "Usage:  scaleimage filename factor"
	echo " e.g.:  scaleimage p2000ho1.img 0.01"
	exit 1
endif

set filename = $1
set factor = $2
set tmpfile = ${filename}.tmp
set recfile = ${filename}.tmp.rec
set now = `date`
set uid = `whoami`

if (! -e $filename) then
	echo "ERROR: cannot find $filename"
	exit 1
endif

echo "rec ${filename} ${now} ${uid}" > ${recfile}
echo "scaleimage $1 $2" >> ${recfile}
ident ${NPG}/bin/scaleimage.bin | grep scaleimage.c | cut -d$ -f2 >> ${recfile}
if (-e ${filename}.rec) then
	echo ${filename}.rec
	cat ${filename}.rec >> ${recfile}
endif

cp ${filename} ${tmpfile}
${NPG}/bin/scaleimage.bin ${tmpfile} ${factor} `date` `whoami`

set endstat = $status
if ($endstat == 0) then
	echo "endrec" >> ${recfile}
	unalias mv
	mv ${tmpfile} ${filename}
	mv ${recfile} ${filename}.rec
else
	unalias rm
	rm ${tmpfile}
	rm ${recfile}
endif
