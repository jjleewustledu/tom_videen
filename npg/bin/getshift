#!/bin/csh
#	Program: getshift
#	Author:	 Tom Videen
#	Date:	 Apr 2003
#	Intent:	 Compare dta with dcv files to compute shifts applied in dta file

if ($#argv != 1) then
    echo "Usage: getshift pid"
    echo " e.g.  getshift p5000"
    exit
endif

set PID = $1
set DTA = $PID.dta

if (! -e $DTA) then
    echo "$DTA does not exist"
    exit
endif

set scans = `grep '^[123]' $DTA | cut -c3-`
set start = `grep "       1.0  " $DTA | cut -c11-`

set i = 1
foreach scn ($scans)
	set DCV = $PID$scn.dcv 
	if (! -e $DCV) then
		echo "Cannot find $DCV"
		exit
	endif
	set time = $start[$i]
	set starttime = `grep $time $DCV | cut -f1 -d.`
	set shift = `echo  $starttime - 1 | bc`
	echo "$PID$scn  -$shift"
	@ i++
end

