#!/bin/csh
#	Name:	t88mean
#	Date:	Oct 1993
#	Author:	Tom Videen
#	Use:	Create a stereotaxic-reformatted image to Talairach 1988 atlas brain
#			Uses parameter and imagelist files

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 3) then
	echo "Usage:  t88mean prm imglist image"
	echo " e.g.:  t88mean my.prm my.lst output.t88"
	exit 1
endif

set prm = $1
set lst = $2
set smg = $3
set rec = ${smg}.rec
set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (! -e $prm) then
	echo "ERROR: cannot find $prm"
	exit 1
endif

if (! -e $lst) then
	echo "ERROR: cannot find $lst"
	exit 1
endif

unalias rm
if (-e $rec) rm $rec
echo "rec ${smg} ${now} ${uid}" > ${rec}
echo "t88mean $1 $2 $3" >> ${rec}
ident ${NPG}/bin/t88mean.bin | grep t88mean.c >> ${rec}
echo "prm" >> ${rec}
cat ${prm} >> ${rec}
echo "endprm" >> ${rec}
echo "imglst" >> ${rec}
cat ${lst} >> ${rec}
echo "endimglst" >> ${rec}

${NPG}/bin/t88mean.bin ${prm} ${lst} ${smg} ${rec} ${now} ${today} ${uid}
echo "endrec" >> ${rec}
