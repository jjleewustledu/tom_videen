#!/bin/csh
#   Module: total_shift_oxy2
#   Author: Tom Videen
#   Date:   Jun 1999
#   Use:    Compare total shift of PET images (OC2->OC1->HO1) 
#	rcsid = $Id: total_shift_oxy2,v 1.4 2003/12/17 14:45:02 tom Exp $

if ($#argv != 5) then
    echo "Usage:  total_shift_oxy2 pid oc2 oc1 ho1 mask"
	echo " i.e.,  total_shift_oxy2 p5000 2 1 1 p5000mask.v"
	echo " (assumes aligned images rp5000oo1_g3.v, etc.)"
    exit 1
endif

set PID = $1
set C2  = oc$2
set C1  = oc$3
set H1  = ho$4
set MASK = $5

set RID   = r$PID
set AIR2  = ${RID}${C2}_to_${C1}.air
set AIR1  = ${PID}${C1}_to_${H1}.air
set AIR21 = ${RID}${C2}_to_${H1}.air

# Check existence of files:

set EFILES = "$MASK $AIR1 $AIR2"
set STATUS = 0
foreach i ($EFILES)
	if (! -e $i) then
		echo "ERROR [total_shift_oxy2]: $i does not exist."
		set STATUS = 1
	endif
end
if ( $STATUS != 0) exit 1

# Setup log file:

set BINFILE = `which total_shift_oxy2`
set VER = `grep rcsid $BINFILE | cut -f2- -d: | cut -f1 -dE | head -1`
set LOGFILE = ${RID}_align.log
if (-e $LOGFILE) then
	echo "" >> $LOGFILE
else
	touch $LOGFILE
endif
echo "$PID total shift:  " `whoami` `date` >> $LOGFILE
echo "Version: " $VER         >> $LOGFILE
echo ""                       >> $LOGFILE
combine_air $AIR21 y $AIR2 $AIR1
airdif1 $AIR21 null.air $MASK >> $LOGFILE
echo "Complete [total_shift_oxy2] =================="
