#!/bin/csh
#	Module:	reslice_S_t4
#	Date:	May 2009
#	Author:	Tom Videen
#	Use:	Takes an Analyze image in standard orientation (Right->Left, Post->Ant, Inf->Sup)
#			and reslices it to 711-2B atlas with 2-mm voxels using an existing t4 transformation matrix.
#			Assumes the original transform was based on a sagittally oriented MRI.

if ($#argv != 2) then
	echo "Usage:  reslice_S_t4 filename S_t4_transform"
	echo " e.g.:  reslice_S_t4 p7000_tis p7000_mprage1_to_711-2B_t4"
	exit 1
endif

if (! $?AZS) setenv AZS /data/petsun4/data1/solaris
set in    = $1
set trans = $2
set out   = r$in
set inS   = ${in}S

checkfiles $in.hdr $in.img $trans
if ($status != 0) exit 1

check_absent_files $in.4dfp.img $inS.4dfp.img $out.4dint.img $out.img
if ($status != 0) then
	echo "Existing files must be removed to prevent unintentional replacement or removal."
	exit 1
endif

${AZS}/analyzeto4dfp $in
${AZS}/T2S_4dfp $in
${AZS}/t4img_4dfp $trans $inS $out -O222
${AZS}/4dfptoanalyze $out

unalias rm
unalias mv
mv $out.4dint.hdr $out.hdr
mv $out.4dint.img $out.img
mv $out.4dint.img.rec $out.img.rec
rm $in.4dfp.* $inS.4dfp.* $out.4dfp.*

