#!/bin/csh
#	Module:	align_ct2tr
#	Author:	Tom Videen
#	Date:	Nov-99
#	Use:	Align CT image to PET transmission image.
#	Creates:
#		rp5000ct.v		- coregistered CT
#	Uses:
#		p5000ct.img		- original CT
#		p5000tr1_2dg5.v	- filtered transmission

if ($#argv != 2) then
    echo "Usage: align_ct2tr id tr"
    echo " e.g., align_ct2tr p5000 tr1"
    exit 1
endif

set PID = $1
set TR  = $2
set CT  = ${PID}ct
set PET  = $PID${TR}_2dg5
set AIR = ${PID}_ct2$TR
set LOGFILE = align_ct2$TR.log
set INIT = air_init

set EFILES = "$CT.img $PET.v $CT.hdr"
set STATUS = 0
foreach i ($EFILES)
	if (! -e $i) then
		echo "ERROR [align_ct2tr]: $i does not exist."
		set STATUS = 1
	endif
end
if ( $STATUS != 0) exit 1

img2analyze $PET.v a$PET
setanamin a$PET 0
setanamin $CT 0
set PRM = "-m 6 -p2 16384 -p1 0 -t2 100 -x 1 -v" 
alignlinear a$PET $CT $AIR.air $PRM >! log
tail -6 log >! $LOGFILE
set ERROR = `grep HESSIAN log`
set CHK = `echo $ERROR | wc -w`
if ($CHK > 0) then
	echo $ERROR
	echo "Alignment may require an initialization file ($INIT)"
	echo $ERROR >> $LOGFILE
	exit 1
endif
imgreslice $CT $AIR
analyze2img r$CT r$CT.v
if (-e r$CT.v) echo "Created r$CT.v"
rm a$PET.img
rm a$PET.hdr
rm a$PET.ifh
rm log

