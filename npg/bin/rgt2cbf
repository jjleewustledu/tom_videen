#!/bin/csh
#   Module: rgt2cbf
#   Author: Tom Videen
#   Date:   Dec 2007
#   Use:    Read rgt file of HO counts and convert to CBF

if ($#argv < 2) then
    echo "Usage:  rgt2cbf rgt out scan1 scan2 ..."
    echo " e.g.,  rgt2cbf sub1_hocnts.rgt sub1_cbf.dat sub1_ho1 sub1_ho2"
    exit 1
endif

set RGT  = $1
set DAT  = $2

if (! -e $RGT) then
	echo "ERROR [rgt2cbf]: $RGT does not exist."
	exit
endif

shift argv
shift argv
if ($#argv <= 0) then
	echo "Usage:  rgt2cbf rgt out scan1 [scan2 ...]"
	echo "ERROR:  no scans listed"
	exit
endif

set rois = `head -2 $RGT | tail -1 | cut -c14-`
set nroi = `echo $rois | wc -w`
echo $rois file >! $DAT

while ($#argv > 0)
	set cbf = ()
	set scn = $argv[1]
	set n = `grep $scn $RGT | wc -w`
	set str = `grep $scn $RGT`
	set fnam = `echo $str | cut -f$n -d' '`
	set i = 1
	while ($i <= $nroi)
		set roi = $rois[$i]
		set num = `echo $str | cut -f$i -d' '`
		metcalc $num $scn.hdr
		set val = `grep CBF metcalc.log | cut -f2 -d= | cut -f1 -dm`
		set cbf = ($cbf $val)
		@ i++
	end
	shift argv
	echo $cbf $fnam >>! $DAT
end
rm metcalc.log

