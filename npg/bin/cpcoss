#!/bin/csh
# Module: cpcoss
# Date:   Jan 2003
# Author: Tom Videen
# Use: Copy ECAT image to a new file removing patient identifiers
#      from the header (uses hdrEdit)
#      Main Header Field     Set Value
#      -----------------     -----------
#      Original File Name -> siteID-patientID-scanID.v
#      Study Name         -> siteID-patientID
#      Patient ID         -> siteID-patientID-scanID
#      Patient Name       -> siteID-patientID-scanID
#      Patient Birth Date -> 31 Dec 1969

if ($#argv != 4) then
	echo "Usage:  cpcoss filename siteID patientID scanID"
	echo " e.g.,  cpcoss p1234_567_8b12_de3.v cs01 001 ho1"
	exit 1
endif

set original  = $1
set siteID    = $2
set patientID = $3
set scanID    = $4

set study = ${siteID}-${patientID}
set image = ${siteID}-${patientID}-${scanID}
set fname = $image.v

if (! -e $original) then
	echo "ERROR [cpcoss]: cannot find $original"
	exit 1
endif

if (-e $fname) then
	echo "ERROR [cpcoss]: $fname exists"
	exit 1
else
	cp $original $fname
endif
chmod u+w $fname
set tmp = cpcoss.tmp

echo "e" >! $tmp
echo "file" >> $tmp
echo $image.v >> $tmp
echo "study_name" >> $tmp
echo $study >> $tmp
echo "patient_id" >> $tmp
echo $image >> $tmp
echo "patient_name" >> $tmp
echo $image >> $tmp
echo "patient_birth_da" >> $tmp
echo "31 Dec 1969" >> $tmp
echo "d" >> $tmp
echo "done" >> $tmp

hdrEdit -c -f $fname < $tmp >! /dev/null
unalias rm
rm $tmp
echo "New filename is $fname"
echo " "
echo "Transfer to coss1 using:"
echo "ftp coss1.npg.wustl.edu (128.252.244.5)"
echo "login as anonymous"
echo "cd /uploads"
echo "mput ${siteID}*"

