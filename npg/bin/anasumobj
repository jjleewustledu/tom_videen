#!/bin/csh
#	Module:	anasumobj
#	Author:	Tom Videen
#	Date:	Sep 1999
#	Use:	Add objects from 1 or more Analyze images

if ($#argv < 3) then
	echo "Usage:  anasumobj output range input1 input2 ..."
	echo " e.g.:  anasumobj rt_putamen 4to10 p5000obj p5001obj p5020obj"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg
set ohdr = $1:r.hdr
set oimg = $1:r.img
if (-e $oimg) echo "Replacing $oimg"
set bot = `echo $2 | cut -f1 -dt`
set top = `echo $2 | cut -f2 -do`

set PARMS = ""
set i = 3
while ($i <= $#argv)
	set hdr = $argv[$i]:r.hdr
	set img = $argv[$i]:r.img
	if (! -e $img) then
		echo "ERROR [anasumobj]: $img does not exist"
		exit 1
	endif
	set PARMS = "$PARMS $hdr $img"
	@ i++
end

${NPG}/bin/anasumobj.bin $ohdr $oimg $bot $top $PARMS

