#!/bin/csh
#	Module:	dcm_mrsort1
#	Author:	Tom Videen
#	Date:	Feb 2010
#	Use:	Rename files, move to common Raw directory, generate Scans.lst, and remove directories.
#	Note:	This leaves all sequences with names containing "ep2d" as is.

if ($#argv < 1) then
	echo "Usage:  dcm_mrsort1 PID [path]"
	echo " e.g.,  dcm_mrsort1 p7000"
	echo "   or,  dcm_mrsort1 p7000 MR_research"
	echo " "
	echo "Note: This will sort all MR images except those named ep2d."
	echo "      Default MR directory is mri."
	exit 1
endif

set PID = $1
set MRDIR = $PWD
if ($#argv == 2) then
	set MPATH = $2
else
	set MPATH = 'mri'
endif
if ($MRDIR:t != $MPATH) then
	echo "ERROR: dcm_mrsort1 must be run in directory named $MPATH"
	echo "       Current directory is $MRDIR"
	exit 1
endif
	
set RAWDIR = $MRDIR/Raw
if (! -e $RAWDIR) then
	mkdir $RAWDIR
	chmod 770 $RAWDIR
	jsp
endif
set EPDIR = $MRDIR/EP2D
if (! -e $EPDIR) then
	mkdir $EPDIR
	chmod 770 $EPDIR
	jsp
endif
set seq = 0
set count = 1
echo "-------------------------------------------------------------------------------------"
ls -R ? ?? 
echo "-------------------------------------------------------------------------------------"
echo "Directories should begin with a number."
echo "Lowest level folders should contain numbered filenames only"
echo "beginning with number 1 in each folder (repeated numbers)."
echo
echo "RETURN if true;  CTRL-C if false"
set data = `line`

# ------------------------------------------------------
# This Loop assumes dcm files increment across all scans
# ------------------------------------------------------
LOOP:
@ seq++
cd $MRDIR
set SDIR = `ls -R $seq | grep / | tail -1 | cut -f1 -d:`
echo $SDIR
if ($SDIR == '') goto DONE
if (`echo $SDIR | grep ep2d` != '') goto LOOP
cd $SDIR
set first = `ls | sort -n | head -1 | cut -f1 -d\*`
set last = `ls | sort -n | tail -1 | cut -f1 -d\*`
set n = `ls | wc -w`
echo "Convert $n files: $first-$last"
echo "Press RETURN to continue (CTRL-C to quit)"
set data = `line`
dcmren $first $last
dcm_renum $first $last $count
/usr/bin/mv -i *dcm $RAWDIR
set count = `echo $count + $n | bc`
echo $count
cd $MRDIR
rmdir $SDIR
rmdir $seq
goto LOOP
# ---------------------------------
DONE:
cd $RAWDIR
dcmscans 1
jsp
chmod 660 *


