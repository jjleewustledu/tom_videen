#!/bin/csh
#	Name:	research_dbs_paths
#	Author:	Tom Videen
#	Date:	Oct 2012
#	Intent:	Create subdirectories and copy command files to them
#	Created for: Processing Research MR DBS images

if ($#argv != 2) then
    echo "Usage:  research_dbs_paths dbid ctpath"
    echo " e.g.,  research_dbs_paths db600 Clin_CT"
    echo "   or,  research_dbs_paths db600 CCIR"
    exit 1
endif

set DB = $1
set MAICT = $2
set DDIR   = /data/jsp/human/DBS/$DB
if (! -e $DDIR) then
	echo "ERROR [research_dbs_paths]: missing path: $DDIR"
    exit 1
endif

set PROC   = /data/jsp/human/DBS/Processing/Research
set MRDIR  = $DDIR/MR_research
set ALDIR  = $DDIR/Align_research
set ATDIR  = $DDIR/Atlas_research
set MAIDIR = $DDIR/Mai_compute_research

set PDIRS = ($MRDIR $MRDIR $ALDIR $ATDIR $DDIR/Mai_compute $MAIDIR $MAIDIR/$MAICT)
foreach i ($PDIRS)
	echo $i
	if (! -e $i) then
		mkdir $i
		chmod 770 $i
		chgrp jsp $i
	endif
end

# Copy command files to folders

if (! -e $DDIR/Setup_Research_$DB) then
	sed s/db123/$DB/g $PROC/Setup_Research >! $DDIR/Setup_Research_$DB
endif

if (! -e $MRDIR/MR_conversion_$DB) then
	sed s/db123/$DB/g $PROC/MR_conversion >! $MRDIR/MR_conversion_$DB
endif

if (! -e $ALDIR/Align_ct2tse_$DB) then
	sed s/db123/$DB/g $PROC/Align_ct2tse >! $ALDIR/Align_ct2tse_$DB
endif

if (! -e $ALDIR/Manreslice_ct_$DB) then
    sed s/db123/$DB/g $PROC/Manreslice_ct >! $ALDIR/Manreslice_ct_$DB
endif

if (! -e $ALDIR/Align_mpr2tse_$DB) then
    cp $PROC/Align_mpr2tse $ALDIR/Align_mpr2tse_$DB
endif

if (! -e $ATDIR/Align_to_atlas_initial_$DB) then
	sed s/db123/$DB/g $PROC/Align_to_atlas_initial >! $ATDIR/Align_to_atlas_initial_$DB
endif

if ($MAICT == 'CCIR') then
	if (! -e $MAIDIR/CCIR/Reslice_to_Mai_ccir_$DB) then
		sed s/db123/$DB/g $PROC/Reslice_to_Mai_clin_ct >! $MAIDIR/CCIR/Reslice_to_Mai_ccir_$DB
	endif
endif

if ($MAICT == 'Clin_CT') then
	if (! -e $MAIDIR/Clin_CT/Reslice_to_Mai_clin_ct_$DB) then
    	sed s/db123/$DB/g $PROC/Reslice_to_Mai_clin_ct >! $MAIDIR/Clin_CT/Reslice_to_Mai_clin_ct_$DB
	endif
endif

