#!/bin/csh
#	Name:	separate_dynframes
#	Date:	Aug 2008
#	Author: Tom Videen
#	Intent:	Separate individual frames of a dynamic ECAT image.

if ($#argv < 4 || $#argv > 5) then
	echo "Usage: separate_dynframes inputfile output_root output_extension ff,lf [num]"
	echo " e.g.: separate_dynframes m500nmb1.img m500mb i 1,44"
	echo "  or,  separate_dynframes m500nmb1.img m500mb i 1,44 25"
	echo 
	echo "ff,lf = first and last frame numbers of input file"
	echo "num   = first number of output file (to renumber frames)"
	exit 1
endif

set infile = $1
set root = $2
set ext = $3
set ff = `echo $4 | awk -F, '{print $1}'`
set lf = `echo $4 | awk -F, '{print $2}'`
if ($#argv == 5) then
	set outfrm = $5
else
	set outfrm = $ff
endif

set infrm = $ff
while ($infrm <= $lf)
	set outfile = $root$outfrm.$ext
	if (-e $outfile) then
		echo "-- $outfile already exists"
	else
		singleframe $infile $outfile $infrm 
	endif
	@ infrm++
	@ outfrm++
end
