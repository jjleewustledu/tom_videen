#! /bin/csh
#	Name:	recon_2dtr
#	Author:	Tom Videen
#	Date:	Jul 1998
#	Intent:	Reconstruct a transmission scan

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 5) then
	echo "Usage: recon_2dtr image scn filter cutoff zoom"
	echo "	image   = output image (.img)"
	echo "	scn     = scan (.scn)"
	echo "	filter  = 1-6"
	echo "	cutoff <= 0.5"
	echo "	zoom    = image zoom (1.0 - )"
	echo ""
	echo "Specify all files with complete path & name but no extension."
	echo "  e.g., recon_2dtr p2000tr1 p2000tr1l 1 .5 1.875"
	exit 1
endif

setenv DEBUG 0

set imageFile = $1.img
set recFile   = ${imageFile}.rec
set scanFile  = $2.scn
set filter = $3
set cutoff = $4
set zoom   = $5
set frameNumber = 1
set gate = 1
set data = 0
set bed = 0
set xoff = 0.0
set yoff = 0.0
set rot = 0.0
set size = 128
set decayCor = 0
set debug = 0
set scatterCor = 0
set arcCor = 0
set intrinsicTilt = 1
set priority  = 0
set filt_slope = 0

if (! -e ${scanFile}) then
	echo "ERROR: cannot find ${scanFile}"
	exit 1
endif
if (`ls ${scanFile} | grep "l.scn"` != ${scanFile}) then
	echo "ERROR: recon_2dtr requires a log attenuation file"
	echo "     ${scanFile} does not end with *l.scn" 
	exit 1
endif

unalias rm
if (-e ${recFile}) rm ${recFile}
 
set plane = 1
while ( $plane <= 31 )
	set ARGS  = (${scanFile},${frameNumber},$plane,${gate},${data},${bed} \
		${size},${zoom},${xoff},${yoff},${rot},${decayCor},${scatterCor},${arcCor},${intrinsicTilt}  \
		${imageFile},${frameNumber},$plane,${gate},${data},${bed}  \
		${filter},${cutoff},${filt_slope},0,0,0)
	${NPG}/bin/bkproj_sun $ARGS
	@ plane++
end

echo "rec ${imageFile} `date` `whoami` `hostname` `uname`" > ${recFile}
echo "recon_2dtr $1 $2 $3 $4 $5" >> ${recFile}
ident ${NPG}/bin/bkproj_sun | grep bkproj_sun.c | cut -f2- -d: >> ${recFile}
echo "endrec" >> ${recFile}
exit

