#!/bin/csh
#	Module:	analyze2img
#	Date:	Feb 2003
#	Author:	Tom Videen
#	Use:	Convert ANALYZE image file to ECAT version 7 image file

if ($#argv != 2 && $#argv != 4 && $#argv != 5) then
	echo "Usage:  analyze2img ANALYZE_file PET_file [stdfile frm# [v]]"
	echo " e.g.:  analyze2img rap5000ho1 rp5000ho1.v"
	echo "  or    analyze2img rap5000ho1 rp5000ho1.v p5000ho1.v 2"
	echo "  or    analyze2img rap5000ho1 rp5000ho1.v p5000ho1.v 2 v"
	echo " "
	echo "Note: ANALYZE files must have the extensions .img and .hdr"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg
set img = $1.img
set hdr = $1.hdr
set pet = $2
if ($#argv >= 4) then
	set std = $3
	set frm = $4
	if (! -e $std) then
		echo "ERROR [analyze2img]: Standard file $std does not exist"
		exit 1
	endif
else
	set std = "NULL"
	set frm = 0
endif
if ($#argv == 5) then
	set vox = 1
else
	set vox = 0
endif

if (! -e $img) then
	echo "ERROR [analyze2img]: Input file $img does not exist"
	exit 1
endif
if (! -e $hdr) then
	echo "ERROR [analyze2img]: Input file $hdr does not exist"
	exit 1
endif
if (-e $pet) echo "Replacing: $pet"

${NPG}/bin/analyze2img.bin $img $hdr $pet $std $frm $vox `date` `whoami`
