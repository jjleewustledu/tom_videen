#!/bin/csh
#	Name:	ifdyn2img
#	Date:	Apr 2006
#	Author:	Tom Videen
#	Use:	Convert Dynamic Interfile to ECAT 7 format 

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 3) then
	echo "Usage:  ifdyn2img header image output"
	echo " e.g.,  ifdyn2img p1000mgh.hdr p1000mgh.img p1000mgh.v"
	exit 1
endif

set ifh = $1
set ifi = $2
set out = $3
set tmp = $$.img
if (! -e $ifh) then
	echo "$ifh does not exist"
	exit 1
endif
if (! -e $ifi) then
	echo "$ifi does not exist"
	exit 1
endif
if (-e $out) then
	echo "$out already exists"
	exit 1
endif
if (-e $tmp) then
	echo "$tmp already exists"
	exit 1
endif

chmod u+r $ifh $ifi

set CHK = `grep "imagedata byte order" $ifh | grep LITTLEENDIAN`
set CNT = `echo $CHK | wc -c`
if ($CNT > 10) then
	dd if=$ifi of=$tmp conv='swab'
	${NPG}/bin/ifdyn2img.bin $ifh $tmp $out
	rm $tmp
else
	${NPG}/bin/ifdyn2img.bin $ifh $ifi $out
endif

