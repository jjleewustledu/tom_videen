#!/bin/csh
#	Name:	imascans
#	Date:	Nov 2003
#	Author:	Tom Videen
#	Use:	Get basic info on multiple Siemens MRI images (ima)

if ($#argv != 1) then
	echo "Usage:  imascans mrnum"
	echo " e.g.,  imascans 2345"
	exit 1
endif

set mrnum = $1
set LOG = Scans_$mrnum

set files1 = `ls -1 ${mrnum}-?-* | cut -f1-2 -d- | uniq`
set files2 = `ls -1 ${mrnum}-??-* | cut -f1-2 -d- | uniq`
set tmp = `echo $files2 | cut -c1`
if ($tmp == '') then
	set files = ($files1)
else
	set files = ($files1 $files2)
endif
set i = $files[1]
set first = `ls -1 ${i}-* | head -1`
set today = `date +'%d %h 20%y'`
set user = `whoami`
echo $mrnum "    ("$today " - "$user")" >! $LOG
mrihdr $first | grep "Subject ID" >> $LOG
mrihdr $first | grep "Study date" >> $LOG
mrihdr $first | grep "Parameter file" >> $LOG
echo "----------------" >> $LOG

foreach i ($files)
	set first = `ls -1 ${i}-* | cut -f3 -d- | sort -n | cut -f1 -d. | head -1`
	set last = `ls -1 ${i}-* | cut -f3 -d- | sort -n | cut -f1 -d. | tail -1`
	set name1 = ${i}-${first}.ima
	set name2 = ${i}-${last}.ima
	set n = 12
	set seqname = `mrihdr $name1 | grep "Parameter file" | grep / | cut -c1`
	if ($seqname == '') then
		set seqname = 'initialized by operator'
	else
		set seqname = ''
		while ($seqname == '')
			set seqname = `mrihdr $name1 | grep "Parameter file" | cut -f$n -d/ | cut -f1 -d.`
			@ n--
		end
	endif
	echo "$name1  -  $name2    $seqname" >> $LOG
	set chk = `mrihdr $name1 | grep "TI " | cut -c1-2`
	if ($chk == 'TI') then
		mrihdr $name1 | tail -4 >> $LOG
	else
		mrihdr $name1 | tail -3 >> $LOG
	endif
	echo " " >> $LOG
end

cat $LOG
echo saved in $LOG
