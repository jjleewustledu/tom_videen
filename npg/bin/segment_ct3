#!/bin/csh
#   Name:   segment_ct3
#   Date:   Nov-99
#   Author: Tom Videen
#	Input:
#		${PID}brain_n
#		${PID}vent_n
#		${PID}clot_n	(optional)
#	Creates:
#		${PID}tis_n.img

if ($#argv < 4 || $#argv > 5) then
	echo "Usage:  segment_ct3 pid brain vent tis [clot]"
	echo " e.g.,  segment_ct3 p5132 brain_n vent_n tis_n"
	echo "   or,  segment_ct3 p5132 brain_n vent_n tis_n clot_n"
	exit
endif

set PID   = $1
set IN	  = $PID$2
set VENT  = $PID$3
set OUT   = $PID$4
set CLOT  = $PID$5

set SETVAL  = -2000
set BRAIN_MIN  = 15
set BRAIN_CSF  = 20
set BRAIN_MAX  = 70

if (! -e $IN.img) then
	echo "ERROR [segment_ct3]: image file $IN.img does not exist."
	exit
endif
if (! -e $VENT.img) then
	echo "ERROR [segment_ct3]: image file $VENT.img does not exist."
	exit
else
	if2analyze $VENT
endif
if (-e $CLOT.img) then
	if2analyze $CLOT
endif

anamask $IN $VENT $OUT 0 -1
if (-e $CLOT.img) then
	anamask $OUT $CLOT $OUT 0 -1
endif
maskor $OUT $OUT $BRAIN_MIN $BRAIN_MAX $SETVAL
maskelevlines $OUT $OUT $SETVAL 0 $SETVAL
expandor $OUT $OUT $SETVAL $SETVAL $BRAIN_CSF $BRAIN_MAX
maskand $OUT $OUT $SETVAL 0 0
anadif $OUT $VENT $OUT
if (-e $CLOT.img) then
	anadif $OUT $CLOT $OUT
endif

