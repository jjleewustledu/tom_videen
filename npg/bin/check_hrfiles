#!/bin/csh
#	Name:		check_hrfiles
#	Date:		Sep-99
#	Author:		Tom Videen
#	Function:	check data files transferred (presence and size)

if ($#argv != 1) then
	echo "Usage: check_hrfiles pid"
	echo " i.e., check_hrfiles p5000"
	exit (1)
endif

set PID = $1
set ARCPATH = /data/recon/exact/$PID
if (! -e $ARCPATH) then
	echo "ERROR [check_hrfiles]: missing path $ARCPATH"
	exit (1)
endif

cd $ARCPATH
set files = `grep $PID $PID.files | cut -f1 -d" "`

set total = 0
set num = `ls -l $ARCPATH/${PID}*? | awk '{ print $5 }'`
foreach i ($files)
	set num = `ls -l $i* | awk '{ print $5 }'`
	if ($num <= 0) then
		echo "Missing file: $i"
	else
		set count = `expr $num / 1024`
		set total = `expr $total + $count`
	endif
end
set total = `expr $total / 1024`
echo "Total = $total MBytes (transferred)"
grep "(compressed)" $PID.files

set num = `grep "(compressed)" $PID.files | cut -f3 -d" "`
if ($total == $num) then
	echo "Successful Transfer"
else
	echo "ERROR: total compressed file sizes do not match"
endif

