#!/bin/csh
#	Name:	cos_reslice_oo2ref
#	Author: Tom Videen
#	Date:	May 2007
#	Created for: COSS PET Processing
#	Intent: Coregister OO to reference t88-atlas brain

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?OO) then
	echo "ERROR: env OO not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
endif

#set REF  = /usr/local/npg/lib/t88/meanpet
#set MASK = /usr/local/npg/lib/t88/meanmask
set REF  = /data/cvl/coss/Reference/meanpet
set MASK = /data/cvl/coss/Reference/meanmask
set Z0	= 38

set VER   = 1.0
set PID   = $STUDY-
set GAUSS = _g3
set to    = _to_
set HO1   = $PID$HO$GAUSS
set OO1   = $PID$OO$GAUSS
set OOHO  = $PID$OO$to$HO
set HOREF = $PID$HO$to${REF:t}
set OOHO  = $PID$OO$to$HO
set OOREF = $PID$OO$to${REF:t}
set DDIR  = `pwd`

# Check existence of files:
# -------------------------
set EFILES = "$HO1.v $OO1.v $REF.img $MASK.img $REF.t88 $HOREF.air"
set STATUS = 0
foreach i ($EFILES)
	if (! -e $i) then
		echo "ERROR: $i does not exist."
		set STATUS = 1
	endif
end
if ($STATUS != 0) exit 1

echo "==========================================================================" >>! $LOGFILE
echo "cos_reslice_oo2ref  Version:" $VER `whoami` `date` >> $LOGFILE

imgreslice a$HO1 $HOREF
set check = `grep HESSIAN log`
echo $check
tail -8 log | tee -a $LOGFILE
analyze2img ra$HO1 $HO1.t88
scaleimage $HO1.t88 $INVSCALE
setz0 $HO1.t88 $Z0

combine_air $OOREF.air y $HOREF.air $OOHO.air
img2analyze $OO1.v a$OO1
imgreslice a$OO1 $OOREF
analyze2img ra$OO1 $OO1.t88
setz0 $OO1.t88 $Z0

echo "============================================================"
echo "Use vidi to check co-registration of $HO1.t88 and $OO1.t88 to standard"
echo "   Image 1: $REF.t88		(PET Rainbow)"
echo "   Image 2: $DDIR/$HO1.t88	(Hot Metal)"
echo "   Image 2: $DDIR/$OO1.t88	(Hot Metal)"
echo "   Compare composite transverse, sagittal, and coronal views."
echo "Press RETURN to cleanup and finish (CTRL-C to quit)"
set data = `line`

unalias rm
rm tmpho.v
rm a$HO1.*
rm a$OO1.*
rm ra$HO1.*
rm ra$OO1.*
rm log
cvl

echo "HO-to-atlas AIR file:  $HOREF.air" | tee -a $LOGFILE
echo "OO-to-atlas AIR file:  $OOREF.air" | tee -a $LOGFILE
echo "HO atlas-image:        $HO1.t88" | tee -a $LOGFILE
echo "OO atlas-image:        $OO1.t88" | tee -a $LOGFILE
echo "============================================================"

