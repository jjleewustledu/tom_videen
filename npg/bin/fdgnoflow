#!/bin/csh
#	Program:	fdgnoflow
#	Date:		Mar 2002
#	Intent:		Run Joanne's parameter estimation
#		This is the same as /home/usr/joanne/sun24/bin/fdgnoflow.tsk
#		(May 28, 1999) except for the formatting of the output.

if ($#argv != 3) then
	echo "Usage:  fdgnoflow tissue_activity blood_activity parameters"
	echo " e.g.:  fdgnoflow p5949wb_fdg.tsc p5949f.dta p5949wbf.pbl"
	exit 1
endif

set ver = `ls -l /home/usr/joanne/sun24/bin/fdgnoflow.tsk | cut -c34-41`
if ($ver != 130104) then
	echo "WARNING: fdgnoflow differs from Joanne's version"
	echo "Check that the algorithm has not changed"
endif

if (! $?NPG) setenv NPG /usr/local/npg

set tsc = $1
set dta = $2
set pbl = $3
set root = $pbl:r
set in  = $root.in
set out = $root.out

if (! -e $tsc) then
	echo "ERROR [fdgnoflow]: cannot find $tsc"
	exit 1
endif
if (! -e $dta) then
	echo "ERROR [fdgnoflow]: cannot find $dta"
	exit 1
endif
if (! -e $pbl) then
	echo "ERROR [fdgnoflow]: cannot find $pbl"
	exit 1
endif
if (-e $out) then
	echo " Replacing $out"
	unalias rm
	rm $out
endif
if (-e $in) echo " Replacing $in"

echo "$tsc" >! $in
echo "$dta" >> $in
echo "$pbl" >> $in
echo "${root}a.log" >> $in
echo "${root}b.log" >> $in
echo "${root}c.log" >> $in
echo "${root}d.log" >> $in

${NPG}/bin/fdgnoflow.bin < $in >& $out

