#!/bin/csh
#	Name:	t88trsing
#	Date:	Oct 1995
#	Author:	Tom Videen
#	Use:	Script to create individual t88trans images from a composite list
#			Uses parameter and imagelist files

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 2) then
	echo "Usage:  t88trsing prm list"
	echo " e.g.:  t88trsing my.prm my.lst"
	exit 1
endif

set prm = $1
set lst = $2
set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`
unalias rm

if (! -e ${prm}) then
	echo t88trsing": parameter file ${prm} does not exist"
	exit 1
endif

if (! -e ${lst}) then
	echo t88trsing": imagelist file ${lst} does not exist"
	exit 1
endif
split -1 ${lst} /tmp/t88$$.lst.
set list = `ls /tmp/t88$$.lst.*`
if ($#list < 1) then
	echo "Empty list file $lst"
	exit 1
endif

set filterspec = `grep imgfilter: ${prm}|  cut -d":" -f2-`
set imgfilter = `echo ${filterspec} | awk '{print $1}'`
switch (${imgfilter})
	case none:
		set suffix = ""
		breaksw
	case butterworth:
		set cutoff = `echo ${filterspec} | awk -F. '{print $2}' | awk '{print $1}'`
		set order = `echo ${filterspec} | awk '{print $3}'`
		set suffix = _${order}b${cutoff}
		breaksw
	case gauss:
    set cutoff = `echo ${filterspec} | awk -F. '{print $2}' | awk '{print $1}'`
		set suffix = _g${cutoff}
    breaksw
	default:
		set suffix = ""
		breaksw
endsw

set species = `grep species: ${prm} | cut -d":" -f2-`
set tracer = `grep tracer: ${prm} | cut -d":" -f2-`
set mode = `grep smg_mode: ${prm} | cut -d":" -f2-`
foreach i (${list})
	set id = `cat ${i} | cut -f1`
	set num = `cat ${i} | cut -f2`
	if (${mode} == "single" || ${mode} == "diff") then
		set smg = ${species}${id}${tracer}${num}${suffix}.t88
	else if (${mode} == "paired") then
		set num2 = `cat ${i} | cut -f4`
		set smg = ${species}${id}${tracer}${num}-${num2}${suffix}.t88
	else
		echo Cannot identify ${i}
		exit 1
	endif
	echo "Processing ${smg}"
	set rec = ${smg}.rec
	if (-e ${rec}) rm $rec
	echo "rec ${smg} ${now} ${uid}" > ${rec}
	echo "t88trsing $1 $2 $3" >> ${rec}
	ident ${NPG}/bin/t88trans.bin | grep t88trsing.c >> ${rec}
	echo "prm" >> ${rec}
	cat ${prm} >> ${rec}
	echo "endprm" >> ${rec}
	echo "imglst" >> ${rec}
	cat ${i} >> ${rec}
	echo "endimglst" >> ${rec}
 	${NPG}/bin/t88trans.bin ${prm} ${i} ${smg} ${rec} ${now} ${today} ${uid}
	echo "endrec" >> ${rec}
end
rm /tmp/t88$$.lst.*
alias rm "rm -i"
exit 0

