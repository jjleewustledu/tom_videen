#!/bin/csh
#	Name:	renameDyn
#	Date:	23-Oct-92 
#	Author:	Tom Videen
#	Use:	Change a set of dynamic ECAT files to a set of single frame files.

set argc = $#argv
if ($argc != 6) then
	echo "Usage: renameDyn <file> <#frames> <output_root> <initial_frame> <1st slice> <last slice>"
	echo "i.e.,  renameDyn m100sp4 30 m100s 22 1 31"
	exit
endif

set i = 1
set old = $1
set last = $2
set root = $3
set j = $4
set s1 = $5
set s2 = $6

if (!(-e ${old})) then
	echo "File ${old} does not exist"
	exit
endif

unalias rm
while (${i} <= ${last})
	set new = ${root}${j}
	if (-e ${new}) then
		echo "File ${new} already exists"
		exit
	endif
	echo "${old} frame ${i} slices ${s1} to ${s2} --> ${new}"
	set k = ${s1}
	while (${k} <= ${s2})
		matcopy ${old},${i},${k},1,0,0 ${new},1,${k},1,0,0
		@ k++
	end
	ecat2pett6 ${new} ${new}.i 1.0
	rm ${new}
	@ i++
	@ j++
end
alias rm "rm -i"
