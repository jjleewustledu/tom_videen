#!/bin/csh
# Name:		second_multibed
# Date:		Feb 2002
# Author:	Tom Videen
# Intent:	Separate frames of 953 image acquired with scanmultibed
#			and add them to the end of a previous scan

set OK = 0
if ($#argv != 6) then
	echo "Usage: second_multibed input_file output_root nframes initial_frm first_position last_position"
	echo " e.g., second_multibed m822nmb2.scn m822nb 5 9 3 4"
	echo " (nframes = number of frames at each position)"
	exit 1
endif

set infile = $1
set outroot = $2
set nfrms = $3
set frm0  = $4
set first = $5
set last  = $6

if (! -e $infile) then
	echo "ERROR: missing $infile"
	exit
endif

set pos = $first
while ($pos <= $last)
	set outfile = $outroot$pos.scn
	if (! -e $outfile) then
		echo "ERROR: $outfile does not exist"
		exit 1
	endif
	@ pos++
end

set num = 1
set frm = 1
while ($frm <= $nfrms)
	set pos = $first
	while ($pos <= $last)
		set outfile = $outroot$pos.scn
		echo "Copying $infile Frame $num (frame $frm bed $pos) to $outfile Frame $frm0"
		set pln = 1
		while ($pln <= 31)
			matcopy $infile,$num,$pln,1 $outfile,$frm0,$pln,1
			if ($status != $OK) then
				echo "ERROR copying $infile frame $num plane $pln"
				exit 1
			endif
			@ pln++
		end
		@ num++
		@ pos++
	end
	@ frm ++
	@ frm0 ++
end

