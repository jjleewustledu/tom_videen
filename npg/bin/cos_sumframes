#!/bin/csh
#	Name:	cos_sumframes
#   Author: Tom Videen
#   Date:   Apr 2002
#   Created for: COSS PET Processing
#   Intent: Sum dynamic water to determine center-of-brain

if ($#argv != 2) then
	echo "Usage: cos_sumframes firstframe lastframe"
	exit 1
endif

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
endif

set infile   = $STUDY-$HO.v
set outfile  = $STUDY-${HO}_sum.v
set filtered = $STUDY-${HO}_sum_g3.v
set f1 = $1
set f2 = $2

if (! -e $infile) then
	echo "ERROR: cannot find $infile"
	exit 1
endif

set ffrm = `firstfrm $infile | head -1`
set time0 = `imghdr $infile $ffrm | grep Start | cut -f2 -d= | cut -f1 -ds`
set time1 = `imghdr $infile $f1 | grep Start | cut -f2 -d= | cut -f1 -ds`
set starttime = `echo $time1 - $time0 | bc`
sumimgframes $infile $outfile $f1 $f2 $starttime
gauss $outfile $filtered 0.3

echo "=========================================================================="
echo "run vidi"
echo "  Load $filtered"
echo "  View transverse and sagittal sections to identify the X,Y,Z coordinates"
echo "     of center-of-brain for time/activity curves."

