# Create whole-brain image mask		(31-Aug-2005)
#	rp6549whole_brain.v
#	rp6549slice_msk.v
#	p6549atlas_to_fdg.air

if ($#argv != 1) then
    echo "Usage:  whole_brain_mask pid" 
    echo " e.g.:  whole_brain_mask p6000"
    exit 1
endif
set PID = $1

set DATA = /data/cvl/np497
#set DATA = /data/cvl/np325
set PET = $DATA/$PID/pet
set MR = $DATA/$PID/mri/bay_1
if (! -e $MR) set MR = $DATA/$PID/mri/bay_3
set AIR1 = $MR/Segmentation/${PID}mpr_to_atlas.air
set AIR2 = ${PID}mpr_to_${PID}fdg.air
set AIR3 = ${PID}atlas_to_mpr.air
set AIR4 = ${PID}atlas_to_fdg.air
set ZMASK = /usr/local/npg/lib/t88/oxyphos_mask
set BMASK = $PET/r${PID}mask.v
set SLMSK = r${PID}slice_msk
set WHOLE = r${PID}whole_brain.v

if (! -e $PET) then
    echo "ERROR [whole_brain_mask]: missing $PET"
    exit 1
else if (! -e $MR) then
    echo "ERROR [whole_brain_mask]: missing $MR"
    exit 1
else if (! -e $AIR1) then
    echo "ERROR [whole_brain_mask]: missing $AIR1"
    exit 1
else if (! -e $AIR2) then
    echo "ERROR [whole_brain_mask]: missing $AIR2"
    exit 1
endif
if (! -e $ZMASK.img) then
    echo "ERROR [whole_brain_mask]: missing $ZMASK.img"
    exit 1
else if (! -e $BMASK) then
    echo "ERROR [whole_brain_mask]: missing $BMASK"
    exit 1
endif

cd $PET
invert_air $AIR1 $AIR3
combine_air $AIR4 y $AIR2 $AIR3

reslice $AIR4 $SLMSK.img -a $ZMASK.img -k -o 
analyze2img $SLMSK $SLMSK.v
maskimg $BMASK $SLMSK.v $WHOLE 1

chgrp cvl $AIR4 $WHOLE $SLMSK.v
chmod 640 $AIR4 $WHOLE
rm $SLMSK.??? $AIR3
echo "Created:"
ls -l $AIR4 $SLMSK.v $WHOLE

