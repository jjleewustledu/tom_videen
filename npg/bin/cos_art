#!/bin/csh
#	Name:	cos_art
#	Author: Tom Videen
#	Date:	Apr 2004
#	Created for: COSS PET Processing
#	Intent: Check arterial input in t88 space

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?OO) then
	echo "ERROR: env OO not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
endif

set Z0 = 38

set PID   = $STUDY-
set HO1   = $PID$HO
set OO1   = $PID$OO
set HOREF = $PID${HO}_to_meanpet
set OOREF = $PID${OO}_to_meanpet

set EFILES = "$HO1.v $OO1.v $HOREF.air $OOREF.air"
set STATUS = 0
foreach i ($EFILES)
	if (! -e $i) then
		echo "ERROR: $i does not exist."
		set STATUS = 1
	endif
end
if ($STATUS != 0) exit 1

echo "Sum HO through frame?"
set LAST = `line`
set HOART = $PID${HO}_f1to$LAST
sumimgframes $HO1.v $HOART.v 1 $LAST 0
img2analyze $HOART.v a$HOART
imgreslice a$HOART $HOREF
analyze2img ra$HOART $HOART.t88
setz0 $HOART.t88 $Z0

echo "Sum OO through frame?"
set LAST = `line`
set OOART = $PID${OO}_f1to$LAST
sumimgframes $OO1.v $OOART.v 1 $LAST 0
img2analyze $OOART.v a$OOART
imgreslice a$OOART $OOREF
analyze2img ra$OOART $OOART.t88
setz0 $OOART.t88 $Z0

gauss $HOART.t88 1.0
gauss $OOART.t88 1.0

unalias rm
rm $HOART.v*
rm $OOART.v*
rm a$HOART.*
rm a$OOART.*
rm ra$HOART.*
rm ra$OOART.*
cvl

