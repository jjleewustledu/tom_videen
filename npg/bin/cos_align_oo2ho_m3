#!/bin/csh
#	Name:	cos_align_oo2ho_m3
#	Author:	Tom Videen
#	Date:	Mar 2005
#	Created for: COSS PET Processing
#	Intent: Coregister OO to HO
#	Requires:
#		cs01-001-ho1_g3.v (img)
#		cs01-001-oo1_g3.v (img)
#		cs01-001-mask.v
#	Creates:
#		cs01-001-oo1_to_ho1.air
#		rcs01-001-oo1_g3.v
#		cs01-001-oo1_to_ho1.log

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?OO) then
	echo "ERROR: env OO not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
endif

set VER   = 1.0
set PID   = $STUDY-
set GAUSS = _g3
set HO1   = $PID$HO$GAUSS
set OO1   = $PID$OO$GAUSS
set OOHO  = $PID${OO}_to_$HO
set MASK  = ${PID}mask.v
set AMASK = a$MASK:r
set THR   = 50

if (! -e $HO1.v) then
	echo "ERROR: cannot find $HO1.v"
	exit 1
else if (! -e $OO1.v) then
	echo "ERROR: cannot find $OO1.v"
	exit 1
else if (! -e $MASK) then
	echo "ERROR: cannot find $MASK"
	exit 1
endif

echo "==========================================================================" >>! $LOGFILE
echo "cos_align_oo2ho_m3  Version:" $VER `whoami` `date` >> $LOGFILE

# Convert images to Analyze format:
# --------------------------------
img2analyze $MASK $AMASK
img2analyze $HO1.v a$HO1
img2analyze $OO1.v a$OO1
setanamin $AMASK 0
setanamin a$HO1 0
setanamin a$OO1 0

# Align OO to HO, then reslice OO:
# --------------------------------
echo "Aligning:  $OO1 to $HO1"
set PRM = "-m 6 -p1 1 -t1 $THR -p2 1 -t2 $THR -e1 $AMASK -e2 $AMASK -x 3 -v"
echo alignlinear a$HO1 a$OO1 $OOHO.air $PRM >> $LOGFILE

alignlinear a$HO1 a$OO1 $OOHO.air $PRM >! log
set ERROR = `grep HESSIAN log`
if (`echo $ERROR | wc -w` != 0) then
	echo $ERROR
	echo "Alignment failed and will require manual processing"
	exit 1
endif
tail -7 log >> $LOGFILE
set ERROR = `grep HESSIAN log`
set CHK = `echo $ERROR | wc -w`
if ($CHK > 0) then
	echo $ERROR
	echo "Alignment may require an initialization file"
	echo $ERROR >> $LOGFILE
	exit 1
endif
imgreslice a$OO1 $OOHO
analyze2img ra$OO1 r$OO1.v
if (! -e null.air) alignlinear a$HO1 a$HO1 null.air -m 6 -p1 1 -t1 100 -p2 0 -x 2
airdif1 $OOHO.air null.air $MASK >> $LOGFILE
tail -2 $LOGFILE

echo "============================================================"
echo "Use vidi to check alignment of $OO vs. $HO"
echo "   Image 1: r$OO1.v   (PET Rainbow)"
echo "   Image 2: $HO1.v    (Hot Metal)"
echo "   Compare composite transverse, sagittal, and coronal views."
echo "   Press RETURN to cleanup and finish (CTRL-C to quit)"
set data = `line`

unalias rm
rm a$OO1.*
rm a$HO1.*
rm ra$OO1.*
rm a$MASK:r.???
rm null.air
rm log
cvl

echo "OO-to-HO AIR file:  $OOHO.air" | tee -a $LOGFILE
echo "============================================================"

