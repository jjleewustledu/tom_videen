#!/bin/csh
#	Module:	fdf2analyze
#	Date:	Jul 1998
#	Author:	Tom Videen
#	Use:	Convert FDF image files to ANALYZE image files

if ($#argv < 1 || $#argv > 2) then
	echo "Usage: fdf2analyze filename (without extension) [1]"
	echo " e.g.: fdf2analyze 3ddata_128x128x128   (creates short integers)"
	echo " or    fdf2analyze 3ddata_128x128x128 1 (creates floating pt)"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

set fdf = $1.fdf
set img = $1.img
set hdr = $1.hdr
set tmpimg = ${1}$$.img
set tmphdr = ${1}$$.hdr
set flag = 0

if ($#argv == 2) set flag = $2
if (! -e $fdf) then
	echo "ERROR: cannot find $fdf"
	exit 1
endif
if (-e $img) then
	echo "ERROR: output file $img already exists"
	exit 1
endif
if (-e $hdr) then
	echo "ERROR: output file $hdr already exists"
	exit 1
endif

${NPG}/bin/fdfsplit ${fdf} ${tmpimg} ${tmphdr}
${NPG}/bin/fdf2analyze.bin ${tmpimg} ${tmphdr} ${img} ${hdr} ${flag}
rm ${tmpimg} ${tmphdr}

