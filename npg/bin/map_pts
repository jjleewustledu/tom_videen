#!/bin/csh
#	Module:	map_pts
#	Author:	Tom Videen
#	Date:	Jul 2008
#	Use:	Map 2 contact points in one image to another using AIR file

if ($#argv != 8) then
	echo "Usage:  map_pts air output x0 y0 z0 x1 y1 z1"
	echo " e.g.,  map_pts db339_ct2_to_db339_ct1 tips 271 264 58 334 275 60"
	exit 1
endif

set AIR = $1.air
set LOC = $2.loc
set xp0 = $3
set yp0 = $4
set zp0 = $5
set xp1 = $6
set yp1 = $7
set zp1 = $8

if (! -e $AIR) then
	echo "ERROR [map_pts]: missing $AIR"
	exit 1
endif

echo map_pts $AIR $LOC $3 $4 $5 $6 $7 $8 >! $LOC

# Image in which contact locations are defined (CT0)
set axdim = `dumpair $AIR | grep "res xdim" | cut -f2 -d:`
set aydim = `dumpair $AIR | grep "res ydim" | cut -f2 -d:`
set azdim = `dumpair $AIR | grep "res zdim" | cut -f2 -d:`
set axsize = `dumpair $AIR | grep "res xsize" | cut -f2 -d:`
set aysize = `dumpair $AIR | grep "res ysize" | cut -f2 -d:`
set azsize = `dumpair $AIR | grep "res zsize" | cut -f2 -d:`

# Image to which CT0 is aligned
set mxdim = `dumpair $AIR | grep "std xdim" | cut -f2 -d:`
set mydim = `dumpair $AIR | grep "std ydim" | cut -f2 -d:`
set mzdim = `dumpair $AIR | grep "std zdim" | cut -f2 -d:`
set mxsize = `dumpair $AIR | grep "std xsize" | cut -f2 -d:`
set mysize = `dumpair $AIR | grep "std ysize" | cut -f2 -d:`
set mzsize = `dumpair $AIR | grep "std zsize" | cut -f2 -d:`

# convert coordinates to Analyze order in mm
set x0 = `echo "($axdim - $xp0) * $axsize" | bc -l | cut -c1-6`
set y0 = `echo "($aydim - $yp0) * $aysize" | bc -l | cut -c1-6`
set z0 = `echo "($azdim - $zp0) * $azsize" | bc -l | cut -c1-6`
set x1 = `echo "($axdim - $xp1) * $axsize" | bc -l | cut -c1-6`
set y1 = `echo "($aydim - $yp1) * $aysize" | bc -l | cut -c1-6`
set z1 = `echo "($azdim - $zp1) * $azsize" | bc -l | cut -c1-6`

echo $x0 $y0 $z0 >! ptmp
echo $x1 $y1 $z1 >> ptmp

reslice_ucf $AIR ptmp rtmp y

# convert final locations from Analyze order
set xi = `head -1 rtmp | tail -1 | cut -f1 -d' '`
set yi = `head -1 rtmp | tail -1 | cut -f2 -d' '`
set zi = `head -1 rtmp | tail -1 | cut -f3 -d' '`
set rx = `echo "$mxdim - ($xi / $mxsize)" | bc -l | cut -c1-6`
set ry = `echo "$mydim - ($yi / $mysize)" | bc -l | cut -c1-5`
set rz = `echo "$mzdim - ($zi / $mzsize)" | bc -l | cut -c1-6`
echo $rx $ry $rz >> $LOC

set xi = `head -2 rtmp | tail -1 | cut -f1 -d' '`
set yi = `head -2 rtmp | tail -1 | cut -f2 -d' '`
set zi = `head -2 rtmp | tail -1 | cut -f3 -d' '`
set rx = `echo "$mxdim - ($xi / $mxsize)" | bc -l | cut -c1-6`
set ry = `echo "$mydim - ($yi / $mysize)" | bc -l | cut -c1-5`
set rz = `echo "$mzdim - ($zi / $mzsize)" | bc -l | cut -c1-6`
echo $rx $ry $rz >> $LOC

unalias rm
rm ptmp rtmp
