#!/bin/csh
#	Module:	imgsum
#	Date:	Jan 1997
#	Author:	Tom Videen
#	Use:	Create a summed image

if ($#argv != 3) then
	echo "Usage:  imgsum img1,f1 img2,f2 out"
	echo " e.g.:  imgsum p5000ho1.v p5000ho2.v p5000sum.v"
	echo "   or:  imgsum p5000ho1.v,.5 p5000ho2.v,2 p5000sum.v"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

set scan1 = `echo $1 | awk -F, '{print $1}'`
if (`echo $1 | grep ','` != "") then
	set f1 = `echo $1 | awk -F, '{print $2}'`
else
	set f1 = 1
endif

set scan2 = `echo $2 | awk -F, '{print $1}'`
if (`echo $2 | grep ','` != "") then
	set f2  = `echo $2 | awk -F, '{print $2}'`
else
	set f2 = 1
endif

set sum = $3
set rec = ${sum}.rec

set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (! -e $scan1) then
	echo "ERROR: cannot find $scan1"
	exit 1
endif
if (! -e $scan2) then
	echo "ERROR: cannot find $scan2"
	exit 1
endif

if (-e $rec) rm $rec
echo "rec $sum $now $uid" > $rec
echo "imgsum $1 $2 $3" >> $rec
ident ${NPG}/bin/imgsum.bin | grep imgsum.c >> $rec

${NPG}/bin/imgsum.bin $scan1 $f1 $scan2 $f2 $sum $rec $now $today $uid

echo "endrec" >> $rec
