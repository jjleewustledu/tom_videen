#!/bin/csh
#	Module:	ana2dgauss
#	Author:	Tom Videen
#	Date:	Aug 1999
# 	Use:	Apply 2D Gaussian filter to an Analyze image

if ($#argv != 2 && $#argv != 3) then
	echo "Usage:  ana2dgauss inimg [outimg] half_freq"
	echo " e.g.:  ana2dgauss p2000ho1 0.3"
	echo "        ana2dgauss p2000ho1 p2000ho1_2dg3 0.3"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg
set inimg = $1.img
set inhdr = $1.hdr

if ($#argv == 3) then
	set outimg	= $2.img
	set outhdr	= $2.hdr
	set f0 = $3
else
	set f0 = $2
	set base = `basename ${inimg}`
	set root = `echo ${base} | awk -F. '{print $1}'`
	set filterid = `echo "scale=0;${f0} * 10" | bc`
	set whole = `echo ${filterid} | awk -F. '{print $1}'`
	set fraction = `echo ${filterid} | awk -F. '{print $2}'`
	if (${fraction} == 0 || ${fraction} == 00 || ${fraction} == 000) then
		set filterid = ${whole}
	endif
	set outimg = ${root}_2dg${filterid}.img
	set outhdr = ${root}_2dg${filterid}.hdr
endif

if (! -e $inimg) then
	echo "ERROR [ana2dgauss]: $inimg not found"
	exit 1
endif
if (-e $outimg) then
    echo "Replacing $outimg"
endif

${NPG}/bin/ana2dgauss.bin $inhdr $inimg $outhdr $outimg $f0 

