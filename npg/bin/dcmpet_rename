#!/usr/local/bin/tcsh
#   Name:	dcmpet_rename
#   Author: Tom Videen
#   Date:   Feb 2011
#   Use:    Rename PET/CT DICOM files

if ($#argv != 2) then
    echo "Usage:  dcmpet_rename root extension"
    echo " e.g.,  dcmpet_rename R IMA"
    exit 1
endif

set all = $1*.$2

# Check first file for DICOM format
# ------------------------------------
set chk = `dcm_dump_file -t "$all[1]" | head -1 | cut -c1-5`
if ($chk != "DICOM") then
	echo "ERROR: not a DICOM file $all[1]"
	exit 1
endif

# Convert to 6 digit number: first digit = series, digits 2-6 = image number
# --------------------------------------------------------------------------
foreach i ($all)
	set ser = `dcm_dump_file -t "$i" | grep 'Series Number' | cut -f5 -d/`
	set num = `dcm_dump_file -t "$i" | grep 'Image Number' | cut -f5 -d/`
    if ($num < 10) then
        set nam = "${ser}0000${num}.dcm"
    else if ($num < 100) then
        set nam = "${ser}000${num}.dcm"
    else if ($num < 1000) then
        set nam = "${ser}00${num}.dcm"
    else if ($num < 10000) then
        set nam = "${ser}0${num}.dcm"
    else 
        set nam = "${ser}${num}.dcm"
    endif
	echo $nam
	mv "$i" $nam
end

