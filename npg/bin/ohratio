#!/bin/csh
#	Name:	ohratio
#	Author:	Tom Videen
#	Date:	Feb 2002
#	Use:	Compute ratio image of OO and HO input

if (! $?NPG) setenv NPG /usr/local/npg

# Parameters for creating 65-slice t88 images
set PLNS = 65
set Z0   = 38
# Threshold is applied to water in t88 space over planes 5-45
set TOP  = 4
set BOT  = 46
set THR  = 20
set NVAL = 40

if ($#argv != 4) then
	echo "Usage:  ohratio oo ho out mask"
	echo " e.g.:  ohratio p5000oo1_g3.t88 p5000ho1_g3.t88 p5000oh1_g3.t88 p5000mask.t88"
	exit 1
endif

set OO   = $1
set HO   = $2
set OUT  = $3
set MASK = $4
set TEMP = ohratio$$.tmp

if (! -e $OO) then
	echo "ERROR [ohratio]: $OO does not exist."
	exit 1
endif
if (! -e $HO) then
	echo "ERROR [ohratio]: $HO does not exist."
	exit 1
endif
if (-e $MASK) then
	echo 
	echo ">>>  Warning [ohratio]: $MASK exists."
	echo ">>>  enter CTRL-C to cancel, then delete $MASK before running ohratio, or"
	echo ">>>  enter RETURN to continue computing oo/ho ratio using this mask."
	echo 
	set status = `line`
else
	maskslc $HO $MASK 1to$TOP
	maskslc $MASK $MASK ${BOT}to$PLNS
	maskpct $MASK $MASK $THR
	maskimg $HO $MASK $MASK 1
endif

${NPG}/bin/ohratio.bin $OO $HO $OUT $MASK $NVAL | tee $TEMP
setz0 $OUT $Z0

set PLN1 = `echo $TOP + 1 | bc`
set PLN2 = `echo $BOT - 1 | bc`
set SCL = `tail -1 $TEMP | cut -f2 -d=`
echo
echo ">>> Normalize to:       $NVAL"
echo ">>> Thresholding HO at: $THR"
echo ">>> Over Atlas Planes:  $PLN1 - $PLN2"
echo ">>> Scaler:             $SCL"
echo ">>> OO/HO Image:        $OUT"

unalias rm
rm $TEMP


