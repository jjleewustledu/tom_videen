#!/bin/csh
#   Name:   segment_ct1
#   Date:   Oct 2006
#   Author: Tom Videen
# 
# Prior to calling this, run vidi loading ${PID}ct_n.img:
#   Check all planes of original image.
#   If necessary, convert to Hounsfield units.
#	   anasub ${PID}ct_n 1020
#   Locate pineal calcification and fill it in with maskand (fill=60)
#     (possibly running maskand more than once with additional seeds)
#	   maskand ${PID}ct_n $IN 60 1000 60 241 319 26
#   Locate venticular calcification and edit it (fill=10).
#   Identify a pixel within the brain (as input for this script).
#	Save edited image as $IN.img

if ($#argv != 4) then
    echo "Usage:  segment_ct1 pid x_brain y_brain z_brain"
    echo " e.g.:  segment_ct1 p5132 300 300 10"
    exit
endif

set PID   = $1
set BRAIN = "$2 $3 $4"
set IN    = ${PID}seg1
set OUT   = ${PID}seg2
set NEXT  = ${PID}seg3

set SETVAL = -2000
set ABSMIN = -100
set ABSMAX =  200
set BRAIN_RANGE = "10 75"

if (! -e $IN.img) then
	echo "ERROR [segment_ct1]: image file $IN.img does not exist."
	exit
endif
if (! -e $IN.hdr) then
	if2analyze $IN
endif

echo ">>>  maskor"
maskor $IN $OUT $ABSMIN $ABSMAX $SETVAL
echo ">>>  expandout"
expandor $OUT $OUT $SETVAL $SETVAL $BRAIN_RANGE
echo ">>>  maskout"
maskout $OUT $OUT $ABSMIN $ABSMAX $SETVAL $BRAIN

echo "------------------------------------------------------------------"
echo "Edit $OUT.img"
echo "  Remove eyeballs & tissue near brainstem (fill = -2000)"
echo "  Save as $NEXT.img"
echo
