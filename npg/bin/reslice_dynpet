#!/bin/csh
#	Module:	reslice_dynpet
#   Date:	Sep 2008
#   Author:	Tom Videen
#	Use:	Reslice all frames of a dynamic PET image (fdopa, nmb)

if ($#argv != 5) then
	echo "Usage: reslice_dynpet root extension firstfrm lastfrm referencefrm"
	echo " e.g., reslice_dynpet p6000fd v 1 30 2"
	exit 1
endif

set ROOT  = $1
set EXT   = $2
set FIRST = $3
set LAST  = $4
set REF   = $5

set FRAMES = ${FIRST}to$LAST
set NULLAIR = ${ROOT}_null
set ORG = ../${ROOT}1.$EXT
set RORG = r${ROOT}1.$EXT

# Check for existence of original image and AIR files
# -------------------------------------------------------
if (! -e $ORG) then
	echo "ERROR [reslice_dynpet]: cannot find $ORG"
	exit 1
endif

# Check for existence of AIR files
# -------------------------------------------------------
if (! -e $NULLAIR.air) then
	echo "ERROR [reslice_dynpet]: missing $NULLAIR.air"
	exit 1
endif
set i = $REF
@ i++
while ($i <= $LAST)
	set NETAIR = ${ROOT}_${REF}vs${i}
	if (! -e $NETAIR.air) then
		echo "ERROR [reslice_dynpet]: missing $NETAIR.air"
		exit 1
	endif
	@ i++
end

# Separate frames, reslice, merge, and remove intermediate files
# -------------------------------------------------------
single_frames $ORG $ROOT i $FIRST $LAST
	if ($status != 0) exit 1
reslicedyn $ROOT .i $FRAMES $REF $ORG
	if ($status != 0) exit 1
merge_dynframes r$ROOT $FRAMES .i $RORG
	if ($status != 0) exit 1
remove_dynfiles $ROOT .i $FRAMES
remove_dynfiles r$ROOT .i $FRAMES

