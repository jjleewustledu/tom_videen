#!/bin/csh
#	Module:	des2if
#	Date:	Dec 2000
#	Author:	Tom Videen
#	Use:	Convert Descriptor file format to Interfile format,

if ($#argv != 4) then
	echo "Usage:  des2if prefix ff,lf suffix output"
	echo " e.g.:  des2if phb1143o1507 1,60 00cu026565 p1143ho1"
	echo " "
	echo "Note: Input (Descriptor) files must have extensions .des and .ima"
	echo "      Output (Interfile) files will have extensions .ifh and .ifi"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

set pre = $1
set ff  = `echo $2 | awk -F, '{print $1}'`
set lf  = `echo $2 | awk -F, '{print $2}'`
set suf = $3
set out = $4

set i = $ff
while ($i <= $lf)
	if ($i < 10) then
		set in = ${pre}000$i$suf
	else if ($i < 100) then
		set in = ${pre}00$i$suf
	else if ($i < 1000) then
		set in = ${pre}0$i$suf
	else
		set in = $pre$i$suf
	endif
	set ima = $in.ima
	set hdr = $in.des
	if (! -e $ima) then
		echo "ERROR [des2if]: $ima does not exist"
		exit 1
	endif
	if (! -e $hdr) then
		echo "ERROR [des2if]: $hdr does not exist"
		exit 1
	endif
	@ i++
end

if (-e $out.ifh) echo "Replacing $out.ifh"
if (-e $out.ifi) echo "Replacing $out.ifi"

${NPG}/bin/des2if.bin $pre $suf $out $ff $lf
