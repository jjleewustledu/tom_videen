#!/bin/csh
#	Name:	tapedup
#	Date:	26-Jun-92 
#	Author:	Tom Videen
#	Use:	Copy a tape from local tape drive to one on another Sun 

set argc = $#argv
if ($argc != 1 && $argc != 3) then
	echo "Usage: tapedup remote_host [local_tape# remote_tape#]"
	echo "where:"
	echo "  remote_host is where the writeable tape is to be found"
	echo "  local_tape# is the local (input) host's tape drive number"
	echo "  remote_tape# is the remote (output) host's tape drive number"
	echo "  (default tape drive number is 1, as in /dev/rst1)"
	echo "  i.e., tapedup petsun2"
	echo "  or  , tapedup petsun6 29 1"
	exit
endif

ping $1
if ($#argv == 3) then
	set devin = $2
	set devout = $3
else
	set devin = 1
	set devout = 1
endif

echo "rewinding local (input) tape /dev/rst${devin}in `hostname`"
mt -f /dev/rst${devin} rewind
if ($status != 0) then
	exit
endif

echo "rewinding (output) tape /dev/rst${devout} in $1"
rsh $1 "mt -f /dev/rst${devout} rewind"
if ($status != 0) then
	exit
endif

echo "copying tape from `hostname` to $1 at `date`"
copytape /dev/nrst${devin} | rsh $1 "copytape -t > /dev/nrst${devout}"

echo "rewinding local tape"
mt -f /dev/rst${devin} offline

echo "rewinding tape in " $1
rsh $1 "mt -f /dev/rst${devout} offline"

echo "tapedup complete at `date`"
