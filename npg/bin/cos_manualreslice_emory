#!/bin/csh
#	Name:	cos_manualreslice
#	Author: Tom Videen
#	Date:	Feb 2008
#	Created for: COSS PET Processing
#	Intent: Coregister HO to reference t88-atlas brain

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?OO) then
	echo "ERROR: env OO not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
endif

set REF  = meanpet
set Z0	= 38

set VER   = 1.0
set PID   = $STUDY-
set GAUSS = _g3
set to    = _to_
set HO1   = $PID$HO$GAUSS
set OO1   = $PID$OO$GAUSS
set OOHO  = $PID$OO$to$HO
set HOREF = $PID$HO$to$REF
set OOHO  = $PID$OO$to$HO
set OOREF = $PID$OO$to$REF
set DDIR  = `pwd`

echo "==========================================================================" >>! $LOGFILE
echo "cos_manualreslice  Version:" $VER `whoami` `date` >> $LOGFILE

imgreslice $HO1 $HOREF
analyze2img r$HO1 $HO1.t88
setz0 $HO1.t88 $Z0

combine_air $OOREF.air y $HOREF.air $OOHO.air
imgreslice $OO1 $OOREF
analyze2img r$OO1 $OO1.t88
setz0 $OO1.t88 $Z0

echo "============================================================"
echo "Use vidi to check co-registration of $HO1.t88 and $OO1.t88 to standard"
echo "   Image 1: $REF.t88		(PET Rainbow)"
echo "   Image 2: $DDIR/$HO1.t88	(Hot Metal)"
echo "   Image 2: $DDIR/$OO1.t88	(Hot Metal)"
echo "   Compare composite transverse, sagittal, and coronal views."
echo "Press RETURN to cleanup and finish (CTRL-C to quit)"
set data = `line`

rm log
cvl

echo "HO-to-atlas AIR file:  $HOREF.air" | tee -a $LOGFILE
echo "OO-to-atlas AIR file:  $OOREF.air" | tee -a $LOGFILE
echo "HO atlas-image:        $HO1.t88" | tee -a $LOGFILE
echo "OO atlas-image:        $OO1.t88" | tee -a $LOGFILE
echo "============================================================"

