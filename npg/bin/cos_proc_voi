#!/bin/csh
#	Name:	cos_proc_voi
#	Author:	Tom Videen
#	Date:	Apr 2002
#	Created for: COSS PET Processing
#	Intent: Measure VOI activity and compute left/right ratio

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
else if (! $?OO) then
	echo "ERROR: env OO not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
endif

set VER   = 1.0
set DIAM  = 19.0
set GAUSS = _g3
set PID = $STUDY-
set HO1 = $PID$HO$GAUSS.t88
set OO1 = $PID$OO$GAUSS.t88
set RGT = $PID$OO$HO-mca.rgt
set VOI = ${PID}mca.voi

if (! -e $OO1) then
	echo "ERROR [cos_proc_voi]: $OO1 not found"
	exit 1
else if (! -e $HO1) then
	echo "ERROR [cos_proc_voi]: $HO1 not found"
	exit 1
else if (! -e $VOI) then
	echo "ERROR [cos_proc_voi]: $VOI not found"
	exit 1
endif

echo "==========================================================================" >>! $LOGFILE
echo "cos_proc_voi  Version:" $VER `whoami` `date` >> $LOGFILE

voicoss.bin $OO1 $HO1 $VOI $RGT $DIAM `date` `whoami`
echo
echo "VOI Diameter: $DIAM mm" | tee -a $LOGFILE
echo "MCA VOI File: $VOI" | tee -a $LOGFILE
echo "RGT File:     $RGT" | tee -a $LOGFILE
echo
cat $RGT | tee -a $LOGFILE
echo "" >> $LOGFILE
set today = `date +'%d %h 20%y'`
set ratio = `grep Ratio $RGT | cut -f1 -dR`
echo "Study:     $STUDY" >> $LOGFILE
echo "Ratio:     $ratio" >> $LOGFILE
echo "Processed: $today" `whoami` >> $LOGFILE
echo "==========================================================================" >> $LOGFILE

