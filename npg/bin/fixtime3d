#!/bin/csh
# Module: fixtime3d
# Date:   Aug 2003
# Author: Tom Videen
# Use:  Correct frame duration and adjust scale factor in dynamic frames
#       after running fixsingles on ECAT sinogram and reconstructing.
#       (uses hdrEdit)
# Note: On hrcon, must skip the "/home/ecat7/capp" returned after command on on "set first" line:
#       hrcon:  head -7
#       others: head -6

if ($#argv != 1) then
	echo "Usage:  fixtime3d imagefile"
	echo " e.g.,  fixtime3d p1234_567_8b12_de3.v"
	exit 1
endif

set file = $1
set nname = $file:t
set dmp = /tmp/$nname:r".dmp"
set cmd = /tmp/$nname:r".cmd"
set tmp = /tmp/$nname:r".tmp"

unalias rm
imgfrms $file | cut -c1-3 >! junk
set first = `head -5 junk | tail -1`
set last  = `tail -1 junk`
echo $first $last
rm junk

echo s >! $cmd
echo n >> $cmd
set i = $first
while ($i <= $last)
	echo m >> $cmd
	echo $i >> $cmd
	echo d >> $cmd
	@ i++
end
echo q >> $cmd
hdrEdit -c -f $file < $cmd >&! $dmp

fixtime3d.awk $dmp >! $cmd
set len =  `wc -l $cmd | cut -c1-8`

if ($len > 4) then
	hdrEdit -c -f $file -d $tmp < $cmd >& /dev/null 
	echo "Frame Durations restored in $file"
	rm $tmp
else
	echo "No frame errors found in $file"
endif
rm $cmd
rm $dmp
exit

