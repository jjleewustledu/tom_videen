#!/bin/csh
#	Name:	voicoss
#	Author:	Tom Videen
#	Date:	Apr 2002
#	Created for: COSS PET Processing
#	Intent: Measure VOI activity and compute left/right ratio

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 6) then
	echo "Usage:  voicoss site subject ho oo voi logfile"
	echo " e.g.,  voicoss 01 001 ho1 oo1 t88newmca.voi proc.log"
	exit 1
endif

set SITE  = $1
set SUBJ  = $2
set HO    = $3
set OO    = $4
set VOI   = $5
set LOG   = $6

set VER   = 1.0
set DIAM  = 19.0
set GAUSS = _g3
set ext   = t88

set PID = cs$SITE-$SUBJ-
set HO1 = $PID$HO$GAUSS.$ext
set OO1 = $PID$OO$GAUSS.$ext
set RGT = $PID$OO$HO-mca.rgt

if (! -e $OO1) then
	echo "ERROR [voicoss]: $OO1 not found"
	exit 1
else if (! -e $HO1) then
	echo "ERROR [voicoss]: $HO1 not found"
	exit 1
else if (! -e $VOI) then
	echo "ERROR [voicoss]: $VOI not found"
	exit 1
endif

echo "==========================================================================" >>! $LOG
echo "voicoss  Version:" $VER `whoami` `date` >> $LOG
echo $0 $1 $2 $3 $4 $5 $6 >> $LOG

${NPG}/bin/voicoss.bin $OO1 $HO1 $VOI $RGT $DIAM `date` `whoami`
echo
echo "VOI Diameter: $DIAM mm" | tee -a $LOG
echo "MCA VOI File: $VOI" | tee -a $LOG
echo "RGT File:     $RGT" | tee -a $LOG
echo
cat $RGT | tee -a $LOG

