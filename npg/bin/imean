#!/bin/csh
#	Module:	imean
#	Date:	Apr 1996
#	Author:	Tom Videen
#	Use:	Create a composite mean image.
#			Uses parameter and imagelist files

if ($#argv < 3 || $#argv > 4) then
	echo "Usage:  imean prm imagelist image [-z]"
	echo " e.g.:  imean prm lst output.smg"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

set prm = $1
set lst = $2
set smg = $3
set rec = ${smg}.rec
set zflag = n
if ($#argv == 4) then
	switch ("$argv[4]")
	case -z:
		set zflag = y
		breaksw
	default:
		echo "Usage ERROR [imean]: extra parameter $argv[4]"
		echo "Usage: imean prm imagelist image [-z]"
		exit 1
	endsw
endif

set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (! -e $prm) then
	echo "ERROR [imean]: cannot find $prm"
	exit 1
endif

if (! -e $lst) then
	echo "ERROR [imean]: cannot find $lst"
	exit 1
endif

if (-e $rec) rm $rec

echo "rec ${smg} ${now} ${uid}" > ${rec}
echo "imean $1 $2 $3 $4" >> ${rec}
ident ${NPG}/bin/imean.bin | grep imean.c >> ${rec}
echo "prm" >> ${rec}
cat ${prm} >> ${rec}
echo "endprm" >> ${rec}
echo "imglst" >> ${rec}
cat ${lst} >> ${rec}
echo "endimglst" >> ${rec}

${NPG}/bin/imean.bin ${prm} ${lst} ${smg} ${rec} ${zflag} ${now} ${today} ${uid}

echo "endrec" >> ${rec}
