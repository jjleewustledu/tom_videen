#!/bin/csh
#	Name:	cos_scale_water
#	Author:	Tom Videen
#	Date:	Nov 2007
#	Created for: COSS PET Processing
#	Intent:	Scale water to reasonable pixel values (matching water to meanpet)

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
endif

set GAUSS = _g3
set WATER = $STUDY-$HO$GAUSS.v
if (! -e $WATER) then
	echo "ERROR: $WATER not found"
	exit 1
endif

set NPLANES = `imghdr $WATER | grep slices | cut -f1 -ds`
set MASK = `echo 2 \* $NPLANES/3 | bc`
maskslc $WATER tmpho.v ${MASK}to${NPLANES}

set maxHO = `imghdr tmpho.v | grep Max | cut -f2 -d= | cut -f1 -dM`
set scaleHO = `echo 1550 / $maxHO | bc -l`
echo "$WATER    Max = $maxHO   Scaler = $scaleHO"
echo "$WATER    Max = $maxHO   Scaler = $scaleHO" >> $LOGFILE
echo "=========================================================================="

scaleimage $WATER $scaleHO

set maxHO = `imghdr $WATER | grep Max | cut -f2 -d= | cut -f1 -dM`
echo "$WATER    Max = $maxHO"
unalias rm
rm tmpho.v
exit
