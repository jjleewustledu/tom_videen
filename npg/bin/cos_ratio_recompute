#!/bin/csh
#	Name:	cos_ratio_recompute
#	Author:	Tom Videen
#	Date:	Dec 2004
#	Created for: COSS PET Processing
#	Intent: Compute ratio image of OO/HO

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?OO) then
	echo "ERROR: env OO not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
endif

# Parameters for creating 65-slice t88 images
# Threshold is applied to water in t88 space over planes 5-45
set VER  = 1.0
set Z0   = 38
set NVAL = 40

set GAUSS = _g3
set PID   = $STUDY-
set HO1   = $PID$HO$GAUSS.t88
set OO1   = $PID$OO$GAUSS.t88
set MASK  = ${PID}mask.t88
set OOHO  = $PID$OO$HO$GAUSS.t88
set TMP   = cos_ratio$$.tmp

if (! -e $OO1) then
	echo "ERROR: $OO1 does not exist."
	exit 1
endif
if (! -e $HO1) then
	echo "ERROR: $HO1 does not exist."
	exit 1
endif
if (! -e $MASK) then
	echo "ERROR: $MASK does not exist."
	exit 1
endif

echo "==========================================================================" >> $LOGFILE
echo "cos_ratio_recompute  Version:" $VER `whoami` `date` >> $LOGFILE

ohratio.bin $OO1 $HO1 $OOHO $MASK $NVAL | tee $TMP
setz0 $OOHO $Z0

set SCL = `tail -1 $TMP | cut -f2 -d=`
echo
echo "Normalize to:        $NVAL" | tee -a $LOGFILE
echo "Scaler:              $SCL" | tee -a $LOGFILE
echo "OO/HO Image:         $OOHO" | tee -a $LOGFILE

unalias rm
rm $TMP
echo "=========================================================================="

