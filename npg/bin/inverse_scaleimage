#!/bin/csh
#	Name:	inverse_scaleimage
#	Author:	Tom Videen
#	Date:	Oct 2006
# 	Use:	Change scaling factor in ECAT image files using inverse of factor'

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 2) then
	echo "Usage:  inverse_scaleimage filename factor"
	echo " e.g.:  inverse_scaleimage p2000ho1.img 0.85"
	exit 1
endif

set filename = $1
set factor = $2
set tmpfile = ${filename}.tmp
set recfile = ${filename}.tmp.rec
set now = `date`
set uid = `whoami`

if (! -e $filename) then
	echo "ERROR: cannot find $filename"
	exit 1
endif

echo "rec ${filename} ${now} ${uid}" > ${recfile}
echo "inverse_scaleimage $1 $2" >> ${recfile}
ident ${NPG}/bin/inverse_scaleimage.bin | grep inverse_scaleimage.c | cut -d$ -f2 >> ${recfile}
if (-e ${filename}.rec) then
	echo ${filename}.rec
	cat ${filename}.rec >> ${recfile}
endif

cp ${filename} ${tmpfile}
${NPG}/bin/inverse_scaleimage.bin ${tmpfile} ${factor} `date` `whoami`

set endstat = $status
if ($endstat == 0) then
	echo "endrec" >> ${recfile}
	unalias mv
	mv ${tmpfile} ${filename}
	mv ${recfile} ${filename}.rec
else
	unalias rm
	rm ${tmpfile}
	rm ${recfile}
endif
