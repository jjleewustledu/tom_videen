#!/bin/csh
#	Name:	if2img
#	Date:	Aug 2006
#	Author:	Tom Videen
#	Use:	Convert Interfile to ECAT 7 format via an Analyze format

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv < 1 || $#argv > 2) then
	echo "Usage:  if2img input output"
	echo " e.g.,  if2img p2000mpr p2000mpr1.v"
	exit 1
endif

set ifh = $1.ifh
set ifi = $1.img
if (! -e $ifh) then
	echo "ERROR [if2img]: $ifh does not exist"
	exit 1
endif
if (! -e $ifi) then
	set ifi = $1.ifi
	if (! -e $ifi) then
		echo "ERROR [if2img]: $ifi does not exist"
		exit 1
	endif
endif

set out = $2
if ($out:e != "v" && $out:e != "t88") then
	echo "ERROR [if2img]: output file must have extension 'v' or 't88'"
	exit 1
endif
if (-e $out) echo "Warning: Replacing $out"

set tmphdr = $$.hdr
set tmpimg = $$.img
set std = "NULL"
set frm = 0
set vox = 0

${NPG}/bin/if2analyze.bin $ifh $ifi $tmphdr $tmpimg
${NPG}/bin/analyze2img.bin $tmpimg $tmphdr $out $std $frm $vox `date` `whoami`

unalias rm
rm $tmpimg $tmphdr
