#!/bin/csh
#	Name:	imgreslice
#	Date:	Jan 2006
#	Author:	Tom Videen
#	Use:	Simplified call to reslice (AIR)

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv < 2 || $#argv > 6) then
	echo "Usage:  imgreslice input_file air_file (without extensions) [interpolation_model]"
	echo " e.g.,  imgreslice p5000ct p5000ct2ho"
	echo "  or,   imgreslice p5000mpr p5000mpr2pet 5 6 6 6 (for 3d windowed sinc interpolation)"
	exit 1
endif

set in = $1
set air = $2.air
set out = r$in
if ($#argv == 2) then
	set model = 1
else
	set model = $3
endif

if (! -e $in.img) then
	echo "ERROR [imgreslice]: cannot find $in.img"
	exit 1
endif
if (! -e $air) then
	echo "ERROR [imgreslice]: cannot find $air"
	exit 1
endif
if (-e $out.img) echo "Replacing: $out.img"

${NPG}/bin/setanamin $in -1
${NPG}/bin/reslice $air $out -a $in -k -o -n $model $4 $5 $6

# Set -32768 values to 0 (AIR quirk)
${NPG}/bin/maskand $out $out -32768 -32768 0 >& /dev/null
