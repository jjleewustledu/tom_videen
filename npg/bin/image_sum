#!/bin/csh
#   Name:   image_sum
#   Date:   May 2000
#   Author: Tom Videen
#	Use:	Create a summed image in Interfile format

if ($#argv != 3) then
	echo "Usage: image_sum img1,f1 img2,f2 out"
	echo " e.g.: image_sum ct1.img ct2.img ctsum.img"
	echo "   or: image_sum mr1.img,1 mr2.img,-1 mrdif.img"
	exit 1
endif

set infile1 = `echo $1 | awk -F, '{print $1}'`
if (`echo $1 | grep ','` != "") then
	set f1 = `echo $1 | awk -F, '{print $2}'`
else
	set f1 = 1
endif

set infile2 = `echo $2 | awk -F, '{print $1}'`
if (`echo $2 | grep ','` != "") then
	set f2  = `echo $2 | awk -F, '{print $2}'`
else
	set f2 = 1
endif

set outfile = $3

if (! -e ${infile1}) then
	echo "ERROR [image_sum]: ${infile1} does not exist."
	exit 1
endif
if (! -e ${infile2}) then
	echo "ERROR [image_sum]: ${infile2} does not exist."
	exit 1
endif
if (-e ${outfile}) then
	echo "WARNING [image_sum]: ${outfile} will be replaced."
endif

setenv INFILE1 $infile1
setenv INFILE2 $infile2
setenv OUTFILE $outfile
setenv NORMALIZATION_VALUE1 $f1
setenv NORMALIZATION_VALUE2 $f2
setenv IDL_NAME image_sum
setenv RESOURCES plot.xrdb
run_idl
