#!/bin/csh
#	Name:	imgrotate
#	Date:	Jul 2000
#	Author:	Tom Videen
#	Use:	Apply manualreslice (AIR) to an ECAT image

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 2 && $#argv != 5) then
	echo "Usage: imgrotate imgname airname [xdeg ydeg zdeg]"
	echo " e.g., imgrotate rp5428oc1_g3.v rotate.air 0 0 -10"
	echo "  or   imgrotate rp5428ho1_g3.v rotate.air"
	echo ""
	echo " Note: positive angles rotate image CW from vidi's perspective"
	echo "       negative angles rotate image CCW"
	exit 1
endif

set IN   = $1
set AIR  = $2
set YANG = $3
set XANG = $4
set ZANG = $5
set OUT  = r$IN
set INROOT  = a$IN:r
set OUTROOT = r$INROOT
set TMP = /tmp/imgrotate.$$

unalias rm
if ($#argv == 5) then
	if (-e $AIR) then
		echo "Replacing $AIR"
		rm $AIR
	endif
else if (! -e $AIR) then
	echo "ERROR [imgrotate]: missing file $AIR"
	exit 1
endif

${NPG}/bin/img2analyze $IN $INROOT
${NPG}/bin/setanamin $INROOT -1

if ($#argv != 2) then
	echo 0 >! $TMP
	echo 0 >> $TMP
	echo 0 >> $TMP
	echo $XANG >> $TMP
	echo $YANG >> $TMP
	echo $ZANG >> $TMP
	echo 1 >> $TMP
	echo 1 >> $TMP
	echo 1 >> $TMP
	echo n >> $TMP
	echo $INROOT.img >> $TMP
	echo e >> $TMP
	echo $INROOT.img >> $TMP
	echo y >> $TMP
	echo $AIR >> $TMP
	echo n >> $TMP
	echo 1 >> $TMP
	echo n >> $TMP
	echo $OUTROOT.img >> $TMP
	manualreslice < $TMP >& /dev/null
	rm $TMP
else
	echo "RESLICE"
	reslice $AIR $OUTROOT -a $INROOT -k -o -n 1
endif

${NPG}/bin/analyze2img $OUTROOT $OUT

rm $INROOT.img
rm $INROOT.hdr
rm $INROOT.ifh
rm $OUTROOT.img
rm $OUTROOT.hdr

