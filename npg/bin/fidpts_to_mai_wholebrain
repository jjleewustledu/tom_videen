#!/bin/csh
#	Module:	fidpts_to_mai_wholebrain
#	Author:	Tom Videen
#	Date:	Nov 2008
#	Use:	Convert fiducial point MaiAtlas

if ($#argv != 2) then
	echo "Usage:  fidpts_to_mai_wholebrain air pts"
	echo " e.g.,  fidpts_to_mai_wholebrain db250_atlas_to_MaiAtlas db250"
	exit 1
endif

set AIR1 = $1.air
set LIST = $2.pts
set LOC  = $2.loc
set ATL  = $2.atl

if (! -e $AIR1) then
	echo "ERROR [fidpts_to_mai_wholebrain]: missing $AIR1"
	exit 1
endif
if (! -e $LIST) then
	echo "ERROR [fidpts_to_mai_wholebrain]: missing $LIST"
	exit 1
endif

echo fidpts_to_mai_wholebrain $1 $2 >! $LOC
echo fidpts_to_mai_wholebrain $1 $2 >! $ATL

# atlas image (on which fiducial pts are defined)
set axdim = 256
set aydim = 256
set azdim = 128

# MaiAtlas image
set xpix = 0.14545
set ypix = 1.336
set zpix = 0.14545
set mxdim = 1120
set mydim = 39
set mzdim = 936
set mx0 = 483
set my0 = 17
set mz0 = 586

set n = 3
while ($n < 14)
	set line = `head -$n $LIST | tail -1`
	set nam = `echo $line | cut -f1 -d' '`
	set xp = `echo $line | cut -f2 -d' '`
	set yp = `echo $line | cut -f3 -d' '`
	set zp = `echo $line | cut -f4 -d' '`
	set xc = `echo "$axdim - $xp" | bc -l | cut -c1-6`
	set yc = `echo "$aydim - $yp" | bc -l | cut -c1-6`
	set zc = `echo "$azdim - $zp" | bc -l | cut -c1-6`
	echo $xc $yc $zc >! ptmp
	reslice_ucf $AIR1 ptmp rtmp y
	set xc = `head -1 rtmp | cut -f1 -d' '`
	set yc = `head -1 rtmp | cut -f2 -d' '`
	set zc = `head -1 rtmp | cut -f3 -d' '`
	set rxc = `echo "$mxdim - ($xc / $xpix)" | bc -l | cut -c1-6`
	set ryc = `echo "$mydim - ($yc / $ypix)" | bc -l | cut -c1-5`
	set rzc = `echo "$mzdim - ($zc / $zpix)" | bc -l | cut -c1-6`
	echo $nam $rxc $ryc $rzc >> $LOC
	set x0 = `echo "($mx0 - $rxc) * $xpix" | bc -l | cut -c1-8`
	set y0 = `echo "($my0 - $ryc) * $ypix" | bc -l | cut -c1-8`
	set z0 = `echo "($mz0 - $rzc) * $zpix" | bc -l | cut -c1-8`
	echo $nam $x0 $y0 $z0 >> $ATL
	@ n++
end

unalias rm
rm ptmp rtmp 

