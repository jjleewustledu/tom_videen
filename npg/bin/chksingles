#!/bin/csh
# Module: chksingles
# Date:   Jan 2003
# Author: Tom Videen
# Use:    Read uncorrected singles from ECAT (Matrix 7) sinograms
#         (uses hdrEdit)

if ($#argv != 3) then
	echo "Usage:  chksingles file firstfrm lastfrm"
	echo " e.g.,  chksingles /sd0/p1234_567_8b12_de3.S 1 60"
	exit 1
endif

set nname = $1:t
set out  = $nname:r".sgl"
set list = $nname:r".singles"
set tmp  = chksingles.tmp
set num  = chksingles.num
set sng  = chksingles.sng
set ucs  = chksingles.ucs
set tim  = chksingles.tim

echo "Frame  Singles  Total Singles    msec  for $1" >! $num
echo s >! $tmp
echo n >> $tmp
set i = $2
while ($i <= $3)
	echo m >> $tmp
	echo $i >> $tmp
	echo d >> $tmp
	echo "   "$i >> $num
	@ i++
end
echo q >> $tmp
hdrEdit -c -f $1 < $tmp >! $out
echo "" >! $sng
grep "^Uncorrected Singles" $out | cut -f2 -d: >> $sng
echo "" >! $ucs
grep "^Total Avg. Uncorrected Singles" $out | cut -f2 -d: >> $ucs
echo "" >! $tim
grep "^Frame Duration" $out | cut -f2 -d: >> $tim
paste $num $sng $ucs $tim>! $list
cat $list
echo "Singles saved in $list"

unalias rm
rm $tmp
rm $out
rm $num
rm $sng
rm $ucs
rm $tim
exit

