#!/bin/csh
#   Name:   cos_sum_init
#   Author: Tom Videen
#   Date:   Aug 2005
#   Created for: COSS PET Processing
#   Intent: View time-activity curve and determine frames to sum for 40-sec image (Emory)
#   	Activity plotted as mean counts/pixel/sec.

if ($#argv != 4) then
	echo "Usage:  cos_sum_init tracer X Y Z"
	exit 1
endif

set TRACER = $1
set X = $2
set Y = $3
set Z = $4

if (! $?SITE) then
	echo "ERROR: env SITE not defined"
	exit 1
else if (! $?SUBJ) then
	echo "ERROR: env SUBJ not defined"
	exit 1
else if (! $?SCAN) then
	echo "ERROR: env SCAN not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
else if (! $?OO) then
	echo "ERROR: env OO not defined"
	exit 1
else if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
endif

if ($TRACER == 'ho') then
	set FNAME = $STUDY-$HO
else if ($TRACER == 'oo') then
	set FNAME = $STUDY-$OO
else
	echo "ERROR: tracer must be ho or oo"
	exit 1
endif

set VER      = 1.0
set DIAMETER = 80
set NFRAMES  = 20

set PREFIX = cs$SITE-
set GAUSS = _g3
set IN    = $FNAME.v
set LST   = $FNAME.lst
set PRM   = $FNAME.prm
set REG   = $FNAME.reg
set RGT   = $FNAME.rgt
set PLT   = $FNAME.plt
set START = $FNAME.start
set DURA  = $FNAME.duration
set CNTS  = $FNAME.cnts
set TMP   = cos_sum_init$$.tmp

if (! -e $IN) then
	echo "ERROR: file not found: $IN"
	exit 1
endif

# Compare choice of Z to image size

set PLANE_SEP = `imghdr $IN | grep Plane_separation | cut -f2 -d= | cut -f1 -dc`
set NPLANES = `imghdr $IN | grep slices | cut -f1 -ds`
set RADIUS = `echo $DIAMETER / 20 / $PLANE_SEP | bc`
set MAX_Z = `echo $NPLANES - $RADIUS - 1 | bc`
set MIN_Z = `echo $RADIUS + 2 | bc`
if ($Z < $MIN_Z) then
    echo ">>> ================================================================================" 
	echo ">>> WARNING: Z-coordinate may be too low and place the VOI partly outside the image"
	echo ">>>          Z should be between $MIN_Z and $MAX_Z"
    echo ">>> ================================================================================" 
else if ($Z > $MAX_Z) then
    echo ">>> ================================================================================" 
	echo ">>> WARNING: Z-coordinate may be too high and place the VOI partly outside the image"
	echo ">>>          Z should be between $MIN_Z and $MAX_Z"
    echo ">>> ================================================================================" 
endif

echo "==========================================================================" >>! $LOGFILE
echo "cos_sum_ho  Version:" $VER `whoami` `date` >> $LOGFILE
echo $0 $1 $2 $3 >> $LOGFILE

imgfrms $IN | cut -c1-3 >! $TMP
set FIRST = `head -5 $TMP | tail -1`
set LAST  = `tail -1 $TMP`
set NFRMS = `echo $LAST - $FIRST + 1 | bc`
set MISSING = `echo $FIRST - 1 | bc`
echo "$IN    Frames $FIRST to $LAST"

# Create lst, prm, and reg files:

echo "$SUBJ $SCAN" >! $LST

echo "species:    $PREFIX"	>! $PRM
if ($TRACER == 'ho') then
	echo "tracer:     -ho"		>> $PRM
else if ($TRACER == 'oo') then
	echo "tracer:     -oo"		>> $PRM
endif
echo "extension:  v"		>> $PRM
echo "imgfilter:  none"		>> $PRM
echo "voi:        new_sphere $DIAMETER" >> $PRM

echo "30  voifile"                  >! $REG
echo "center of brain for $STUDY"     >> $REG
echo "1"                            >> $REG
echo "center_of_brain   $X  $Y  $Z" >> $REG

echo 0 >! $TMP
chklist_nosxr $PRM $LST 1
voistat $PRM $LST $RGT $REG < $TMP

set SIZE = `wc -l $RGT | cut -f1 -dc`
set NN = `echo $SIZE - 2 | bc`
tail -$NN $RGT | head -$NFRMS | cut -c2-12 >! $START
tail -$NN $RGT | head -$NFRMS | cut -c14-24 >! $DURA
tail -$NN $RGT | head -$NFRMS | cut -c25-40 >! $CNTS
set TIME = `cat $START`
set DURATION = `cat $DURA`
set COUNTS = `cat $CNTS`

unalias rm
if (-e $PLT) rm $PLT
touch $PLT
set i = 1
while ($i <= $NFRMS)
	set MEAN = `echo $COUNTS[$i] / $DURATION[$i] | bc -l`
	echo $MEAN | cut -c1-12 >> $PLT
	@ i++
end

set STRING = "$DIAMETER-mm sphere at $X,$Y,$Z"
cos_plot $PLT $FIRST $LAST $FNAME.v $STRING

echo "$IN    Frames $FIRST to $LAST"
set STR = 'y'
prompt1:
echo "================================================================="
echo "First frame of 40-sec integration?"
set FIRST = `line`
set LAST = `echo $FIRST + $NFRAMES - 1 | bc`
set T1 = $TIME[$FIRST]
set T2 = $TIME[$LAST]
set T3 = $DURATION[$LAST]
set TOT = `echo $T2 - $T1 + $T3 | bc -l`
prompt2:
echo "Integration time for frames $FIRST-$LAST = $TOT sec"

if (-e idl.ps) rm idl.ps
rm $LST
rm $PRM
rm $RGT
rm $START
rm $DURA
rm $CNTS
rm $REG
rm $PLT
rm $TMP

