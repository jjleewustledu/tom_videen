#!/bin/csh
#	Name:	dynreslice
#	Date:	Jun 2000
#	Author:	Tom Videen
#	Use:	Apply reslice (AIR) to a dynamic PET image

if ($#argv != 2) then
	echo "Usage:  dynreslice image airfile"
	echo " e.g.,  dynreslice rp3000fd rp3000fdsum_to_p3000mprc"
	exit 1
endif

set IN = $1
set AIR = $2
set OUT = c$IN
set TMP = ${IN}tmp

if (! -e $IN.img) then
	echo "ERROR [dynreslice]: $IN.img does not exist"
	exit 1
endif
if (! -e $AIR.air) then
	echo "ERROR [dynreslice]: $AIR.air does not exist"
	exit 1
endif
if (-e $OUT.ifi) echo "Replacing: $OUT.ifi"

# Remove single frames from image and convert to Analyze format

set NFRMS = `imghdr $IN.img | grep "Bed Position" | cut -c12-14`
single_frames $IN.img $TMP i 1 $NFRMS

# Reslice and append to ifi image

unalias rm
set i = 1
while ($i <= $NFRMS)
	imgreslice a${TMP}$i $AIR
	if ($i == 1) then
		analyze2if ra${TMP}$i $OUT $NFRMS
	else
		analyze2if ra${TMP}$i
		cat ra${TMP}$i.ifi >> $OUT.ifi
		rm ra${TMP}$i.ifi
		rm ra${TMP}$i.ifh
	endif
	rm a${TMP}$i.hdr
	rm a${TMP}$i.img
	@ i++
end

