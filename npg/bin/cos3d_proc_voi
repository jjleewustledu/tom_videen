#!/bin/csh
#	Name:	cos3d_proc_voi
#	Author:	Tom Videen
#	Date:	Dec 2004
#	Created for: COSS PET Processing
#	Intent: Measure VOI activity and compute left/right ratio

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
endif

set LOG = ${STUDY}-2d3d.log
set DIAM  = 19.0
set GAUSS = _g3
set PID = $STUDY-
set VOI = ${PID}mca.voi

set HO3  = ${PID}ho3$GAUSS.t88
set OO3  = ${PID}oo3$GAUSS.t88
set RGT3 = ${PID}oo3ho3-mca.rgt

if ($#argv == 1) then
	set HO1  = 2d/${PID}ho$1$GAUSS.t88
	set OO1  = 2d/${PID}oo$1$GAUSS.t88
	set RGT1 = 2d/${PID}oo${1}ho${1}-mca.rgt
else
	set HO1  = 2d/${PID}ho1$GAUSS.t88
	set OO1  = 2d/${PID}oo1$GAUSS.t88
	set RGT1 = 2d/${PID}oo1ho1-mca.rgt
endif

if (! -e $VOI) then
	echo "ERROR [cos3d_proc_voi]: $VOI not found"
	exit 1
else if (! -e $OO1) then
	echo "ERROR [cos3d_proc_voi]: $OO1 not found"
	exit 1
else if (! -e $HO1) then
	echo "ERROR [cos3d_proc_voi]: $HO1 not found"
	exit 1
else if (! -e $OO3) then
	echo "ERROR [cos3d_proc_voi]: $OO3 not found"
	exit 1
else if (! -e $HO3) then
	echo "ERROR [cos3d_proc_voi]: $HO3 not found"
	exit 1
endif

set today = `date +'%d %h 20%y'`
echo "$PID  $today" >! $LOG
echo "============================" >>! $LOG
echo "2D Ratio" >>! $LOG
voicoss.bin $OO1 $HO1 $VOI $RGT1 $DIAM `date` `whoami`
set m = `wc $RGT1 | cut -c1-8`
set n = `echo "$m - 3" | bc`
head -$n $RGT1 | tee -a $LOG
echo "============================" >>! $LOG
echo "3D Ratio" >>! $LOG
voicoss.bin $OO3 $HO3 $VOI $RGT3 $DIAM `date` `whoami`
set m = `wc $RGT3 | cut -c1-8`
set n = `echo "$m - 3" | bc`
head -$n $RGT3 | tee -a $LOG
echo "============================" >>! $LOG

