#!/bin/csh
#	Name:   cos_qc
#	Author: Tom Videen
#	Date:   Mar 2003
#	Created for: COSS PET Processing
#	Intent: Quick quality control check of COSS PET Processing

set DEPTH = `xwininfo -root | grep Depth | cut -f2 -d:`
setenv RESOURCES vidi_$DEPTH.xrdb
setenv RESOURCES vidi.xrdb
if (-e cos-*) source cos-*
if (! $?STUDY) then
	echo "You must define: setenv STUDY and setenv SCAN"
	exit
endif

set HO    = $STUDY-ho$SCAN
set OO    = $STUDY-oo$SCAN
set LOG   = $STUDY-$SCAN-processing.log

if (! -e $LOG) then
	echo "ERROR: cannot find $LOG"
	exit 1
endif
head -10 $LOG

if ($#argv > 0) goto viewonly

echo ">>> Check Start Frame and Duration for Water"
echo "======================================================"
grep HO: $LOG
set first = `imgfrms $HO.v | head -5 | tail -1 | cut -c1-3`
set last  = `imgfrms $HO.v | tail -1 | cut -c1-3`
cos_plot $HO.plt $first $last $HO.v

echo ">>> Check Start Frame and Duration for Oxygen"
echo "======================================================"
grep OO: $LOG
set first = `imgfrms $OO.v | head -5 | tail -1 | cut -c1-3`
set last  = `imgfrms $OO.v | tail -1 | cut -c1-3`
cos_plot $OO.plt $first $last $OO.v
if (-e idl.ps) rm idl.ps

viewonly:
echo ">>> Check Coregistration of water to oxygen"
echo ">>>   and Coregistration of water to atlas"
echo "======================================================"
vidiq ${OO}_g3.t88 ${HO}_g3.t88 36 3

echo ">>> Check Radiologist's drawings"
echo ">>> Check VOIs on HO, OO and OO/HO ratio"
echo "======================================================"
cos_pic

echo ">>> VOI values in Logfile"
tail -28 $LOG

