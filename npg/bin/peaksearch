#!/bin/csh
#	Name:	peaksearch
#	Date:	Jul 1999
#	Author:	Tom Videen
# 	Use:	Identify distinct peaks in an ECAT image

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv < 5 || $#argv > 6) then
	echo "Usage:  peaksearch image mask output distance(mm) cutoff [scaler]"
	echo " e.g.:  peaksearch p5000oef1.v p5000mask.v p5000oef1.pk1 14.0 0.01"
	echo "  or :  peaksearch p5000oef1.v p5000mask.v p5000oef1.pk1 14.0 0.01 0.001"
	echo " "
	echo "Note that distance should be 2x radius used in smooth3d to avoid overlapping peaks."
	echo "A negative cutoff will search for the lowest positive peaks up to the cutoff."
	exit 1
endif

set in 		= $1
set mask	= $2
set out		= $3
set dist	= $4
set cutoff	= $5
if ($#argv == 6) then
	set scaler = $6
else
	set scaler = 1
endif

set EFILES = "$in $mask"
set STATUS = 0
foreach i ($EFILES)
    if (! -e $i) then
        echo "ERROR [peaksearch]: $i does not exist"
        set STATUS = 1
    endif
end
if ( $STATUS != 0) exit 1
if (-e $out) then
    echo "ERROR [peaksearch]: $out already exists"
    exit 1
endif

set now = `date`
set uid = `whoami`

${NPG}/bin/peaksearch.bin $in $mask $out $dist $cutoff $scaler $now $uid
