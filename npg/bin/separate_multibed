#!/bin/csh
# Name:		separate_multibed
# Date:		Oct 2001
# Author:	Tom Videen
# Intent:	Separate frames of 953 image acquired with scanmultibed

set OK = 0
if ($#argv != 4) then
	echo "Usage: separate_multibed input_file output_root nframes npositions"
	echo " e.g., separate_multibed m822nmb1.scn m822nb 8 4"
	echo " (nframes = number of frames at each position)"
	exit 1
endif

set infile = $1
set outroot = $2
set nfrms = $3
set npos = $4

if (! -e $infile) then
	echo "ERROR: missing $infile"
	exit
endif

set pos = 1
while ($pos <= $npos)
	set outfile = $outroot$pos.scn
	if (-e $outfile) then
		echo "ERROR: $outfile already exists"
		exit
	endif
	@ pos++
end

set num = 1
set frm = 1
while ($frm <= $nfrms)
	set pos = 1
	while ($pos <= $npos)
		set outfile = $outroot$pos.scn
		echo "Copying $infile Frame $num (frame $frm bed $pos) to $outfile Frame $frm"
		set pln = 1
		while ($pln <= 31)
			matcopy $infile,$num,$pln,1 $outfile,$frm,$pln,1
			if ($status != $OK) then
				echo "ERROR copying $infile frame $num plane $pln"
				exit 1
			endif
			@ pln++
		end
		@ num++
		@ pos++
	end
	@ frm ++
end

