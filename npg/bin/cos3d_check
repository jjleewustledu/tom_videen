#!/bin/csh
#   Name:   cos3d_check
#   Author: Tom Videen
#   Date:   Nov 2003
#   Created for: 3D COSS PET Processing
#   Intent: Check size, headers and frames

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
endif

set files = `ls $STUDY*.v`
set count = `echo $files | wc -w`
if ($count == 0) then
	echo "ERROR: no files found named $STUDY*.v"
	echo "Copy, convert, and rename files if necessary."
	exit 1
endif
chmod 660 $files

set pettype = `imghdr $files[1] | grep ECAT | head -1 | cut -f2 -d' '`
if ($pettype == 962) then
	set fsize = 123895296
else if ($pettype == 961) then
	set fsize = 30813184
else if ($pettype == 951) then
	set fsize = 61932032
else if ($pettype == 9110) then
	set pettype = 'GE Advance'
	set fsize = 69923328
else 
	set fsize = 'unknown'
endif
echo "$pettype  Normal 20-frame File Size = $fsize"

# Check File Size
# ECAT 921 = (Emory, Michigan, Case)
# ECAT 951 = 61932032
# ECAT 961 = 92438016
# ECAT 962 = 123895296
# LSO ACCEL = (NCPC)
# GE Advance = (Seattle, Mayo, Wisc, Duke, WFU)
# GE 4096 = (Iowa)
echo "==========================================================================" | tee -a $LOGFILE
ls -l $files | tee -a $LOGFILE
echo "File sizes" | tee -a $LOGFILE
echo "=========================================================================="
set data = `line`

# Check Image Headers
foreach i ($files)
	imghdr $i | tee -a $LOGFILE
	echo "Header for $i" | tee -a $LOGFILE
	echo "=========================================================================="
	set data = `line`
end

# Check Image Frames
foreach i ($files)
	set first = `imgfrms $i | head -5 | tail -1 | cut -c1-3`
	set last = `imgfrms $i | tail -1 | cut -c1-3`
	imgfrms $i | tee -a $LOGFILE
	echo "Frames $first $last for $i" | tee -a $LOGFILE
	echo "=========================================================================="
	set data = `line`
end

