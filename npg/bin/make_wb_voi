#!/bin/csh
#	Name:	make_wb_voi
#	Date:	Mar 2002
#	Author:	Tom Videen
#	Intent:	Mask slices of a tissue image and create a voi file

if ($#argv != 3) then
	echo "Usage: make_wb_voi image slices threshold"
	echo " e.g.: make_wb_voi rp5000mask.v 7to25 1"
	exit 1
endif

set FNAME = $1
set ROOT = $FNAME:r
set EXT = $FNAME:e
set TOP = `echo $2 | nawk '{FS="to";print $1}'`
set BOT = `echo $2 | nawk '{FS="to";print $2}'`
set THRESH = $3

if (! -e $FNAME) then
	echo "ERROR: Cannot find $FNAME"
	exit 1
endif
set LAST = `imghdr $FNAME | grep slices | cut -f1 -d" "`
set MAX = `imghdr $FNAME | grep Max | cut -f3 -d" "`

if ($TOP > $BOT) then
	echo "ERROR: $TOP > $BOT"
	exit 1
endif
if ($BOT > $LAST) then
	echo "ERROR: $BOT > $LAST (number of slices)"
	exit 1
endif

set S1 = `echo "$TOP - 1" | bc`
set S2 = `echo "$BOT + 1" | bc`
set WB  = ${ROOT}_${TOP}to${BOT}

if (-e $WB.voi) then
	echo "ERROR: VOI file already exists $WB.voi"
	exit 1
endif

maskslc $FNAME   $WB.$EXT 1to${S1}
maskslc $WB.$EXT $WB.$EXT ${S2}to$LAST
voimake $WB.$EXT $WB.voi whole_brain $THRESH $MAX
echo "      All pixels ${THRESH}-${MAX} over planes ${TOP}-${BOT}"
