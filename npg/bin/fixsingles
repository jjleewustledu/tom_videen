#!/bin/csh
# Module: fixsingles
# Date:   Jan 2003
# Author: Tom Videen
# Use:  Repair missing singles in ECAT (Matrix 7) sinograms
#       using interpolated singles from the surrounding frames
#       (uses hdrEdit)
# Note: Edit fixsingles.awk and set nbuckets for your scanner.

if ($#argv != 3) then
	echo "Usage:  fixsingles file firstfrm lastfrm"
	echo " e.g.,  fixsingles /sd0/p1234_5657_8b12_de3.S 1 60"
	exit 1
endif

set file = $1
set nname = $file:t
set out = /tmp/$nname:r".dump"
set cmd = /tmp/$nname:r".cmd"

echo s >! $cmd
echo n >> $cmd
set i = $2
while ($i <= $3)
	echo m >> $cmd
	echo $i >> $cmd
	echo d >> $cmd
	@ i++
end
echo q >> $cmd
echo "Header dump commands created in $cmd"

hdrEdit -c -f $file < $cmd >! $out
echo "Headers dumped to $out"

fixsingles.awk $out >! $cmd
echo "Header edit commands created in $cmd"

hdrEdit -c -f $file < $cmd >! /dev/null
echo "Singles repaired in $file"
#rm $out
#rm $cmd
exit

