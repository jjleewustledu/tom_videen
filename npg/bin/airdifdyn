#!/bin/csh
# Program:	airdifdyn
# Author:	Tom Videen
# Date:		Oct 1998
# Usage:	Compare air files created by aligndyn
# 	Requires:
# 		1) a reference image has been created (masked, filtered image defining brain)
#		2) combined air files aligning each image to the first
#		3) null air file
#	Creates:
#		1) log file showing net movement between each scan and first (using reference image)

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 4) then
	echo "Usage:  airdifdyn root frms ref nullair"
	echo " e.g.,  airdifdyn m500mb 3to44 m500mb_mask.img m500mb_null.air"
	exit 1
endif

set ROOT  = $1
set FIRST = `echo $2 | nawk '{FS="to";print $1}'`
set LAST  = `echo $2 | nawk '{FS="to";print $2}'`
set BRAIN = $3
set NULLAIR = $4
set ALOG  = ${ROOT}_airdif.log
echo `date` >! $ALOG

set i = $FIRST
while ($i < $LAST)
	set j = `echo "$i + 1; quit" | bc`
	set NETAIR = ${ROOT}_${FIRST}vs${j}.air
	echo "-- airdif $NETAIR $NULLAIR $BRAIN >> $ALOG"
    ${NPG}/bin/airdif $NETAIR $NULLAIR $BRAIN | grep Mean >> $ALOG
	@ i++
end
echo "[airdifdyn] Complete at `date`"

