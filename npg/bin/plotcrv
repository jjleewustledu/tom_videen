#!/bin/csh
#   Module: plotcrv
#   Date:   3/25/92
#   Author: Dave Carpenter
#   Use:    Plot crv files
#	Note that this script will plot any desired number of .CRV files
#		PROVIDED that they contain 90 bins.  Otherwise things get screwy.

set argno = $#argv
if ($argno < 1) then
	echo "Usage: plotcrv <wildcard>"
	echo " e.g.: plotcrv p6000*crv"
	exit
endif

if (-e $HOME/.Xdefaults) then
	/usr/openwin/bin/xrdb -merge $HOME/.Xdefaults
endif

startover:

echo "Do you wish to:	(1)  Plot curves on screen or"
echo "		(2)  Print them on your default printer?"

set ans1 = $<

if (($ans1 != 1) && ($ans1 != 2)) then
	goto startover
endif

# convert .CRV files to gnuplot format, count no. of files
# First 2 points are forced to 0 to eliminate artifact (TOV)

foreach f ($argv)
	echo "1.000000 0 0" >! /tmp/$$.$f
	echo "2.000000 0 0" >> /tmp/$$.$f
	sed -e '1,4 d' $f >> /tmp/$$.$f
	sed
end

#create gnuplot commands, run gnuplot

switch ($ans1)

case 1:
	echo set data style lines >>! /tmp/$$.gnuplot
	chmod g+w /tmp/$$.*
	foreach f ($argv)
		echo plot \"/tmp/$$.$f\" using 1:2 title \"$f\" >>! /tmp/$$.gnuplot
		echo pause -1 \"hit return to continue\" >>! /tmp/$$.gnuplot
	end
	gnuplot /tmp/$$.gnuplot
	breaksw
case 2:
	echo set terminal postscript >>! /tmp/$$.gnuplot
	echo set output \"/tmp/$$.printfile\" >>! /tmp/$$.gnuplot
	echo set data style lines >>! /tmp/$$.gnuplot
	chmod g+w /tmp/$$.*
	foreach f ($argv)
		echo plot \"/tmp/$$.$f\" using 1:2 title \"$f\" >>! /tmp/$$.gnuplot
	end
	gnuplot /tmp/$$.gnuplot
	lpr /tmp/$$.printfile
	breaksw
endsw
/bin/rm /tmp/$$*
exit
