#	Date:	Mar 2005
#	Author:	Tom Videen
#	Intent:	Create text file for data import into Excel
#	Import into Excel with:
#		select first PID cell
#			Alt-d,d,t,m,d
#			Return
#			Alt-s,f
#		move cursor over Import Data window
#			Alt-r,a,o
#			Return
#			Return
#	Output1:
#		PID Shift Gluc CBF CBV OEF CMRO2 k21 k12 k32 k23 Metindex
#	Output2: input file for fdg_calc and/or cmrglucose where:
#		K1 = K-21 * CBV / 100
#		k2 = K-12
#		k3 = K-32
#		k4 = K-23
# ----------------------------------------------
if ($#argv != 7) then
	echo "Usage: excel_fdg ID gluc shift fit pvc_fit prms pvc_prms"
	echo " e.g., excel_fdg 6506 4.27 11 fit.txt fit_pvc.txt fitprms.dat fitprms_pvc.dat"
	exit
endif

set ID       = $1
set GLUC     = $2
set SHIFT    = $3
set FIT      = $4
set FIT_PVC  = $5
set PRMS     = $6
set PRMS_PVC = $7
set PID = p$ID

# Uncorrected -- without PVC
# --------------------------
set valu = `tail -1 metcalc.log | cut -c1-28,38-`
set k1u = `cat ${PID}wba.log | grep "K-21" | cut -c15-22`
set k2u = `cat ${PID}wba.log | grep "K-12" | cut -c15-25`
set k3u = `cat ${PID}wba.log | grep "K-32" | cut -c15-25`
set k4u = `cat ${PID}wba.log | grep "K-23" | cut -c15-25`
set metu = `cat ${PID}wba.log | grep "FDG MET INDEX" | cut -c15-22`
echo $ID $SHIFT $GLUC $valu $k1u $k2u $k3u $k4u $metu >! $FIT

set cbvu = `echo $valu | cut -f2 -d" "`
set K1u = `echo "$k1u * $cbvu / 100" | bc -l | cut -c1-10`
echo $K1u $k2u $k3u $k4u >! $PRMS

# Corrected -- with PVC
# --------------------------
set valc = `tail -1 metcalc_pvc.log | cut -c1-28,38-`
set k1c = `cat ${PID}wb_pvca.log | grep "K-21" | cut -c15-22`
set k2c = `cat ${PID}wb_pvca.log | grep "K-12" | cut -c15-25`
set k3c = `cat ${PID}wb_pvca.log | grep "K-32" | cut -c15-25`
set k4c = `cat ${PID}wb_pvca.log | grep "K-23" | cut -c15-25`
set metc = `cat ${PID}wb_pvca.log | grep "FDG MET INDEX" | cut -c15-22`
echo $ID $SHIFT $GLUC $valc $k1c $k2c $k3c $k4c $metc >! $FIT_PVC

set cbvc = `echo $valc | cut -f2 -d" "`
set K1c = `echo "$k1c * $cbvc / 100" | bc -l | cut -c1-10`
echo $K1c $k2c $k3c $k4c >! $PRMS_PVC

