#!/bin/csh
#	Name:	dcmtilt
#	Date:	May 2006
#	Author:	Tom Videen
#	Use:	Get Gantry Tilt from DICOM images 

if ($#argv != 1) then
	echo "Usage:  dcmtilt filename"
	echo " e.g.,  dcmtilt 00123.dcm"
	echo "  or,   dcmtilt 123"
	exit 1
endif

set file = $1
if (! -e $file) then
	if ($1 < 10 ) then
    	set prefix = "0000"
	else if ($1 < 100) then
    	set prefix = "000"
	else if ($1 < 1000) then
    	set prefix = "00"
	else if ($1 < 10000) then
    	set prefix = "0"
	else 
    	set prefix = ""
	endif
	set file = "$prefix$1.dcm"
	if (! -e $file) then
		echo "ERROR: cannot find $1 or $file"
		exit 1
	endif
endif

set tmp  = dcmtilt$$.tmp
dcm_dump_element -t 0008 0033 $file $tmp >& /dev/null
if ($status == 0) then
	set type = "-t"
else 
	dcm_dump_element 0008 0033 $file $tmp >& /dev/null
	if ($status == 0) then
		set type = ""
	else
		echo "$file is not DICOM format"
		exit 1
	endif
endif

set key = "0018 1120"
dcm_dump_element $type $key $file $tmp
set GT = `cat $tmp`
echo "Gantry Tilt = $GT"
unalias rm
rm $tmp 

