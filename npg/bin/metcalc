#!/bin/csh
#	Program:	metcalc
#	Author:		Tom Videen
#	Date:		Apr 2004

if (! $?NPG) setenv NPG /usr/local/npg

set argc = $#argv
if ($argc != 2 && $argc != 6 && $argc != 7) then
	echo "Usage:  metcalc HOcounts COcounts OOcounts HOhdr COhdr OOhdr [output]"
	echo " e.g.:  metcalc 1536.37 478.78 383.72 p2229ho1.hdr p2229oc1.hdr p2229oo1.hdr"
	echo "  or,   metcalc 1536.37 478.78 383.72 p2229ho1.hdr p2229oc1.hdr p2229oo1.hdr p2229_metcalc.log"
	echo "  or,   metcalc 1536.37 p2229ho1.hdr"
	exit 1
endif

if ($argc == 2) then
	set HO = $1
	set HOHDR = $2
	if (! -e $HOHDR) then
		echo "ERROR: HO hdr file does not exist:  $HOHDR"
		exit 1
	endif
else if ($argc >= 6) then
	set HO = $1
	set CO = $2
	set OO = $3
	set HOHDR = $4
	set COHDR = $5
	set OOHDR = $6
	if (! -e $HOHDR) then
		echo "ERROR: HO hdr file does not exist:  $HOHDR"
		exit 1
	endif
	if (! -e $COHDR) then
		echo "ERROR: CO hdr file does not exist:  $COHDR"
		exit 1
	endif
	if (! -e $OOHDR) then
		echo "ERROR: OO hdr file does not exist:  $OOHDR"
		exit 1
	endif
endif
if ($argc == 7) then
	set OUT = $7
else
	set OUT = metcalc.log
endif
if ( -e $OUT) echo " Replacing: $OUT"

set TMP = metcalc$$.tmp

if ($argc == 2) then
	${NPG}/bin/metcalc.bin $HO $HOHDR >! $TMP
	echo "metcalc $HO $HOHDR" >! $OUT
else
	${NPG}/bin/metcalc.bin $HO $CO $OO $HOHDR $COHDR $OOHDR >! $TMP
	echo "metcalc $HO $CO $OO $HOHDR $COHDR $OOHDR" >! $OUT
endif
tail -3 $TMP >> $OUT
tail -3 $TMP
unalias rm
rm $TMP

