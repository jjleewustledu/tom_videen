#!/bin/csh
#	Name:	clinical_dbs_paths
#	Author:	Tom Videen
#	Date:	Nov 2009
#	Intent:	Create subdirectories and copy command files to them
#	Created for: Processing Clinical DBS images

if ($#argv != 1) then
    echo "Usage:  clinical_dbs_paths dbid"
    echo " e.g.,  clinical_dbs_paths db100"
    exit 1
endif

set DB = $1
set DDIR   = /data/jsp/human/DBS/$DB
if (! -e $DDIR) then
	echo "ERROR [clinical_dbs_paths]: missing path: $DDIR"
    exit 1
endif

set PROC   = /data/jsp/human/DBS/Processing/Clinical
set CTDIR  = $DDIR/CT_clinical
set MRDIR  = $DDIR/MR_clinical
set ALDIR  = $DDIR/Align
set ATDIR  = $DDIR/Atlas
set MAIDIR = $DDIR/Mai_compute/Clin_CT

set PDIRS = ($CTDIR $CTDIR/Raw $MRDIR $MRDIR $ALDIR $ATDIR $DDIR/Mai_compute $MAIDIR)
foreach i ($PDIRS)
	echo $i
	if (! -e $i) then
		mkdir $i
		chmod 770 $i
		chgrp jsp $i
	endif
end

# Copy command files to folders

if (! -e $DDIR/Setup_Clinical_$DB) then
	sed s/db123/$DB/g $PROC/Setup_Clinical >! $DDIR/Setup_Clinical_$DB
endif

if (! -e $CTDIR/Raw/CT_conversion_$DB) then
	sed s/db123/$DB/g $PROC/CT_conversion >! $CTDIR/Raw/CT_conversion_$DB
endif

if (! -e $CTDIR/CT_contacts_$DB) then
	sed s/db123/$DB/g $PROC/CT_contacts >! $CTDIR/CT_contacts_$DB
endif

if (! -e $MRDIR/MR_conversion_$DB) then
	sed s/db123/$DB/g $PROC/MR_conversion >! $MRDIR/MR_conversion_$DB
endif

if (! -e $ALDIR/Align_ct2tse_$DB) then
	sed s/db123/$DB/g $PROC/Align_ct2tse >! $ALDIR/Align_ct2tse_$DB
endif

if (! -e $ALDIR/Manreslice_ct_$DB) then
	sed s/db123/$DB/g $PROC/Manreslice_ct >! $ALDIR/Manreslice_ct_$DB
endif

if (! -e $ALDIR/Align_cor2tra_tse_$DB) then
	sed s/db123/$DB/g $PROC/Align_cor2tra_tse >! $ALDIR/Align_cor2tra_tse_$DB
endif

if (! -e $ALDIR/Manreslice_tse_$DB) then
	sed s/db123/$DB/g $PROC/Manreslice_tse >! $ALDIR/Manreslice_tse_$DB
endif

if (! -e $ATDIR/Align_to_atlas_initial_$DB) then
	sed s/db123/$DB/g $PROC/Align_to_atlas_initial >! $ATDIR/Align_to_atlas_initial_$DB
endif

if (! -e $MAIDIR/Reslice_to_Mai_clin_ct_$DB) then
	sed s/db123/$DB/g $PROC/Reslice_to_Mai_clin_ct >! $MAIDIR/Reslice_to_Mai_clin_ct_$DB
endif

