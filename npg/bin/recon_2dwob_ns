#! /bin/csh
#	Name:	recon_2dwob_ns
#	Author:	Tom Videen
#	Date:	Jan 2000
#	Intent:	Reconstruct an emission scan without scatter correction

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv < 7 || $#argv > 8) then
	echo "Usage: recon_2dwob_ns image scn atn nrm filter cutoff zoom [frame]"
	echo "	image    = output image (.img)"
	echo "	scn      = scan (.scn)"
	echo "	atn      = attenuation (.atn)"
	echo "	nrm      = normalization (.nrm)"
	echo "	filter   = 1-6 (1=ramp)"
	echo "	cutoff  <= 0.5"
	echo "	zoom     = image zoom (1.0 - )"
	echo "	frame    = frame number (default=1)"   
	echo
	echo "Specify all files with complete path & name but no extensions."
	echo "  e.g., recon_2dwob_ns p2000ho1 p2000ho1 p2000tr1_wob normalwob_940130 1 .5 1.875"
	exit 1
endif

set imageFile = $1.img
set recFile   = ${imageFile}.rec
set scanFile  = $2.scn
set attenFile = $3.atn
set normFile  = $4.nrm
set filter = $5
set cutoff = $6
set zoom   = $7
if ($#argv < 8) then
	set frameNumber = 1
else
	set frameNumber = $8
endif
set gate = 1
set data = 0
set bed = 0
set xoff = -0.204
set yoff = +0.592
set rot = 0.
set size = 128
set decayCor = 0
set debug = 0
set scatterCor = 0
set sf = 0
set slope = 0
set arcCor = 1
set intrinsicTilt = 1
set priority  = 0
set filt_slope = 0

if (! -e ${scanFile}) then
  echo "ERROR: cannot find ${scanFile}"
  exit 1
endif
if (! -e ${attenFile}) then
  echo "ERROR: cannot find ${attenFile}"
  exit 1
endif
if (! -e ${normFile}) then
  echo "ERROR: cannot find ${normFile}"
  exit 1
endif

echo "Reconstructing ${imageFile}"
set plane = 1
while ( $plane <= 31 )
    set ARGS  = (${scanFile},${frameNumber},$plane,${gate},${data},${bed} \
	    ${size},${zoom},${xoff},${yoff},${rot},${decayCor},${scatterCor},${arcCor},${intrinsicTilt}  \
	    ${imageFile},${frameNumber},$plane,${gate},${data},${bed}  \
	    ${filter},${cutoff},0,${filt_slope},0,${sf},${slope} \
	    -a ${attenFile},1,$plane,${gate},${data},${bed} \
	    -n ${normFile},1,$plane,${gate},${data},${bed})
    ${NPG}/bin/bkproj_sun $ARGS
    @ plane++
end

echo "rec ${imageFile} `date` `whoami` `hostname` `uname`" >! ${recFile}
echo "recon_2dwob_ns $1 $2 $3 $4 $5 $6 $7 $8" >> ${recFile}
ident ${NPG}/bin/bkproj_sun | grep bkproj_sun.c | cut -f2- -d: >> ${recFile}
if (`echo ${scanFile} | grep rn` == ${scanFile}) then
	ident ${NPG}/bin/rnwproc | grep rnwproc.c | cut -f2- -d: >> ${recFile}
endif
echo "endrec" >> ${recFile}
exit
