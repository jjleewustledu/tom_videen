#!/bin/csh
# Program:	alignlst
# Author:	Tom Videen
# Date:		May 1999
# Usage:	Align all images in a list to a specified standard

set usage	= "Usage: alignlst prm lst ref [-u] [-r]"
if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv < 3) then
	echo "$usage"
	echo "	ref = reference scan number"
	echo "	-u  = unidirectional alignment to standard (default is bidirectional)"
	echo "	-r  = use ratios for cost function (default is least squares)"
	exit(1)
endif

# Get first 3 parameters

set prm = $1
set lst = $2
set ref = $3
set uni = 1
set fit = 2
shift argv
shift argv
shift argv
top:	 
if ($#argv > 0) then
	switch ("$argv[1]")
	case -u:
		set uni = 0
		shift argv
		goto top
	case -r:
		set fit = 1
		shift argv
		goto top
	case -?*:
		echo "ERROR [alignlst]: illegal option $argv[1]"
		echo "$usage"
		exit (1)
	endsw
endif

if (! -e $prm) then
	echo "ERROR [alignlst]: Parameter file ${prm} not found"
	exit 1
endif
if (! -e $lst) then
	echo "ERROR [alignlst]: Imagelist file ${lst} not found"
	exit 1
endif 

${NPG}/bin/alignlst.bin $prm $lst $ref $uni $fit

