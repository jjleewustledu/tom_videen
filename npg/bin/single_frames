#!/bin/csh
#	Name:	single_frames
#	Date:	Oct 1999
#	Author: Tom Videen
#	Use:	Separate individual frames of a dynamic ECAT image and convert to Analyze
#	History:
#		08-Oct-2008	- pass starttime to sumimgframes
#		05-May-2009	- pass starttime = -1 to sumimgframes to prevent decay correction

if ($#argv != 5) then
	echo "Usage:  single_frames inputfile output_root output_extension firstfrm lastfrm"
	echo " e.g.:  single_frames m500nmb1.img m500mb i 1 44"
	exit 1
endif

set in   = $1
set root = $2
set ext  = $3
set ff   = $4
set lf   = $5
unalias rm

set frm = $ff
while ($frm <= $lf)
	set aout = a$root$frm
	set out = $root$frm.$ext
	if (-e a$out.img) then
		echo "-- a$out.img already exists"
	else
		if (-e $out) then
			echo "-- $out already exists"
		else
			sumimgframes $in $out $frm $frm -1
		endif
        img2analyze $out $aout
        rm $aout.ifh
	endif
	@ frm++
end
