#!/bin/csh
#	Module:	imgdiv
#	Date:	Jan 1997
#	Author:	Tom Videen
#	Use:	Create a ratio image

if ($#argv != 3) then
	echo "Usage:  imgdiv img1,f1 img2,f2 out"
	echo "computes:  (f1 * img1)/(f2 * img2)"
	echo " e.g.:  imgdiv p5000ho1.v p5000ho2.v p5000ratio.v"
	echo "   or:  imgdiv p5000ho1.v,.5 p5000ho2.v,2 p5000ratio.v"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

set scan1 = `echo $1 | awk -F, '{print $1}'`
if (`echo $1 | grep ','` != "") then
	set f1 = `echo $1 | awk -F, '{print $2}'`
else
	set f1 = 1
endif

set scan2 = `echo $2 | awk -F, '{print $1}'`
if (`echo $2 | grep ','` != "") then
	set f2  = `echo $2 | awk -F, '{print $2}'`
else
	set f2 = 1
endif

set sum = $3
set rec = ${sum}.rec

set now = `date`
set uid = `whoami`

if (! -e $scan1) then
	echo "ERROR: cannot find $scan1"
	exit 1
endif
if (! -e $scan2) then
	echo "ERROR: cannot find $scan2"
	exit 1
endif

if (-e $rec) rm $rec
echo "rec ${sum} ${now} ${uid}" > ${rec}
echo "imgdiv $1 $2 $3" >> ${rec}
ident ${NPG}/bin/imgdiv.bin | grep imgdiv.c >> ${rec}

${NPG}/bin/imgdiv.bin ${scan1} ${f1} ${scan2} ${f2} ${sum} ${rec} ${now} ${uid}

echo "endrec" >> ${rec}
