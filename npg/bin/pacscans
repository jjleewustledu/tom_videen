#!/bin/csh
#	Name:	pacscans
#	Date:	Jan 2002
#	Author:	Tom Videen
#	Use:	Get basic info on multiple DICOM images 

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv != 1) then
	echo "Usage:  pacscans <root>"
	echo " e.g.,  pacscans 1."
	exit 1
endif

unalias rm
set in = $1
set hdr = pacscans$$.hdr
set files = `ls -1 ${in}*`
set root = `echo $files[1] | cut -f1-9 -d.`
set sdate = `echo $files[1] | cut -f10 -d. | cut -c1-8`
set Log = Scans.lst
touch $Log

set snum = `ls -1 ${in}* | cut -f10 -d. | cut -c 19- | sort -n`
set seq1 = 1
set first = $snum[1]
set last = 0
set num = 1
foreach i ($snum)
    set seq2 = `echo $i | cut -c2`
	if ($seq2 != $seq1) then
		echo Sequence $seq1 >> $Log
		set nam1 = `ls ${root}.${sdate}*$first`
		set nam2 = `ls ${root}.${sdate}*$last`
		set seq1 = $seq2
		set first = $i
		set n = 0
		${NPG}/bin/dcm_dump_file $nam1 >! $hdr
		${NPG}/bin/dcmscans.bin $nam1 $num $hdr $last >> $Log
		set num = 1
	endif
	set last = $i
	@ num++
	@ n++
end

echo Sequence $seq1 >> $Log
set nam1 = `ls ${root}.${sdate}*$first`
set nam2 = `ls ${root}.${sdate}*$last`
${NPG}/bin/dcm_dump_file $nam1 >! $hdr
${NPG}/bin/dcmscans.bin $nam1 $num $hdr $last >> $Log
rm $hdr

cat $Log
echo saved in $Log
