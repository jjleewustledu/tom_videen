#!/bin/csh
#	Module:	dcm_mrsort
#	Author:	Tom Videen
#	Date:	Sept 2009
#	Use:	Rename files, move to common Raw directory, generate Scans.lst, and remove directories

if ($#argv != 2) then
	echo "Usage:  dcm_mrsort DBID folder"
	echo " e.g.,  dcm_mrsort db500 MR_clinical"
	exit 1
endif

set DB  = $1
set LOC = $2
set MRDIR  = /data/jsp/human/DBS/$DB/$LOC
set RAWDIR = $MRDIR/Raw
if (! -e $MRDIR) then
	echo "Directory does not exist: $MRDIR"
	exit 1
endif

cd $MRDIR
if (! -e $RAWDIR) then
	mkdir $RAWDIR
	chmod 770 $RAWDIR
	jsp
endif
set seq = 0
set count = 1
echo "-------------------------------------------------------------------------------------"
ls -R ?
echo "-------------------------------------------------------------------------------------"
echo "Directories should begin with a number."
echo "Lowest level folders should contain numbered filenames only"
echo "with sequential numbers across folders (no repeated numbers)."
echo
echo "RETURN if true;  CTRL-C if false (use dcm_mrsort2)"
set data = `line`

# ------------------------------------------------------
# This Loop assumes dcm files increment across all scans
# ------------------------------------------------------
LOOP:
@ seq++
cd $MRDIR
set SDIR = `ls -R $seq | grep / | tail -1 | cut -f1 -d:`
if ($SDIR == '') goto DONE
cd $SDIR
set first = `ls | sort -n | head -1 | cut -f1 -d\*`
set last = `ls | sort -n | tail -1 | cut -f1 -d\*`
set n = `ls | wc -w`
echo "Convert $n files: $first-$last"
echo "Press RETURN to continue (CTRL-C to quit)"
set data = `line`
dcmren $first $last
/usr/bin/mv -i *dcm $RAWDIR
goto LOOP

# Unused variation of Loop if dcm file numbers are repeated
# dcm_renum $first $last $count
# /usr/bin/mv -i *dcm $RAWDIR
# set count = `echo $count + $n | bc`
# echo $count
# ---------------------------------
DONE:
cd $RAWDIR
dcmscans 1
jsp
chmod 660 *

# ---------------------------------
# After final checking, cleanup
# Run the final remove with CARE!
# ---------------------------------
cd $MRDIR
echo "WARNING: Everything listed below will be deleted:"
echo "-------------------------------------------------------------------------------------"
ls -R ?
echo "-------------------------------------------------------------------------------------"
echo "Check that these are all directories and do not include any files"
echo "Press RETURN to delete (CTRL-C to cancel)"
set data = `line`
rm -rf ?

