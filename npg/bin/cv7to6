#!/bin/csh
#	Module:	cv7to6
#	Date:	Apr 1996
#	Author:	Tom Videen
#	Intent:	Convert Version 7 ECAT image files to Version 6 format

if ($#argv < 1) then
	echo "Usage:  cv7to6 filename (no extension) [frame]"
	echo "   or:  cv7to6 infile outfile (both with extensions) frame"
	echo " i.e.:  cv7to6 p5000ho1"
	echo "        cv7to6 p5000cg1 10"
	echo "        cv7to6 p5000mean.t88 p5000mean.img 1"
	exit 1
endif

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv == 1) then
	set infile = $1.v
	set outfile = $1.img
	set frm = 1
else if ($#argv == 2) then
	set infile = $1.v
	set outfile = $1.img
	set frm = $2
else if ($#argv == 3) then
	set infile = $1
	set outfile = $2
	set frm = $3
else
	echo "Usage: cv7to6 filename (no extension) [frame]"
	echo "   or: cv7to6 infile outfile (both with extensions) frame"
	exit 1
endif
set rec = ${outfile}.rec
set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (! -e $infile) then
	echo "ERROR [cv7to6]: $infile does not exist"
	exit 1
endif

if (-e $outfile) rm $outfile
if (-e $rec) rm $rec

echo "rec ${outfile} ${now} ${uid}" > ${rec}
echo "cv7to6 $1" >> ${rec}
ident ${NPG}/bin/cv7to6.bin | grep cv7to6.c >> ${rec}
${NPG}/bin/cv7to6.bin ${infile} ${frm} ${outfile} ${rec} ${now}
set endstat = $status
echo "endrec" >> ${rec}
if ($endstat != 0) echo "End of Task Status = $endstat"
