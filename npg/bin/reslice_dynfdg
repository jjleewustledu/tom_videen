#!/bin/csh
#	Name:	reslice_dynfdg
#	Date:	Feb 2005
#	Author: Tom Videen
#	Use:	Reslice dynamic ECAT fdg image using AIR file.
#   Note:
#       sumimgframes requires the start time;

if ($#argv != 5) then
	echo "Usage:  reslice_dynfdg image air_file firstfrm lastfrm"
	echo " e.g.:  reslice_dynfdg p5000fdg1.v p5000fdg_to_p5000mpr 2 45"
	exit 1
endif

set in  = $1
set air = $2
set ff  = $4
set lf  = $5

if (! -e $in) then
	echo "ERROR [reslice_dynfdg]: missing $in"
	exit
endif
if (! -e $air.air) then
	echo "ERROR [reslice_dynfdg]: missing $air.air"
	exit
endif
imghdr $in $ff >! tmp
if ($status != 0) then
	echo "ERROR [reslice_dynfdg]: missing $in frame $ff"
	exit
endif
imghdr $in $lf >! tmp
if ($status != 0) then
	echo "ERROR [reslice_dynfdg]: missing $in frame $lf"
	exit
endif
ls tmpdyn* atmpdyn* ratmpdyn* rtmpdyn* >! tmp
if ($status != 0) then
	echo "ERROR [reslice_dynfdg]: files named tmpdyn, atmpdyn, ratmpdyn or rtmpdyn  must be removed"
	exit
endif
unalias rm
rm tmp

set time0 = `imghdr $in $ff | grep Start | cut -f2 -d= | cut -f1 -ds`
set frm = $ff
while ($frm <= $lf)
	set time1 = `imghdr $in $frm | grep Start | cut -f2 -d= | cut -f1 -ds`
	set starttime = `echo $time1 - $time0 | bc`
	set out = tmpdyn$frm.v
	set aout = atmpdyn$frm
	sumimgframes $in $out $frm $frm $starttime
    img2analyze $out $aout
	imgreslice $aout $air
	analyze2img r$aout r$out $in $frm
    rm $out*
    rm $aout.*
    rm r$aout.*
	@ frm++
end

merge_dynframes rtmpdyn ${ff}to${lf} .v r$in

set frm = $ff
while ($frm <= $lf)
	rm rtmpdyn$frm.v
	@ frm++
end

