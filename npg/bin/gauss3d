#!/bin/csh
#   Name:   gauss3d
#   Date:   May 2000
#   Author: Tom Videen

set argc = $#argv
if ($argc != 2 && $argc != 3) then
	echo " Usage:  gauss3d infile [outfile] half_freq"
	echo "  i.e.:  gauss3d p5000ho1.v 0.3"
	echo "         gauss3d p5000ho1.v p5000ho1_g2.img 0.2"
	exit
endif

set infile = $1
if (! -e $infile) then
	echo "ERROR [gauss]: $infile does not exist."
	exit 1
endif

if ($argc == 3) then
	set outfile	= $2
	set f0 = $3
else
	set f0 = $2
	set base = `basename ${infile}`
	set root = `echo ${base} | awk -F. '{print $1}'`
	set ext = '.img'
	set filterid = `echo "scale=0;${f0} * 10" | bc`
	set whole = `echo ${filterid} | awk -F. '{print $1}'`
	set fraction = `echo ${filterid} | awk -F. '{print $2}'`
	if (${fraction} == 0 || ${fraction} == 00 || ${fraction} == 000) then
		set filterid = ${whole}
	endif
	set outfile = ${root}_g${filterid}${ext}
endif

setenv INFILE $infile
setenv OUTFILE $outfile
setenv HALF_FREQUENCY $f0
setenv IDL_NAME gauss3d
setenv RESOURCES plot.xrdb
run_idl
