#!/bin/csh
#	Name:	cos_scale_images
#	Author:	Tom Videen
#	Date:	Oct 2003
#	Created for: COSS PET Processing
#	Intent:	Scale images to reasonable pixel values (matching water to meanpet)

if (! $?STUDY) then
	echo "ERROR: env STUDY not defined"
	exit 1
else if (! $?LOGFILE) then
	echo "ERROR: env LOGFILE not defined"
	exit 1
else if (! $?HO) then
	echo "ERROR: env HO not defined"
	exit 1
else if (! $?OO) then
	echo "ERROR: env OO not defined"
	exit 1
endif

set GAUSS = _g3
set WATER = $STUDY-$HO$GAUSS.v
set OXYGN = $STUDY-$OO$GAUSS.v
if (! -e $WATER) then
	echo "ERROR: $WATER not found"
	exit 1
else if (! -e $OXYGN) then
	echo "ERROR: $OXYGN not found"
	exit 1
endif

set NPLANES = `imghdr $WATER | grep slices | cut -f1 -ds`
set MASK = `echo 2 \* $NPLANES/3 | bc`
maskslc $WATER tmpho.v ${MASK}to${NPLANES}
maskslc $OXYGN tmpoo.v ${MASK}to${NPLANES}

set maxHO = `imghdr tmpho.v | grep Max | cut -f2 -d= | cut -f1 -dM`
set maxOO = `imghdr tmpoo.v | grep Max | cut -f2 -d= | cut -f1 -dM`
set scaleHO = `echo 1550 / $maxHO | bc -l`
set scaleOO = `echo 1550 / $maxOO | bc -l`
echo "$WATER    Max = $maxHO   Scaler = $scaleHO"
echo "$OXYGN    Max = $maxOO   Scaler = $scaleOO"
echo "$WATER    Max = $maxHO   Scaler = $scaleHO" >> $LOGFILE
echo "$OXYGN    Max = $maxOO   Scaler = $scaleOO" >> $LOGFILE
echo "=========================================================================="

scaleimage $WATER $scaleHO
scaleimage $OXYGN $scaleOO

set maxHO = `imghdr $WATER | grep Max | cut -f2 -d= | cut -f1 -dM`
set maxOO = `imghdr $OXYGN | grep Max | cut -f2 -d= | cut -f1 -dM`
echo "$WATER    Max = $maxHO"
echo "$OXYGN    Max = $maxOO"
unalias rm
rm tmpho.v
rm tmpoo.v
exit
