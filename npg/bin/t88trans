#!/bin/csh
#	Name:	t88trans
#	Date:	Oct 1995
#	Author:	Tom Videen
#	Use:	Create a stereotaxic-reformatted image to Talairach 1988 atlas brain
#			Uses parameter, imagelist, and map files.

if (! $?NPG) setenv NPG /usr/local/npg
set program = t88trans

if ($#argv != 3) then
	echo "Usage:  t88trans prm list image"
	echo " i.e.:  t88trans my.prm my.lst output.t88"
	exit 1
endif

set prm = $1
set lst = $2
set smg = $3
set rec = ${smg}.rec
set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (! -e $prm) then
	echo "ERROR: cannot find $prm"
	exit 1
endif

if (! -e $lst) then
	echo "ERROR: cannot find $lst"
	exit 1
endif

unalias rm
if (-e $rec) rm $rec
echo "rec ${smg} ${now} ${uid}" > ${rec}
echo "${program} $1 $2 $3" >> ${rec}
ident ${NPG}/bin/t88trans.bin | grep ${program}.c >> ${rec}
echo "prm" >> ${rec}
cat ${prm} >> ${rec}
echo "endprm" >> ${rec}
echo "imglst" >> ${rec}
cat ${lst} >> ${rec}
echo "endimglst" >> ${rec}

${NPG}/bin/t88trans.bin ${prm} ${lst} ${smg} ${rec} ${now} ${today} ${uid}

set endstat = $status
if ($endstat == 0) then
	echo "endrec" >> ${rec}
else if ($endstat == 3) then
	rm $rec
else
	echo "End of Task Status = $endstat"
endif
