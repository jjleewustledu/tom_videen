#! /bin/csh
#	Name:	recon_2dwobdyn_ns
#	Author:	Tom Videen
#	Date:	Jul 1998
#	Intent:	Reconstruct a dynamic wobbled emission scan
#	Note:	The sharpest cutoff is 0.5, which is analogous to stationary reconstruction
#			and differs from the 0.25 on the ACS.

if (! $?NPG) setenv NPG /usr/local/npg

if ($#argv !=	8) then
	echo "Usage: recon_2dwobdyn_ns image scn atn nrm filter cutoff zoom frames"
	echo "	image	= output image (.img)"
	echo "	scn		= scan (.scn)"
	echo "	atn		= attenuation (.atn)"
	echo "	nrm		= normalization (.nrm)"
	echo "	filter	 = 1-6 (1=ramp)"
	echo "	cutoff	<= 0.5"
	echo "	zoom	 = image zoom (1.0 - )"
	echo "	frames	 = number of frames"
	echo
	echo "Reconstructs dynamic files WITHOUT Scatter Correction"
	echo "Specify all files with complete path & name but no extensions."
	echo "  e.g., recon_2dwobdyn_ns p2000sp1 p2000sp1 p2000tr1_wob normalwob_940130 1 .5 1.5 50"
	exit 1
endif

setenv DEBUG 0
set scatdata = ${NPG}/953/scatparms.dat

set imageFile = $1.img
set recFile	= ${imageFile}.rec
set scanFile  = $2.scn
set attenFile = $3.atn
set normFile  = $4.nrm
set filter = $5
set cutoff = $6
set zoom  = $7
set frames = $8
set gate = 1
set data = 0
set bed = 0
set xoff = -0.204
set yoff = +0.592
set rot = 0.
set size = 128
set decayCor = 0
set debug = 0
set scatterCor = 0
set arcCor = 1
set intrinsicTilt = 1
set priority = 0
set filt_slope = 0

if (! -e ${scanFile}) then
	echo "ERROR: cannot find ${scanFile}"
	exit 1
endif
if (! -e ${attenFile}) then
	echo "ERROR: cannot find ${attenFile}"
	exit 1
endif
if (! -e ${normFile}) then
	echo "ERROR: cannot find ${normFile}"
	exit 1
endif
if (${scatterCor} == 1 && !(-e ${scatdata})) then
	echo "ERROR: cannot find ${scatdata}"
	exit 1
endif

unalias rm
if (-e ${recFile}) rm ${recFile}
 
set frameNumber = 1
while ( $frameNumber <= $frames )
	set plane = 1
	while ( $plane <= 31 )
		set line = `sed -n "$plane","$plane"p $scatdata`
		set sf	= `echo $line | awk '{print $2}'`
		set slope = `echo $line | awk '{print $3}'`
		set ARGS	= (${scanFile},${frameNumber},$plane,${gate},${data},${bed} \
			${size},${zoom},${xoff},${yoff},${rot},${decayCor},${scatterCor},${arcCor},${intrinsicTilt}	\
			${imageFile},${frameNumber},$plane,${gate},${data},${bed}	\
			${filter},${cutoff},0,${filt_slope},0,${sf},${slope} \
			-a ${attenFile},1,$plane,${gate},${data},${bed} \
			-n ${normFile},1,$plane,${gate},${data},${bed})
		${NPG}/bin/bkproj_sun $ARGS
		@ plane++
	end
	@ frameNumber++
end

echo "rec ${imageFile} `date` `whoami` `hostname` `uname`" > ${recFile}
echo "recon_2dwobdyn_ns $1 $2 $3 $4 $5 $6 $7 $8" >> ${recFile}
ident ${NPG}/bin/bkproj_sun | grep bkproj_sun.c | cut -f2- -d: >> ${recFile}
if (`echo ${scanFile} | grep rn` == ${scanFile}) then
	ident ${NPG}/bin/rnwproc | grep rnwproc.c | cut -f2- -d: >> ${recFile}
endif
echo "endrec" >> ${recFile}
exit
