#!/bin/csh
#	Module:	t88trans
#	Date:	12-Oct-95
#	Author:	Tom Videen
#	Use:	Create a stereotaxic-reformatted image to Talairach 1988 atlas brain
#			Uses parameter, imagelist, and map files.

set BINDIR  = /home/npggw/tom/src/t88trans
set program = t88trans
if ($#argv != 3) then
	echo "Usage: ${program} prm list image"
	echo " i.e.: ${program} my.prm my.lst output.t88"
	exit
endif

set prm = $1
set lst = $2
set smg = $3
set rec = ${smg}.rec
set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (!(-e ${prm})) then
	echo $program": parameter file ${prm} does not exist"
	exit
endif

if (!(-e ${lst})) then
	echo $program": imagelist file ${lst} does not exist"
	exit
endif

if (-e ${rec}) then
	rm $rec
endif
echo "rec ${smg} ${now} ${uid}" > ${rec}
echo "${program} $1 $2 $3" >> ${rec}
ident ${BINDIR}/t88trans.bin | grep ${program}.c >> ${rec}
echo "prm" >> ${rec}
cat ${prm} >> ${rec}
echo "endprm" >> ${rec}
echo "imglst" >> ${rec}
cat ${lst} >> ${rec}
echo "endimglst" >> ${rec}

${BINDIR}/t88trans.bin ${prm} ${lst} ${smg} ${rec} ${now} ${today} ${uid}

set endstat = $status
if ($endstat == 0) then
	echo "endrec" >> ${rec}
else if ($endstat == 3) then
	rm $rec
else
	echo "End of Task Status = $endstat"
endif
