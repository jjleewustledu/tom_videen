CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  Subroutine:   OAXES.FTN
C
C  Author:       Tom O. Videen
C  Date:         12-Feb-87
C  Written For:  OPROFILE.FTN
C
C  Intent:
C     This subroutine draws graph axes on the GraphOn terminal screen,
C     marks and labels ticks on the axes, and writes a title on the
C     graph.
C
C     Called by OPLOT.FTN
C
C  Common Blocks:
C     The following variables should have values assigned before
C     this subroutine is called:
C
C       XMIN,XMAX = range of x-axis;
C       YMIN,YMAX = range of y-axis;
C       XLOG:  true--> x-axis is logarithmic; false--> linear;
C       YLOG:  true--> y-axis is logarithmic; false--> linear;
C
C  Local Variables:
C     X1,Y1 = terminal coordinates of lower left corner of rectangle
C        to be scaled;
C     X2,Y2 = terminal coordinates of upper right corner of rectangle
C        to be scaled;
C        (range of coordinates is 0-1023 for X; 0-781 for Y)
C     INTERVAL = tick interval on axis;
C     INTV = tick interval on axis for real scale;
C     FIELDWTH = fieldwidth of of numbers labeling axes;
C     CHSIZE = character size of integers on x-axis scale
C        (1-4 where 1 is the largest size);
C     XRANGE = difference between XMAX-XMIN;
C     YRANGE = difference between ALOG10(YMAX)-ALOG10(YMIN)
C     X,Y,XTICK,YTICK = coordinates used to draw ticks;
C     XNUM,YNUM = coordinates used for writing numbers on axes;
C     STR = string to pass to subroutines;
C
C  Uses Subroutines:
C
C  GRAPHON.LIB
C     GORESET
C     GOLTYPE
C     GOSCALE
C     GOSPLOT
C     GOSCINT
C     GOSRINT
C     GOSLINE
C     GOEXIT
C     GOSCREAL
C     GOSRREAL
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      SUBROUTINE OAXES
C
      INTEGER*4    X1,X2,Y1,Y2,INTERVAL,FIELDWTH,CHSIZE,I,M,N,NPOINTS
      REAL         XRANGE,YRANGE,XVAL,YVAL,XTICK,YTICK,XNUM,YNUM,
     2             X(101),Y(101),XMIN,YMIN,XMAX,YMAX,INTV
      CHARACTER*80 STR
      LOGICAL*1    XLOG,YLOG
C
      COMMON /SCALE/ X,Y,XMIN,XMAX,YMIN,YMAX,NPOINTS,XLOG,YLOG
C
      DATA X1,X2,Y1,Y2  /120,920,50,650/
      DATA CHSIZE /2/
C
C  Reset screen, scale the plot, and draw x- and y-axes (box)
C
      CALL GOLTYPE(1)
      CALL GOSCALE(X1,X2,Y1,Y2,XMIN,XMAX,YMIN,YMAX,XLOG,YLOG)
      CALL GORESET
      CALL GOSPLOT(XMIN,YMIN)
      CALL GOSPLOT(XMAX,YMIN)
      CALL GOSPLOT(XMAX,YMAX)
      CALL GOSPLOT(XMIN,YMAX)
      CALL GOSPLOT(XMIN,YMIN)
C
C  Compute range and tick locations for each axis
C
      IF (XLOG) THEN
        XRANGE = ALOG10(XMAX) - ALOG10(XMIN)
        XTICK = 10**(ALOG10(XMIN) - XRANGE/100.0)
        XNUM = 10**(ALOG10(XMIN) - XRANGE/65.0)
      ELSE
        XRANGE = XMAX-XMIN
        XTICK = XMIN - XRANGE/100.0
        XNUM  = XMIN - XRANGE/65.0
      END IF
C
      IF (YLOG) THEN
        YRANGE = ALOG10(YMAX) - ALOG10(YMIN)
        YTICK = 10**(ALOG10(YMIN) - YRANGE/75.0)
        YNUM = 10**(ALOG10(YMIN) - YRANGE/50.0)
      ELSE
        YRANGE = YMAX-YMIN
        YTICK = YMIN - YRANGE/75.0
        YNUM  = YMIN - YRANGE/50.0
      END IF
C
C  Draw tick marks on the x-axes
C
      IF (XLOG) THEN
        FIELDWTH = 5
        N = INT(XRANGE)
        DO 100 I=0,N
          XVAL = XMIN * (10**I)
          CALL GOSLINE(XVAL,YMIN,XVAL,YTICK)
          CALL GOSCINT(XVAL,YNUM,CHSIZE,NINT(XVAL),FIELDWTH)
100     CONTINUE
      ELSE
        IF (XRANGE.GE.2.0) THEN
          FIELDWTH = 5
          IF (XRANGE.LE.10.0) THEN
            INTERVAL = 1
          ELSE IF (XRANGE.LE.100.0) THEN
            INTERVAL = 10
          ELSE IF (XRANGE.LE.500.0) THEN
            INTERVAL = 50
          ELSE IF (XRANGE.LE.1000.0) THEN
            INTERVAL = 100
          ELSE IF (XRANGE.LE.5000.0) THEN
            INTERVAL = 500
          ELSE IF (XRANGE.LE.10000.0) THEN
            INTERVAL = 1000
          ELSE IF (XRANGE.LE.50000.0) THEN
            INTERVAL = 5000
          ELSE
            INTERVAL = 10000
          END IF
          N = INT(XRANGE)/INTERVAL
          DO 200 I=0,N
            XVAL = XMIN + FLOAT(I*INTERVAL)
            CALL GOSLINE(XVAL,YMIN,XVAL,YTICK)
            CALL GOSCINT(XVAL,YNUM,CHSIZE,NINT(XVAL),FIELDWTH)
200       CONTINUE
C
        ELSE
          IF (XRANGE.GE.0.6) THEN
            INTV = 0.2
            FIELDWTH = 3
          ELSE IF (XRANGE.GE.0.2) THEN
            INTV = 0.1
            FIELDWTH = 3
          ELSE IF (XRANGE.GE.0.06) THEN
            INTV = 0.02
            FIELDWTH = 4
          ELSE IF (XRANGE.GT.0.02) THEN
            INTV = 0.01
            FIELDWTH = 4
          ELSE IF (XRANGE.GT.0.006) THEN
            INTV = 0.002
            FIELDWTH = 5
          ELSE
            INTV = 0.001
            FIELDWTH = 5
          END IF
          N = INT((XRANGE/INTV)+0.1)
          DO 300 I=0,N
            XVAL = XMIN + FLOAT(I)*INTV
            CALL GOSLINE(XVAL,XMIN,XVAL,YTICK)
            CALL GOSCREAL(XVAL,YNUM,CHSIZE,XVAL,FIELDWTH)
300       CONTINUE
        END IF
      END IF
C
C  Draw tick marks on the y-axes
C
      IF (YLOG) THEN
        FIELDWTH = 6
        N = INT(ALOG10(YMAX)-ALOG10(YMIN))
        DO 400 I=0,N
          YVAL = YMIN * (10**I)
          CALL GOSLINE(XTICK,YVAL,XMIN,YVAL)
          CALL GOSRINT(XNUM,YVAL,CHSIZE,NINT(YVAL),FIELDWTH)
400     CONTINUE
      ELSE
        IF (YRANGE.GE.2.0) THEN
          FIELDWTH = 6
          IF (YRANGE.LE.10.0) THEN
            INTERVAL = 1
          ELSE IF (YRANGE.LE.100.0) THEN
            INTERVAL = 10
          ELSE IF (YRANGE.LE.500.0) THEN
            INTERVAL = 50
          ELSE IF (YRANGE.LE.1000.0) THEN
            INTERVAL = 100
          ELSE IF (YRANGE.LE.5000.0) THEN
            INTERVAL = 500
          ELSE IF (YRANGE.LE.10000.0) THEN
            INTERVAL = 1000
          ELSE IF (YRANGE.LE.50000.0) THEN
            INTERVAL = 5000
          ELSE
            INTERVAL = 10000
          END IF
          N = INT(YRANGE)/INTERVAL
          DO 500 I=0,N
            YVAL = YMIN + FLOAT(I*INTERVAL)
            CALL GOSLINE(XTICK,YVAL,XMIN,YVAL)
            CALL GOSRINT(XNUM,YVAL,CHSIZE,NINT(YVAL),FIELDWTH)
500       CONTINUE
C
        ELSE
          IF (YRANGE.GE.0.6) THEN
            INTV = 0.2
            FIELDWTH = 3
          ELSE IF (YRANGE.GE.0.2) THEN
            INTV = 0.1
            FIELDWTH = 3
          ELSE IF (YRANGE.GE.0.06) THEN
            INTV = 0.02
            FIELDWTH = 4
          ELSE IF (YRANGE.GE.0.02) THEN
            INTV = 0.01
            FIELDWTH = 4
          ELSE IF (YRANGE.GE.0.006) THEN
            INTV = 0.002
            FIELDWTH = 5
          ELSE
            INTV = 0.001
            FIELDWTH = 5
          END IF
          N = INT((YRANGE/INTV)+0.1)
          DO 600 I=0,N
            YVAL = YMIN + FLOAT(I)*INTV
            CALL GOSLINE(XTICK,YVAL,XMIN,YVAL)
            CALL GOSRREAL(XNUM,YVAL,CHSIZE,YVAL,FIELDWTH)
600       CONTINUE
        END IF
      END IF
C
      CALL GOEXIT
      RETURN
      END
