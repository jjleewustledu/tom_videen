CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  Subroutine:   OPLOTDAT.FTN
C
C  Author:       Tom O. Videen
C  Date:         12-Feb-87
C  Written For:  OPROFILE.FTN
C
C  Intent:
C     Plot values.
C     Y-values less than YMIN are plotted as YMIN.
C
C  Variables Passed:
C     PN = plot number (1-12);
C     RESET: true--> clear screen before plotting;
C     LAST:  true--> last plot on graph;
C     LEGEND = legend to write at top for this plot;
C
C  Uses Subroutines:
C     GETREAL
C     OAXES
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      SUBROUTINE OPLOTDAT(PN,RESET,LAST,LEGEND)
C
      INTEGER*4    PTYPE(12),PSIZE(12),LTYPE(12),XKEY(12),YKEY(12),
     2             PN,NPOINTS,X1,X2,Y1,Y2
      REAL         X(101),Y(101),XMIN,XMAX,YMIN,YMAX
      CHARACTER*80 Q,LEGEND
      LOGICAL*1    XLOG,YLOG,PLOTPNTS,RESET,LAST
C
      COMMON /SCALE/ X,Y,XMIN,XMAX,YMIN,YMAX,NPOINTS,XLOG,YLOG
      COMMON /PLOTTYPE/ PTYPE,PSIZE,LTYPE,NUMPLOTS,PLOTPNTS
C
      DATA XKEY  /4*0,4*345,4*690/
      DATA YKEY  /760,730,700,670,760,730,700,670,760,730,700,670/
C
C  The following values should agree with those in OAXES
C
      DATA X1,X2,Y1,Y2 /120,920,50,650/
C
      IF (RESET) THEN
        Q = 'Maximum Y value'
        CALL GETREAL(Q,YMAX,0.,1000000.)
        CALL OAXES
      ELSE
        CALL GOSCALE(X1,X2,Y1,Y2,XMIN,XMAX,YMIN,YMAX,XLOG,YLOG)
      END IF
C
C  Plot the data
C
      CALL GOINIT
      IF (PLOTPNTS) THEN
        DO 100 I=1,NPOINTS
          IF (X(I).GE.XMIN .AND. X(I).LE.XMAX) THEN
            IF (Y(I).GE.YMIN .AND. Y(I).LE.YMAX) THEN
              CALL GOSPOINT(X(I),Y(I),PTYPE(PN),PSIZE(PN))
            ELSE IF (Y(I).LT.YMIN) THEN
              CALL GOSPOINT(X(I),YMIN,PTYPE(PN),PSIZE(PN))
            END IF
          END IF
100     CONTINUE
      ELSE
        CALL GOLTYPE(LTYPE(PN))
        DO 200 I=1,NPOINTS
          IF (X(I).GE.XMIN .AND. X(I).LE.XMAX) THEN
            IF (Y(I).GE.YMIN .AND. Y(I).LE.YMAX) THEN
              CALL GOSPLOT(X(I),Y(I))
            ELSE IF (Y(I).LT.YMIN) THEN
              CALL GOSPLOT(X(I),YMIN)
            ELSE
              CALL GOSPLOT(X(I),YMAX)
            END IF
          END IF
200     CONTINUE
      END IF
C
C  Write a legend at the top of the plot:
C     2 dots followed by the file name if PLOTPNTS, or else
C     a line followed by the file name;
C
      CALL GOCHSIZE(1)
      IF (PN.LE.12) THEN
        IF (PLOTPNTS) THEN
          CALL GOPOINT(XKEY(PN)+15,YKEY(PN)+5,PTYPE(PN),PSIZE(PN))
          CALL GOPOINT(XKEY(PN)+45,YKEY(PN)+5,PTYPE(PN),PSIZE(PN))
        ELSE
          CALL GOLINE(XKEY(PN),YKEY(PN)+5,XKEY(PN)+60,YKEY(PN)+5)
        END IF
        CALL GOTEXT(XKEY(PN)+70,YKEY(PN),LEGEND)
      END IF
C
      IF (LAST) THEN
        CALL GOWAITCR
        CALL GOEXIT
      END IF
C
      RETURN
      END
