#!/bin/csh
#	Name:	dcminfo
#	Date:	May 2006
#	Author:	Tom Videen
#	Use:	Get basic info on single DICOM images 

if ($#argv != 2) then
	echo "Usage:  dcminfo filename <s/p/a/i/r>"
	echo "  or:   dcminfo filename <ID/PAT/ACQ/IMG/REL>"
	echo " e.g.,  dcminfo 00123.dcm s"
	echo "  or,   dcminfo 123 i"
	exit 1
endif

set file = $1
if (! -e $file) then
	if ($1 < 10 ) then
    	set prefix = "0000"
	else if ($1 < 100) then
    	set prefix = "000"
	else if ($1 < 1000) then
    	set prefix = "00"
	else if ($1 < 10000) then
    	set prefix = "0"
	else 
    	set prefix = ""
	endif
	set file = "$prefix$1.dcm"
	if (! -e $file) then
		echo "ERROR: cannot find $1 or $file"
		exit 1
	endif
endif
set key = `echo $2 | tr "a-z" "A-Z"`
if ($key == "P") then
	set key = "PAT"
else if ($key == "A") then
	set key = "ACQ"
else if ($key == "R") then
	set key = "REL"
else if ($key == "S") then
	set key = "ID"
else if ($key == "I") then
	set key = "IMG"
endif

set tmp  = dcminfo$$.tmp
set tmp2  = dcminfo$$.tmp2
dcm_dump_element -t 0008 0033 $file $tmp >& /dev/null
if ($status == 0) then
	set type = "-t"
else 
	dcm_dump_element 0008 0033 $file $tmp >& /dev/null
	if ($status == 0) then
		set type = ""
	else
		echo "$file is not DICOM format"
		exit 1
	endif
endif


dcm_dump_file $type $file | grep "$key " | cut -f3 -d/ >! $tmp
dcm_dump_file $type $file | grep "$key " | cut -f5 -d/ >! $tmp2
paste $tmp $tmp2 

unalias rm
rm $tmp $tmp2 

