#!/bin/csh
# Author:	Tom Videen
# Date:		May 2006
# Purpose:	Rename .dcm files to greater numbers
# -----------------------------------------------------
set argc = $#argv
if ($#argv != 3) then
    echo " Usage:  dcm_renum first last new_first"
    echo "  e.g.:  dcm_renum 1 30 2"
    exit 1
endif

set first = $1
set last = $2
set newfirst = $3

if ($first > $last) then
	echo " first must be < last"
	exit 1
endif
if ($newfirst < 0) then
	echo " new_first must be >= 0"
	exit 1
endif
if ($newfirst == $first) then
	echo " new_first must differ from first"
	exit 1
endif

if ($newfirst > $first) then
	set num = $last
	set new = `echo "$last + $newfirst - $first" | bc`
	while ($num >= $first)
		if ($new < 10 ) then
			set prefix = "0000"
		else if ($new < 100) then
			set prefix = "000"
		else if ($new < 1000) then
			set prefix = "00"
		else if ($new < 10000) then
			set prefix = "0"
		endif
		set newname = $prefix$new.dcm
		if ($num < 10 ) then
			set prefix = "0000"
		else if ($num < 100) then
			set prefix = "000"
		else if ($num < 1000) then
			set prefix = "00"
		else if ($num < 10000) then
			set prefix = "0"
		endif
		set oldname = $prefix$num.dcm
		mv $oldname $newname
		@ num-- 
		@ new-- 
	end
else if ($newfirst < $first) then
	set num = $first
	set new = $newfirst
	while ($num <= $last)
		if ($new < 10 ) then
			set prefix = "0000"
		else if ($new < 100) then
			set prefix = "000"
		else if ($new < 1000) then
			set prefix = "00"
		else if ($new < 10000) then
			set prefix = "0"
		endif
		set newname = $prefix$new.dcm
		if ($num < 10 ) then
			set prefix = "0000"
		else if ($num < 100) then
			set prefix = "000"
		else if ($num < 1000) then
			set prefix = "00"
		else if ($num < 10000) then
			set prefix = "0"
		endif
		set oldname = $prefix$num.dcm
		mv $oldname $newname
		@ num++
		@ new++
	end
endif

