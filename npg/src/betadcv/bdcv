#!/bin/csh
#	Name:	betadcv
#	Date:	Feb 2002
#	Author:	Tom Videen
#	Intent:	Deconvolve, decay-correct and calibrate a blood curve (crt)

set BINPATH = /home/npggw/tom/src/betadcv
if ($#argv != 1) then
	echo " usage: betadcv scanname"
	echo "  i.e.: betadcv p5000ho1"
	exit 1
endif

set dcv = $1.dcv
if (-e $dcv) echo "Replacing $dcv"

set crt = $1.crt
if (-e $crt) then
	set start = `head -1 $crt | cut -f5 -d' '`
	set binwidth = `head -1 $crt | cut -f2 -d= | cut -f1 -d' '`
	set new = 0
else
	set crt = $1.crv
	if (-e $crt) then
		set start = `head -1 $crt | cut -f6 -d' '`
		set binwidth = `head -1 $crt | cut -f2 -d= | cut -f1 -d' '`
		set new = 1
	else
		echo "ERROR: Cannot find $crt or $1.crt"
		exit 1
	endif
endif

set type = `echo $crt | cut -c6-7`
if ($type == "oo") then
    set scantype = 1
else if ($type == "ho") then
    set scantype = 2
else if ($type == "oc") then
    set scantype = 3
else
    set scantype = 0
endif

echo run $crt $dcv $start $binwidth $new $scantype
${BINPATH}/betadcv.bin $crt $dcv $start $binwidth $new $scantype
