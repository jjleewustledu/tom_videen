head	1.5;
access;
symbols;
locks; strict;
comment	@c @;


1.5
date	2004.02.20.14.38.59;	author tom;	state Exp;
branches;
next	1.1;

1.1
date	2000.09.14.21.23.25;	author tom;	state Exp;
branches;
next	;


desc
@@


1.5
log
@Feb 2004
@
text
@CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  Subroutine:   P2AREAI.FTN
C  Author:       Tom O. Videen
C  Date:         Sept 2000
C  Intent:
C     This routine returns all points included within the region REG.
C     Only interior points will be included (not the boundary points).
C     All of the points are inserted into the list INREGION.
C
C  IARRAY(X,Y) = array of points representing an image mask for the ROI
C     points are initialized to 0;
C     then set to 1 if included in the boundary or
C        set to -1 if excluded from the region;
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC

      SUBROUTINE P2AREAI(REG)

      INTEGER*4     REG			! number of the region of interest (1-100)
      INTEGER*4     PTR			! pointer to location in BOUNDARY points
      INTEGER*4     X1,Y1,X2,Y2	! define the image field which contains ROI
      INTEGER*4     I,J,TEMP
      INTEGER*2     IARRAY(128,128)
      LOGICAL*1     MORE
      CHARACTER*256 RCSHEADER

      INCLUDE 'spidreg.inc'
      RCSHEADER = "$Id: p2areai.f,v 1.1 2000/09/14 21:23:25 tom Exp tom $"

C  Initializations

      DO 20 I=1,128
        DO 10 J=1,128
          IARRAY(I,J) = 0
10      CONTINUE
20    CONTINUE
      RTOP = 0
      X1 = XMIN(REG)
      X2 = XMAX(REG)
      Y1 = YMIN(REG)
      Y2 = YMAX(REG)
      PTR = BVAL(REG)

C  First include all the boundary points in IARRAY.
C  (by setting their values in IARRAY to 1)

      DO 200 I=1,AVAL(REG)
        IARRAY(BOUNDARY(1,PTR),BOUNDARY(2,PTR)) = 1
        PTR = PTR+1
200   CONTINUE

C  If the boundary is either no more than two columns or two rows
C     wide, then the region consists only of the boundary points.

      IF (((Y2-Y1).LE.1).OR.((X2-X1).LE.1)) GO TO 2000

C  Exclude all points that are on the edges of the image field
C     (defined by X1,Y1 and X2,Y2) but which are not
C      boundary points.

      DO 300 I=X1,X2
        IF (IARRAY(I,Y1).NE.1) IARRAY(I,Y1)=-1
        IF (IARRAY(I,Y2).NE.1) IARRAY(I,Y2)=-1
300   CONTINUE

      DO 400 J=Y1,Y2
        IF (IARRAY(X1,J).NE.1) IARRAY(X1,J)=-1
        IF (IARRAY(X2,J).NE.1) IARRAY(X2,J)=-1
400   CONTINUE

C  Now, starting with the edges of the image field, expand the
C     area excluded from the region by setting to -1 all points
C     directly adjacent to a point whose value is -1.
C     (directly adjacent means connected by one face;
C      it excludes points connected only on a diagonal)

      MORE = .FALSE.

C  Row of IARRAY for Y1

      DO 500 I=X1,X2
        IF (IARRAY(I,Y1).EQ.-1) THEN
          IF (IARRAY(I,Y1+1).EQ.0) THEN
            IARRAY(I,Y1+1) = -1
            MORE = .TRUE.
          END IF
        END IF
500   CONTINUE

C  Row of IARRAY for Y2

      DO 600 I=X1,X2
        IF (IARRAY(I,Y2).EQ.-1) THEN
          IF (IARRAY(I,Y2-1).EQ.0) THEN
            IARRAY(I,Y2-1) = -1
            MORE = .TRUE.
          END IF
        END IF
600   CONTINUE

C  Column of IARRAY for X1

      DO 700 J=Y1,Y2
        IF (IARRAY(X1,J).EQ.-1) THEN
          IF (IARRAY(X1+1,J).EQ.0) THEN
            IARRAY(X1+1,J) = -1
            MORE = .TRUE.
          END IF
        END IF
700   CONTINUE

C  Column of IARRAY for X1

      DO 800 J=Y1,Y2
        IF (IARRAY(X2,J).EQ.-1) THEN
          IF (IARRAY(X2-1,J).EQ.0) THEN
            IARRAY(X2-1,J) = -1
            MORE = .TRUE.
          END IF
        END IF
800   CONTINUE

C   If there are no more points to be excluded,
C     all the points that are not -1 are in the region.

      IF (.NOT.MORE) GO TO 2000

C   If the boundary is not a rectangle, check all the inside points.
C   This loop is repeated until it cycles once with no more points
C      added to the outside of the region.

1000  MORE = .FALSE.
      DO 1500 I=X1+1,X2-1
        DO 1400 J=Y1+1,Y2-1
          IF (IARRAY(I,J).EQ.-1) THEN
            IF (IARRAY(I-1,J).EQ.0) THEN
              IARRAY(I-1,J) = -1
              MORE = .TRUE.
            END IF
            IF (IARRAY(I+1,J).EQ.0) THEN
              IARRAY(I+1,J) = -1
              MORE = .TRUE.
            END IF
            IF (IARRAY(I,J-1).EQ.0) THEN
              IARRAY(I,J-1) = -1
              MORE = .TRUE.
            END IF
            IF (IARRAY(I,J+1).EQ.0) THEN
              IARRAY(I,J+1) = -1
              MORE = .TRUE.
            END IF
          END IF
1400    CONTINUE
1500  CONTINUE
      IF (MORE) GO TO 1000

C  All points that now have the value 0 are included in the
C    region and will be stored in the array INREGION.

2000  DO 3000 I=X1,X2
        DO 2500 J=Y1,Y2
          TEMP = IARRAY(I,J)
          IF (TEMP.EQ.0) THEN
            RTOP = RTOP+1
            INREGION(RTOP,1) = I
            INREGION(RTOP,2) = J
          END IF
2500    CONTINUE
3000  CONTINUE

C  The list INREGION now contains all the points included in the
C    region.  RTOP is the number of points included.

      RETURN
      END
@


1.1
log
@Initial revision
@
text
@d29 1
a29 1
      RCSHEADER = "$Id$"
@
