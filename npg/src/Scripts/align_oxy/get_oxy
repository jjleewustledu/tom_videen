#!/bin/csh
#   Module: get_oxy
#   Author: Tom Videen
#   Date:   May-99
#   Use:    Get and filter PET images (HO, OO, CO) to create OEF image.
#	rcsid = $Id: get_oxy,v 1.3 1999/10/22 17:16:41 tom Exp $

if ($#argv != 6) then
	echo "Usage: get_oxy pid datapath tr ho oc oo"
	echo " i.e., get_oxy p5000 /data/wjp/data0/np1/p5000 1 1 1 1"
	exit (1)
endif

set PID = $1
set DATADIR = $2

set FIL = _g3
set TR  = ${PID}tr$3
set HO  = ${PID}ho$4
set RHO = r$HO
set OC  = ${PID}oc$5
set OO  = ${PID}oo$6

if (! -e $DATADIR) then
	echo "ERROR [get_oxy]: Cannot find $DATADIR"
	exit (1)
endif

if (! -e $TR.v) cp $DATADIR/$TR.v .

if (! -e $RHO.v) then
	cp $DATADIR/$RHO.v .
	set PIXSIZE = `imghdr $RHO.v | grep 'Pixel size' | cut -f4 -d' '`
	if ($PIXSIZE != 0.214400) then
		echo "ERROR: pixel size for a reference image must be 0.2144"
		echo "Pixel size of $RHO.v is $PIXSIZE"
		exit (1)
	endif
	if (-e $DATADIR/$RHO$FIL.hdr) cp $DATADIR/$RHO$FIL.hdr .
else if (! -e $HO.v) then
	cp $DATADIR/$HO.v $RHO.v
	set PIXSIZE = `imghdr $RHO.v | grep 'Pixel size' | cut -f4 -d' '`
	if ($PIXSIZE == 0.208634) then
		echo "Correcting pixel size of $RHO.v"
		setpixsize $RHO.v 0.2144
	else
		echo "Pixel size of $RHO.v is $PIXSIZE"
	endif
	if (-e $DATADIR/$HO$FIL.hdr) cp $DATADIR/$HO$FIL.hdr $RHO$FIL.hdr
endif

if (! -e $OC.v) cp $DATADIR/$OC.v .
if (-e $DATADIR/$OC$FIL.hdr) cp $DATADIR/$OC$FIL.hdr .

if (! -e $OO.v) cp $DATADIR/$OO.v .
if (-e $DATADIR/$OO$FIL.hdr) cp $DATADIR/$OO$FIL.hdr .

set PIXSIZE = `imghdr $OC.v | grep 'Pixel size' | cut -f4 -d' '`
if ($PIXSIZE == 0.208634) then
	echo "Correcting pixel size of $OC.v"
	setpixsize $OC.v 0.2144
else
	echo "Pixel size of $OC.v is $PIXSIZE"
endif

set PIXSIZE = `imghdr $OO.v | grep 'Pixel size' | cut -f4 -d' '`
if ($PIXSIZE == 0.208634) then
	echo "Correcting pixel size of $OO.v"
	setpixsize $OO.v 0.2144
else
	echo "Pixel size of $OO.v is $PIXSIZE"
endif

set PIXSIZE = `imghdr $TR.v | grep 'Pixel size' | cut -f4 -d' '`
if ($PIXSIZE == 0.208634) then
	echo "Correcting pixel size of $TR.v"
	setpixsize $TR.v 0.2144
else
	echo "Pixel size of $TR.v is $PIXSIZE"
endif
 
gauss2d $TR.v ${TR}_2dg5.v 0.5
gauss $RHO.v $RHO$FIL.v 0.3
gauss $OC.v $OC$FIL.v 0.3
gauss $OO.v $OO$FIL.v 0.3

echo "----------------------------------------------------"
echo ${TR}_2dg5.v
echo $RHO$FIL.v
echo $OC$FIL.v
echo $OO$FIL.v
