#!/bin/csh
#	Name:	gauss_dynamic
#	Author:	Tom Videen
#	Date:	Aug 2006

if ($#argv != 4) then
	echo "Usage:  gauss_dynamic image firstfrm lastfrm cutoff"
	echo " e.g.,  gauss_dynamic p6000ho1.v 2 51 0.3"
	exit 1
endif

set IN	   = $1
set FIRST  = $2
set LAST   = $3
set CUTOFF = $4

set ROOT = $IN:r
set EXT  = $IN:e

set GF = `echo "scale=0;${CUTOFF} * 10" | bc`
set WHOLE = `echo ${GF} | awk -F. '{print $1}'`
set FRACT = `echo ${GF} | awk -F. '{print $2}'`
if (${FRACT} == 0 || ${FRACT} == 00 || ${FRACT} == 000) then
	set GF = ${WHOLE}
else
	set GF = `echo "scale=0;${CUTOFF} * 100" | bc`
	set WHOLE = `echo ${GF} | awk -F. '{print $1}'`
	set GF = ${WHOLE}
endif

set OEXT = _g$GF.$EXT
set OUT  = $ROOT$OEXT

if (! -e $IN) then
	echo "ERROR [gauss_dynamic]: cannot find $IN"
	exit 1
endif

unalias rm
set i = $FIRST
while ($i <= $LAST)
	separate_dynframes $IN tmp $EXT $i,$i
	gauss tmp$i.$EXT $CUTOFF
	mergefrm tmp$i$OEXT 1 $OUT $i $IN $i
	rm tmp$i*
	@ i++
end

# Set number of frames equal to LAST frame
echo e >! tmp
echo frames >> tmp
echo $LAST >> tmp
echo d >> tmp
echo q >> tmp
hdrEdit -c -f $OUT < tmp >& /dev/null
rm tmp

