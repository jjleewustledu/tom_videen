#!/bin/csh
#	Module:	xray
#	Date:	10/26/92
#	Author:	Tom Yang
#	History:
#		revised 11-Sep-96 TOV

set argc = $#argv
if ($argc < 1 || $argc > 7) then
	echo " usage:   xray SXRoutput [XRAYrasterimage PETimage threshold peaktype trim]"
	echo " example: xray p2000"
	echo "          xray p2000 p2000.ras"
	echo "          xray p2000 p2000 p2000ho1"
	echo "          xray p2000.sxr p2000.ras p2000ho1.img"
	echo "          xray p2000.sxr p2000.ras p2000ho1.img 80"
	echo "          xray p2000.sxr p2000.ras p2000ho1.img 20 1"
	echo "          xray p2000.sxr p2000.ras p2000ho1.img 80 2 1"
	exit
endif

set BINPATH		= /usr/local/npg
#set BINPATH		= $HOME/src/xray/yang
set LIBPATH		= /usr/local/npg/lib/xray
set SXRsource	= $1
set SXRroot		= `echo ${SXRsource} | awk -F. '{print $1}'`
set SXRsource	= ${SXRroot}.sxr
set threshold	= 40		# template threshold, [0, 100].
set peaktype	= 2			# 1: peak per slice; 2: peak per scan, [1, 2].
set trim		= 2			# trim from the outside edge, [0, 10]. 
set assignedthreshold	= 0

switch ($argc)
	case 1:
		set XRAYsource	= ${SXRroot}.ras
		set PETsource	= ${SXRroot}ho1.img
		breaksw
	case 2:
		set XRAYsource	= $2
		set PETsource	= ${SXRroot}ho1.img
		breaksw
	case 3:
		set XRAYsource	= $2
		set PETsource	= $3
		breaksw
	case 4:
		set XRAYsource		= $2
		set PETsource		= $3
		set threshold		= $4
		set assignedthreshold	= 1
		breaksw
	case 5:
		set XRAYsource		= $2
		set PETsource		= $3
		set threshold		= $4
		set peaktype		= $5
		set assignedthreshold	= 1
		breaksw
	case 6:
		set XRAYsource		= $2
		set PETsource		= $3
		set threshold		= $4
		set peaktype		= $5
		set trim		= $6
		set assignedthreshold	= 1
		breaksw
	default:
		breaksw
endsw

# Range checking for threshold.
if ($threshold < 0 || $threshold > 100) then
	set threshold	= 40
endif

# Range checking for peaktype.
if ($peaktype < 1 || $peaktype > 2) then
	set peaktype	= 2
endif

# Range checking for trim.
if ($trim < 0 || $trim > 10) then
	set trim	= 2
endif

set XRAYfile	= ${XRAYsource}
set XRAYroot	= `echo ${XRAYsource} | awk -F. '{print $1}'`
set PETfile	= ${PETsource}
set PETroot	= `echo ${PETsource} | awk -F. '{print $1}'`

# Check if the X ray file exists.
if (!(-r ${XRAYfile})) then
	set XRAYfile	= ${XRAYroot}.xray
	if (!(-r ${XRAYfile})) then
		set XRAYfile	= ${XRAYroot}.XRAY
		if (!(-r ${XRAYfile})) then
			echo " Error: X ray file (${XRAYsource}) does not exist."
			exit
		endif
	endif
endif


# Check if the PET image file exists.
if (!(-r ${PETfile})) then
	set PETfile	= ${PETroot}.smg
	if (!(-r ${PETfile})) then
		set PETfile	= ${PETroot}.SMG
		if (!(-r ${PETfile})) then
			set PETfile	= ${PETroot}.csg
			if (!(-r ${PETfile})) then
				set PETfile	= ${PETroot}.CSG
				if (!(-r ${PETfile})) then
					echo " Error: PET image file (${PETsource}) does not exist."
					exit
				endif
			endif
		endif
	endif
endif

loaddef ${LIBPATH}/xray
${BINPATH}/xray.bin $SXRsource $XRAYfile $PETfile $threshold $peaktype $trim $SXRroot $assignedthreshold	
