head	1.1;
access;
symbols;
locks; strict;
comment	@c @;


1.1
date	93.11.17.20.17.08;	author ty7777;	state Exp;
branches;
next	;


desc
@X window version, first release.
@


1.1
log
@Initial revision
@
text
@C$Id$
C$Log$
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
C  Subroutine:   t1upower.f
C
C  Author:       Tom O. Videen
C  Date:         19-Jul-90
C  Written For:  RAPTURE
C
C  History:
C     Modified 17-Oct-90 to compute power for 2,3,5,10,15%
C     Modified 17-Nov-93 by Tom Yang to port to UNIX workstation.
C
C  Description:
C     Estimate power for unpaired data.
C     Both standard error of difference and degrees of freedom
C     are computed with corrections for unequal variance in 2 samples.
C
C  Calling:
C     TTABLE
C     ZTABLE
C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C
      SUBROUTINE T1UPOWER(NT,VAR1,VAR2)
C
      INCLUDE 'rapture.inc'
C
      INTEGER*4     NT        ! no. of test files for unpaired analysis
      REAL*4        VAR1      ! variance for control set
      REAL*4        VAR2      ! variance for test set
C
      REAL*4        DELTA     ! noncentrality parameter
      REAL*4        MEAN(5)   ! difference of means
      REAL*4        SEMSQ1    ! stand error of mean squared for control set
      REAL*4        SEMSQ2    ! stand error of mean squared for test set
      REAL*4        SDIFF     ! standard error of difference of means
      REAL*4        T1        ! critical t-value for 1-tailed test
      REAL*4        T2        ! critical t-value for 2-tailed test
      REAL*4        Z1        ! critical z-value for 1-tailed test
      REAL*4        Z2        ! critical z-value for 2-tailed test
      REAL*4        BETA1     ! probability that MEAN1.ne.MEAN2 and
C                                 failed to detect (1-tailed)
      REAL*4        BETA2     ! probability that MEAN1.ne.MEAN2
C                                 failed to detect (2-tailed)
      REAL*4        POWER1(5) ! 1-BETA1
      REAL*4        POWER2(5) ! 1-BETA2
      REAL*4        TEMP1,TEMP2 ! local reals
      REAL*4        NU        ! real degrees of freedom
C
      INTEGER*4     DF        ! truncated degrees of freedom
C
      LOGICAL*1     TWOTAIL   ! true--> 2-tailed probability
      LOGICAL*1     RETURNP   ! true--> return P for given T
      CHARACTER*256 RCSHEADER
C
C  Compute power for 5 mean differences
      RCSHEADER = '$Id$'
C
      MEAN(1) = .02 * NORMVAL
      MEAN(2) = .03 * NORMVAL
      MEAN(3) = .05 * NORMVAL
      MEAN(4) = .10 * NORMVAL
      MEAN(5) = .15 * NORMVAL
C
C  Compute standard error of difference and degrees of freedom
C
      SDIFF = SQRT((VAR1/FLOAT(N1)) + (VAR2/FLOAT(NT)))
C
      SEMSQ1 = VAR1/FLOAT(N1)
      SEMSQ2 = VAR2/FLOAT(NT)
      TEMP1 = (SEMSQ1**2)/FLOAT(N1+1)
      TEMP2 = (SEMSQ2**2)/FLOAT(NT+1)
      NU = ((SEMSQ1+SEMSQ2)**2)/(TEMP1+TEMP2) - 2.0
      DF = INT(NU)
C
C  Get critical value of T for 1- and 2-tailed tests
C
      RETURNP = .FALSE.
      TWOTAIL = .FALSE.
      CALL TTABLE(T1,ALPHA,DF,TWOTAIL,RETURNP)
      TWOTAIL = .TRUE.
      CALL TTABLE(T2,ALPHA,DF,TWOTAIL,RETURNP)
C
      DO 100 I=1,5
C
C  Compute standard error of difference, noncentrality parameter,
C
        DELTA = ABS(MEAN(I)/SDIFF)
C
C  Compute minimum Z
C
        Z1 = (T1-DELTA)/SQRT(1. + (T1**2/(2.*NU)))
        Z2 = (T2-DELTA)/SQRT(1. + (T2**2/(2.*NU)))
C
C  Get probability (beta) of Z <= Z1, Z2
C
        RETURNP = .TRUE.
        CALL ZTABLE(Z1,BETA1,RETURNP)
        POWER1(I) = 1.-BETA1
        CALL ZTABLE(Z2,BETA2,RETURNP)
        POWER2(I) = 1.-BETA2
C
100   CONTINUE
C
      WRITE(USEROUT,8000) N1,NT,DF,ALPHA,(POWER1(I),POWER2(I),I=1,5)
      WRITE(LOGOUT,8000)  N1,NT,DF,ALPHA,(POWER1(I),POWER2(I),I=1,5)
C
CN1 N  dF    p         2%          3%          5%          10%         15%
C10 20  1  0.0500   1.00 1.00   1.00 1.00   1.00 1.00   1.00 1.00   1.00 1.00
C
8000  FORMAT(1X,I3,I3,I3,F8.4,5(2X,2F5.2))
C
      RETURN
      END
@
