#!/bin/csh
#	Module:	t88mean
#	Date:	May-96
#	Author:	Tom Videen
#	Use:	Create a stereotaxic-reformatted image to Talairach 1988 atlas brain
#			Uses parameter and imagelist files

set BINPATH = $HOME/src/t88mean
set PROGRAM = "t88mean"

if ($#argv != 3) then
	echo "Usage: $PROGRAM prm list image"
	echo " i.e.: $PROGRAM my.prm my.lst output.smg"
	exit
endif

set prm = $1
set lst = $2
set smg = $3
set rec = ${smg}.rec
set now = `date`
set today = `date '+%m/%d/%y'`
set uid = `whoami`

if (!(-e ${prm})) then
	echo $PROGRAM": parameter file ${prm} does not exist"
	exit
endif

if (!(-e ${lst})) then
	echo $PROGRAM": imagelist file ${lst} does not exist"
	exit
endif

if (-e ${rec}) then
	rm $rec
endif
echo "rec ${smg} ${now} ${uid}" > ${rec}
echo "$PROGRAM $1 $2 $3" >> ${rec}
ident $BINPATH/t88mean.bin | grep $PROGRAM.c >> ${rec}
echo "prm" >> ${rec}
cat ${prm} >> ${rec}
echo "endprm" >> ${rec}
echo "imglst" >> ${rec}
cat ${lst} >> ${rec}
echo "endimglst" >> ${rec}

$BINPATH/t88mean.bin ${prm} ${lst} ${smg} ${rec} ${now} ${today} ${uid}
#echo ${prm} ${lst} ${smg} ${rec} ${now} ${today} ${uid}

set endstat = $status
if ($endstat == 0) then
	echo "endrec" >> ${rec}
else if ($endstat == 3) then
	rm $rec
else
	echo "End of Task Status = $endstat"
endif
